#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазнаяНоменклатура		= Параметры.РазнаяНоменклатура;
	ЭтоКомандаНаРазгрузку	= Параметры.ЭтоКомандаНаРазгрузку;
	ВыбранныеРегистрации = Новый Структура("МассивРегистраций", Параметры.МассивРегистраций);
	
	УстановитьВидимостьДоступностьЭлементовФормы();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспНоменклатураПриИзменении(Элемент)
	
	Если ИспНоменклатура Тогда
		Если РазнаяНоменклатура Тогда
			ТекстСообщения = НСтр("ru='В выбранных строках разная номенклатура! Не возможно изменить.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ИспНоменклатура = Ложь;
		Иначе
			Элементы.Номенклатура.ТолькоПросмотр = Ложь;
		КонецЕсли;
	Иначе
		Элементы.Номенклатура.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьКачествоПриИзменении(Элемент)
	
	Элементы.ПринятьКачествоКомментарий.Доступность = ПринятьКачество;

КонецПроцедуры

&НаКлиенте
Процедура СлужебнаяНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	ЭталоныКачества = ПолучитьВидНоменклатурыЭталоныКачества();

	Если ЗначениеЗаполнено(ЭталоныКачества) Тогда
		СтандартнаяОбработка = Ложь;

		ПарамерыВыбораМассив = Новый Массив;
		ПарамерыВыбораМассив.Добавить(Новый ПараметрВыбора("Отбор.ВидНоменклатуры", ЭталоныКачества));
		ПарамерыВыбораМассив.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
		ПарамерыВыбораМассив.Добавить(Новый ПараметрВыбора("Отбор.ЭтоГруппа", Ложь));
		Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПарамерыВыбораМассив);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СлужебнаяНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	
	СтруктураОтбора = ПолучитьОтборНоменклатурыЭталоныКачества();
	Если СтруктураОтбора <> Неопределено И СтруктураОтбора.Количество() Тогда
		ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбора);
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыОткрытия, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СлужебнаяНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением,
	СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;

	Если ПроверитьВыборНоменклатурыЭталоныКачества(ВыбранноеЗначение) Тогда
		СлужебнаяНоменклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
		ТекстСообщения = НСтр("ru = 'У выбранного элемента справочника ""Номенклатура""'") + НСтр(
			"ru = ' вид номенклатуры должен быть ""Эталоны качества""'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	СлужебнаяНоменклатура = ВыбранноеЗначение;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбработатьДанные(Команда)
	
	Если ЭтоКомандаНаРазгрузку Тогда
		
	Если ПринятьКачество И НЕ ЗначениеЗаполнено(ПринятьКачествоКомментарий) Тогда
		ТекстСообщения = НСтр("ru = 'При принятии качества необходимо заполнить комментарий'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПринятьКачествоКомментарий");
		Возврат;
	КонецЕсли; 
			
		ПараметрыНаРазгрузку = Новый Структура;
		ПараметрыНаРазгрузку.Вставить("ПринятьКачество", 		ПринятьКачество);
		ПараметрыНаРазгрузку.Вставить("Комментарий", 			ПринятьКачествоКомментарий);
		ПараметрыНаРазгрузку.Вставить("Склад", 					Силос);
		ПараметрыНаРазгрузку.Вставить("ЯмаРазгрузки", 			МестоРазгрузки);
		ПараметрыНаРазгрузку.Вставить("СлужебнаяНоменклатура", 	СлужебнаяНоменклатура);
		
		Закрыть(ПараметрыНаРазгрузку);
	Иначе
		ОбработатьДанныеНаСервере();
		
		Если ЕстьИзмененные Тогда
			Оповестить("ГрупповаяОбработкаРегистраций");
		КонецЕсли;
		
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыход(Команда)
	
	Закрыть();

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбработатьДанныеНаСервере()
	
	ЕстьИзмененные = Ложь;
	ИзменятьНоменклатуру 	= ИспНоменклатура И ЗначениеЗаполнено(Номенклатура);
	
	Если ИзменятьНоменклатуру Или ИспКомментарий Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("ВыбранныеРегистрации", 	ВыбранныеРегистрации.МассивРегистраций);
		
		Если ИзменятьНоменклатуру Тогда
			ПараметрыЗаполнения.Вставить("Номенклатура", 		Номенклатура);
		КонецЕсли;
		Если ИспКомментарий Тогда
			ПараметрыЗаполнения.Вставить("Комментарий", 		Комментарий);
		КонецЕсли;
		
		Документы.гкс_РегистрацияНаПЛК.ГрупповоеИзменениеРегистрацийИзАРМ(ПараметрыЗаполнения);
		
		ЕстьИзмененные = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступностьЭлементовФормы()
	
	Элементы.ИспНоменклатура.Видимость				= Не ЭтоКомандаНаРазгрузку;
	Элементы.ИспКомментарий.Видимость				= Не ЭтоКомандаНаРазгрузку;
	Элементы.Номенклатура.Видимость					= Не ЭтоКомандаНаРазгрузку;
	Элементы.Комментарий.Видимость					= Не ЭтоКомандаНаРазгрузку;
	Элементы.ПринятьКачество.Видимость				= ЭтоКомандаНаРазгрузку;
	Элементы.ПринятьКачествоКомментарий.Видимость	= ЭтоКомандаНаРазгрузку;
	Элементы.Силос.Видимость						= ЭтоКомандаНаРазгрузку;
	Элементы.МестоРазгрузки.Видимость				= ЭтоКомандаНаРазгрузку;
	Элементы.СлужебнаяНоменклатура.Видимость		= ЭтоКомандаНаРазгрузку;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОтборНоменклатурыЭталоныКачества()
	
	СтруктураОтбора = Новый Структура;
	ЭталоныКачества = ПолучитьВидНоменклатурыЭталоныКачества();
	
	Если ЗначениеЗаполнено(ЭталоныКачества) Тогда
		СтруктураОтбора.Вставить("ВидНоменклатуры", ЭталоныКачества);
		СтруктураОтбора.Вставить("ПометкаУдаления", Ложь);
		СтруктураОтбора.Вставить("ЭтоГруппа", Ложь);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не найден вид номенклатуры ""Эталоны качества"".'")
			+ НСтр("ru = ' Отбор не применен.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат СтруктураОтбора;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьВыборНоменклатурыЭталоныКачества(ВыбранноеЗначение)
	
	ЭталоныКачества = ПолучитьВидНоменклатурыЭталоныКачества();
	Если Не ЗначениеЗаполнено(ЭталоныКачества) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитВидНоменклатуры = "ВидНоменклатуры";
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, РеквизитВидНоменклатуры);
	Возврат ВидНоменклатуры <> ЭталоныКачества;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьВидНоменклатурыЭталоныКачества()
	
	Возврат гкс_ОбщегоНазначенияВызовСервера.ПроизвольныйПредопределенныйОбъект("ВидыНоменклатурыЭталоныКачества");
	
КонецФункции


#КонецОбласти