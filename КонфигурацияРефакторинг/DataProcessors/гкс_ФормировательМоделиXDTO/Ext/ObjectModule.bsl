#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ПолучитьМодельXDTOВСтроке(ИменаПространствXDTO) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИменаПространствXDTO) Тогда
		Возврат "";
	КонецЕсли;
	
	МодельXDTO = ФабрикаXDTO.ЭкспортМоделиXDTO(ИменаПространствXDTO);
	
	ПисательXML = Новый ЗаписьXML;
	ПисательXML.УстановитьСтроку("utf-8");
	ПисательXML.ЗаписатьОбъявлениеXML();
	
	ФабрикаXDTO.ЗаписатьXML(ПисательXML, 
	                        МодельXDTO, 
							"Model", 
							"http://v8.1c.ru/8.1/xdto", 
							ФормаXML.Элемент, 
							НазначениеТипаXML.Явное);
	
	Возврат ПисательXML.Закрыть();
	
КонецФункции

Функция СохранитьМодельВСправочникНаСервере(Знач ОписаниеМодели) Экспорт
	
	МенеджерМоделей = МенеджерМоделей();
	Если МенеджерМоделей = Неопределено Тогда
		ВызватьИсключение НСтр("ru='В конфигурации не существует менеджера моделей - справочника ""Модели данных XDTO""'");
	КонецЕсли;
	
	Если ОписаниеМодели.Свойство("Содержимое") И ТипЗнч(ОписаниеМодели["Содержимое"]) = Тип("Строка") Тогда
		ОписаниеМодели["Содержимое"] = Новый ХранилищеЗначения(ОписаниеМодели["Содержимое"], Новый СжатиеДанных(9));	
	КонецЕсли;
		
	ОбъектМодель = МенеджерМоделей.СоздатьЭлемент();
	ОбъектМодель.Заполнить(ОписаниеМодели);
	
	ОбъектМодель.Записать();
	
	Возврат ОбъектМодель.Ссылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МенеджерМоделей() Экспорт
	
	Если Метаданные.Справочники.Найти("гкс_МоделиДанныхXDTO") = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Справочники["гкс_МоделиДанныхXDTO"];
	
КонецФункции

#КонецОбласти

#Иначе	
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте'");
#КонецЕсли
