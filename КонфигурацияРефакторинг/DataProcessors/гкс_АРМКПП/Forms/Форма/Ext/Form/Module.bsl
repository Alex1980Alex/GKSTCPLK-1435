
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаРегистрацииТранспорта.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДатаСеанса());
	ТаблицаРегистрацииТранспорта.Параметры.УстановитьЗначениеПараметра("ПоказыватьОтмененные", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("АвтообновлениеТаблицыФормы", 60, Ложь);

КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьОтмененныеПриИзменении(Элемент)
	
	ТаблицаРегистрацииТранспорта.Параметры.УстановитьЗначениеПараметра("ПоказыватьОтмененные", ПоказыватьОтмененные);
	АвтообновлениеТаблицыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЗаписьКПП(Команда)

	Оповещение = Новый ОписаниеОповещения("СоздатьЗаписьКППЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Будет создан новый пустой документ Въезд через КПП. Продолжить?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
					
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаписьКППНаОснованииОчереди(Команда)
	
	Оповещение = Новый ОписаниеОповещения("СоздатьЗаписьКППНаОснованииОчередиЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Будет открыт список очередей на приемку для создания 
						|документа Въезд через КПП. Продолжить?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьДокумент(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтменитьДокументЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Пометить на удаление указанный документ Въезд на КПП-1?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура Выезд(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ВыездЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Подтвердить Выезд на КПП-1?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьКомандуВыездНаСервере(ТекущиеДанные)
	
	Документы.гкс_ВыездНаКПП.СоздатьДокументВыездНаКПП(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуОтменитьДокументСервер(ТекущиеДанные)
	
	Обработки.гкс_АРМКПП.ПометитьНаУдалениеДокументВъездНаКПП(ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтообновлениеТаблицыФормы()
	
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	ТаблицаРегистрацииТранспорта.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДата);
	Элементы.ТаблицаРегистрацииТранспорта.Обновить();
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаписьКППЗавершение(Результат, ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Документ.гкс_ВъездНаКПП.Форма.ФормаДокумента");
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗаписьКППНаОснованииОчередиЗавершение(Результат, ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Обработка.гкс_АРМКПП.Форма.ФормаПодбораОчереди");
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьДокументЗавершение(Результат, ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекущиеДанные = Элементы.ТаблицаРегистрацииТранспорта.ТекущиеДанные;
		ВыполнитьКомандуОтменитьДокументСервер(ТекущиеДанные);
		АвтообновлениеТаблицыФормы();
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ВыездЗавершение(Результат, ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекущиеДанные = Элементы.ТаблицаРегистрацииТранспорта.ТекущиеДанные;
		ВыполнитьКомандуВыездНаСервере(ТекущиеДанные);
		АвтообновлениеТаблицыФормы();	
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти