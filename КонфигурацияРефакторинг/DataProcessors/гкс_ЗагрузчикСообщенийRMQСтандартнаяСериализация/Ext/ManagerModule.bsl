#Если ТолстыйКлиентОбычноеПриложение Или Сервер Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Выполняет чтение сообщения обмена и его последующую загрузку. Обрабатывает форматы, представляющие стандартную 
// сериализацию платформы 1С
// См. также Обработки.гкс_ЗагрузчикСообщенийRMQ.ПрочитатьСообщение
// 
// Параметры:
// 	Сообщение - Строка - текст сообщения
//	ПараметрыОбработки - Структура - структура, содержащая входящие вспомогательные данные для обработки. После 
//	обработки сообщения в нее помещается результат. Оставлено для совместимости.
Функция ПрочитатьСообщение(Сообщение, ПараметрыОбработки) Экспорт
	
	Попытка
	
		Загрузчик = ПолучитьЭкземплярДляЗагрузки(ПараметрыОбработки);
		Загрузчик.УстановитьСообщение(Сообщение);
		Загрузчик.Прочитать();
		Загрузчик.Загрузить();
	
		Результат = Загрузчик.Результат();
	
	Исключение
		
		Результат = Загрузчик.Результат();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		гкс_ОбменДаннымиСервер.ЗафиксироватьПроблемыОбработкиСообщения(ПараметрыОбработки, 
		                                                             Результат, 
		                                                             НСтр("ru = 'Ошибка при загрузке данных: %1'"), 
		                                                             ИнформацияОбОшибке);
		ВызватьИсключение КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		 
	КонецПопытки;
	
	Если Результат.Свойство("ЗагруженныеОбъекты") И ЗначениеЗаполнено(Результат.ЗагруженныеОбъекты) Тогда
		ПараметрыОбработки.Вставить("ЗагруженныеОбъекты", Результат.ЗагруженныеОбъекты);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ПолучитьЭкземплярДляЗагрузки(Знач ПараметрыОбработки = Неопределено)
	
	Перем Параметры;
	Перем ИсключаемыеПолучатели; 
	
	Экземпляр = Создать();
	
	Если НЕ ЗначениеЗаполнено(ПараметрыОбработки) Тогда
		Возврат Экземпляр
	КонецЕсли;
	
	ПараметрыОбработки.Свойство("Параметры", Параметры);   
	ПараметрыОбработки.Свойство("ИсключаемыеПолучатели", ИсключаемыеПолучатели);
	
	Если ЗначениеЗаполнено(Параметры) Тогда
		Экземпляр.УстановитьФормат(Параметры.Формат);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИсключаемыеПолучатели) Тогда
		Экземпляр.УстановитьИсключаемыхПолучателей(ИсключаемыеПолучатели);	
	КонецЕсли;

	Возврат Экземпляр;
	
КонецФункции

#КонецОбласти

#КонецЕсли