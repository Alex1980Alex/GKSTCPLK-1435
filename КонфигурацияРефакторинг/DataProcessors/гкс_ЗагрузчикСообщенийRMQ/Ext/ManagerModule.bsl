#Если ТолстыйКлиентОбычноеПриложение Или Сервер Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выполняет чтение сообщения обмена и его последующую загрузку
//
// Параметры:
// 	Содержимое - Структура - структура, хранящая в себе данные сообщения
//		* Текст - Строка - текст сообщения
//		* СвойстваСообщения - Структура - свойства сообщения. См. ОчередьСообщенийПовтИсп.ВозможныеСвойстваСообщения
//	РезультатыОбработки - Структура - структура, содержащая входящие вспомогательные данные для обработки и результаты
//
Процедура ПрочитатьСообщение(Содержимое, РезультатыОбработки) Экспорт

	Перем ТекстСообщения;
	Перем Свойства;
	Перем Менеджер;

	Если Не ТипЗнч(Содержимое) = Тип("Структура") Тогда
		ТекстИсключения = НСтр("ru='Некорректно сформированное сообщение. Неподдерживаемый формат. 
							   |Обработка прервана'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;

	Содержимое.Свойство("Текст", ТекстСообщения);
	Содержимое.Свойство("СвойстваСообщения", Свойства);

	ПараметрыОбработкиСообщения = 
	гкс_ОчередьСообщенийRMQСлужебный.ПараметрыОбработкиСообщенияИзСтруктурыСвойств(СвойстваБезЗаголовков(Свойства));             
	
	ПроверитьВозможностьОбработкиСообщений(ПараметрыОбработкиСообщения);

	ДополнитьРезультатыОбработкиПараметрами(РезультатыОбработки, ПараметрыОбработкиСообщения);

	Менеджер = гкс_ОбщегоНазначения.ОбщийМодуль(ИмяМенеджераОбработкиСообщенияИзПараметров(РезультатыОбработки));

	Попытка
		Менеджер.ПрочитатьСообщение(ТекстСообщения, РезультатыОбработки);
	Исключение
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
		
	//@skip-warning
	Если РезультатыОбработки.Свойство("ЕстьОшибки") И РезультатыОбработки["ЕстьОшибки"] = Истина Тогда
		ВызватьИсключение "Ошибка обработки сообщения обмена";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СвойстваБезЗаголовков(Свойства)
	
	КопияСвойств = гкс_ОбщегоНазначения.СкопироватьРекурсивно(Свойства);
	// Заголовки в структуре как соотвествие, нельзя передать в повт. исп.
	// сейчас вообще передача всех свойств избыточна - требуются только 2
	// сделано с заделом на будущее. Для передачи "Заголовоков"  необходимо будет написать свою конвертацию
	КопияСвойств.Удалить("Заголовки"); 
	
	Возврат  КопияСвойств;
	
КонецФункции

Процедура ПроверитьВозможностьОбработкиСообщений(ПараметрыОбработкиСообщения)

	ТекстИсключенияМассив = Новый Массив;

	Если Не ЗначениеЗаполнено(ПараметрыОбработкиСообщения["Формат"]) Тогда
		ТекстИсключенияМассив.Добавить(НСтр("ru='Не удается отпределить формат сообщения для обработки'"));
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ПараметрыОбработкиСообщения["МенеджерОбработки"]) Тогда
		ТекстИсключенияМассив.Добавить(
		гкс_СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru='Не указан менеджер обработки для формата: %1'"), Строка(ПараметрыОбработкиСообщения["Формат"])));
	КонецЕсли;

	Если ТекстИсключенияМассив.Количество() > 0 Тогда

		ТекстИсключения = СтрСоединить(ТекстИсключенияМассив, Символы.ПС);
		ВызватьИсключение ТекстИсключения;

	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьРезультатыОбработкиПараметрами(РезультатыОбработки, ПараметрыОбработкиСообщения)

	РезультатыОбработки.Вставить("Параметры", Новый Структура(ПараметрыОбработкиСообщения));

КонецПроцедуры

Функция ИмяМенеджераОбработкиСообщенияИзПараметров(РезультатыОбработки)

	//@skip-warning
	Возврат РезультатыОбработки["Параметры"]["МенеджерОбработки"];

КонецФункции

Процедура ЗаполнитьПараметрыОшибки(РезультатыОбработки, ТипПроблемы, ТекстОшибки, ПредставлениеОбъекта) Экспорт

	гкс_ОчередьСообщенийRMQ.УстановитьЗначениеИзСтруктурыПоКлючу(ТекстОшибки, РезультатыОбработки, "ТекстОшибки");
	гкс_ОчередьСообщенийRMQ.УстановитьЗначениеИзСтруктурыПоКлючу(ТипПроблемы, РезультатыОбработки, "ТипПроблемы");
	гкс_ОчередьСообщенийRMQ.УстановитьЗначениеИзСтруктурыПоКлючу(ПредставлениеОбъекта, РезультатыОбработки,
		"ПредставлениеОбъекта");

КонецПроцедуры

#КонецОбласти

#КонецЕсли