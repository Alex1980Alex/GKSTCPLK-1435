
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьФорму();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	ТипТочкиОбмена = ?(ЗначениеЗаполнено(ТочкаОбмена),
	                  гкс_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТочкаОбмена, "ТипТочкиОбмена"),
					  Неопределено);
					  
	Если ТипТочкиОбмена = Перечисления.гкс_ТипыТочекОбменаRMQ.Fanout Тогда
		НепроверяемыеРеквизиты.Добавить("КлючМаршрутизации");		 
	КонецЕсли;
	
	гкс_ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если СтрНайти(ИмяСобытия, "ИзменениеНастроекФорматаОбмена") > 0 Тогда
		ПриИзмененииФорматаСервер();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФорматПриИзменении(Элемент)
	ПриИзмененииФорматаСервер();
КонецПроцедуры

&НаКлиенте
Процедура ТочкаОбменаПриИзменении(Элемент)
	ТочкаОбменаПриИзмененииСервер();
КонецПроцедуры

#КонецОбласти
	
#Область ОбработчикиКоманд

&НаКлиенте
Процедура Распространить(Команда)
	
	РаспространитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьФорму()
	
	ПриИзмененииФорматаСервер();
	ТочкаОбменаПриИзмененииСервер();
	
КонецПроцедуры

#Область ИзменениеФормата

&НаСервере
Процедура ТочкаОбменаПриИзмененииСервер()
	
	ПоказатьГруппуНастройкисоединения = Ложь;
	
	Если ЗначениеЗаполнено(ТочкаОбмена) Тогда
		СформироватьОписаниеСвойствТочкиОбмена();
		ПоказатьГруппуНастройкисоединения = Истина;
	Иначе
		ОчиститьГруппуНастроекСоединения();
	КонецЕсли;
	
	Элементы.ГруппаНастройкиСоединения.Видимость = ПоказатьГруппуНастройкисоединения;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьГруппуНастроекСоединения()
	
	ОписаниеПорт = Неопределено;
	ОписаниеСервер = Неопределено;
	ОписаниеВиртуальныйХост = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОписаниеСвойствТочкиОбмена()

	РеквизитыТочкиОбмена = РеквизитыТочкиОбмена();
	Если НЕ ЗначениеЗаполнено(РеквизитыТочкиОбмена) Тогда
		
		ТекстПредставлениеСервера = "";
		ТекстПредставлениеПорт = "";
		ТекстПредставлениеВиртуальныйХост = "";
	
		ЦветФона = WebЦвета.СветлоРозовый;
		
	Иначе
		
		ТекстПредставлениеСервера = РеквизитыТочкиОбмена.ПредставлениеСервера;
		ТекстПредставлениеПорт = Формат(РеквизитыТочкиОбмена.Порт, "ЧГ=0;");
		ТекстПредставлениеВиртуальныйХост = РеквизитыТочкиОбмена.ВиртуальныйХост;
		
		ЦветФона = WebЦвета.СветлоЖелтый;
		
	КонецЕсли;
	
	ОписаниеПорт = Новый ФорматированнаяСтрока(ТекстПредставлениеПорт, 
	                                           Неопределено, 
	                                           Неопределено, 
	                                           ЦветФона);
	
	ОписаниеСервер = Новый ФорматированнаяСтрока(ТекстПредставлениеСервера, 
		                                         Неопределено, 
		                                         Неопределено, 
		                                         ЦветФона);
	                                           
	ОписаниеВиртуальныйХост = Новый ФорматированнаяСтрока(ТекстПредставлениеВиртуальныйХост, 
				                                          Неопределено, 
				                                          Неопределено, 
				                                          ЦветФона);
	
КонецПроцедуры

&НаСервере
Функция РеквизитыТочкиОбмена()
	
	Запрос = Новый Запрос;
	Запрос.Текст  = ТекстЗапросаРеквизитыТочкиОбмена();
	Запрос.УстановитьПараметр("ТочкаОбмена", ТочкаОбмена);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат гкс_ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(РезультатЗапроса.Выгрузить()[0]);
	
КонецФункции

#КонецОбласти

#Область РаспространениеФормата

&НаСервере
Процедура РаспространитьНаСервере()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитФормыВЗначение("Объект").РаспространитьФормат(ОписаниеФормата());
		
КонецПроцедуры

&НаСервере
Функция ОписаниеФормата()
	
	ОписаниеФормата = Новый Структура;
	ОписаниеФормата.Вставить("Формат", Формат);
	ОписаниеФормата.Вставить("ТочкаОбмена", ТочкаОбмена);
	ОписаниеФормата.Вставить("ВыгружатьМодель", ВыгружатьМодель);
	ОписаниеФормата.Вставить("АктивностьФормата", АктивностьФормата);
	ОписаниеФормата.Вставить("КлючМаршрутизации", КлючМаршрутизации);
	ОписаниеФормата.Вставить("ВыгружатьОбщиеНастройкиФормата", ВыгружатьОбщиеНастройки);
	ОписаниеФормата.Вставить("ВыгружатьНастройкиФорматаОбменаED", ВыгружатьНастройкиФормата);
	
	Возврат ОписаниеФормата;
	
КонецФункции

#КонецОбласти

#Область ИзменениеФормата

&НаСервере
Процедура ПриИзмененииФорматаСервер()
	
	ПоказатьГруппуВыгрузкиНастроек = Ложь;
	
	Если ЗначениеЗаполнено(Формат) Тогда
		СформироватьОписанияНастроек();
		ПоказатьГруппуВыгрузкиНастроек = Истина;
	Иначе
		ОчиститьГруппуНастроекВыгрузки();
	КонецЕсли;
	
	Элементы.ГруппаВыгрузкаНастроек.Видимость = ПоказатьГруппуВыгрузкиНастроек;
		
КонецПроцедуры

&НаСервере
Процедура СформироватьОписанияНастроек()
	
	ОбщиеНастройки = РегистрыСведений.гкс_НастройкиФорматовОбменаОбщие.ПолучитьНастройкиФормата(Формат);
	СформироватьОписаниеОбщихНастроек(ОбщиеНастройки);
	
	НастройкиОбменаУниверсальныйФормат = РегистрыСведений.гкс_НастройкиФорматовОбменаED.ПолучитьНастройки(Формат);
	СформироватьОписаниеНастроекФормата(НастройкиОбменаУниверсальныйФормат);
		
КонецПроцедуры

&НаСервере
Процедура ОчиститьГруппуНастроекВыгрузки()
	
	ВыгружатьОбщиеНастройки = Ложь;
	ВыгружатьНастройкиФормата = Ложь;
	ВыгружатьМодель = Ложь;
	
	ОписаниеВыгружатьОбщиеНастройки = Неопределено;
	ОписаниеВыгружатьНастройкиФормата = Неопределено;
	ОписаниеВыгружатьМодель = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОписаниеНастроекФормата(Настройки)
	
	Если НЕ ЗначениеЗаполнено(Настройки) Тогда
		
		ТекстОписаниеВыгружатьНастройкиФормата = НСтр("ru='Настройки формата ED не заданы'");
		ТекстОписаниеВыгружатьМодель = НСтр("ru='Модель не задана'");
		ЦветФона = WebЦвета.СветлоРозовый;
		
	Иначе
	
		ШаблонОписанияНастройкиФормата = НСтр("ru='Менеджер загрузки: %1; Менеджер выгрузки: %2'");
		ТекстОписаниеВыгружатьНастройкиФормата = 
		гкс_СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияНастройкиФормата, 
		                                                            Настройки["МенеджерЗагрузки"],
		                                                            Настройки["МенеджерВыгрузки"]); 
																	
		ТекстОписаниеВыгружатьМодель = Строка(Настройки["МодельДанных"]); 
		ЦветФона = WebЦвета.СветлоЖелтый;    
		             			                                                            		
	КонецЕсли;
	
	ОписаниеВыгружатьНастройкиФормата = Новый ФорматированнаяСтрока(ТекстОписаниеВыгружатьНастройкиФормата, 
															        Неопределено, 
																    Неопределено,
															        ЦветФона);
	ОписаниеВыгружатьМодель = Новый ФорматированнаяСтрока(ТекстОписаниеВыгружатьМодель, 
	                                                      Неопределено, 
	                                                      Неопределено, 
	                                                      ЦветФона);
		
КонецПроцедуры

&НаСервере
Процедура СформироватьОписаниеОбщихНастроек(Настройки)
	
	Если НЕ ЗначениеЗаполнено(Настройки) Тогда
		ТекстОписанияОбщиеНастройки = НСтр("ru='Общие настройки не заданы'");
		ЦветФона = WebЦвета.СветлоРозовый;
	Иначе
	
		ШаблонОписанияОбщиеНастройки = НСтр("ru='Менеджер формирования: %1; Менеджер обработки: %2'");
		ТекстОписанияОбщиеНастройки = 
		гкс_СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОбщиеНастройки, 
		                                                            Настройки["МенеджерФормирования"],
		                                                            Настройки["МенеджерОбработки"]); 
		ЦветФона = WebЦвета.СветлоЖелтый;
	КонецЕсли;
	
	ОписаниеВыгружатьОбщиеНастройки = Новый ФорматированнаяСтрока(ТекстОписанияОбщиеНастройки, 
															      Неопределено, 
																  Неопределено,
															      ЦветФона);

КонецПроцедуры

#КонецОбласти

#Область ТекстыЗапросов

&НаСервере
Функция ТекстЗапросаРеквизитыТочкиОбмена()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СервераОчередей.ВиртуальныйХост КАК ВиртуальныйХост,
	|	СервераОчередей.Порт КАК Порт,
	|	СервераОчередей.Код КАК ПредставлениеСервера
	|ИЗ
	|	Справочник.гкс_ТочкиОбменаRMQ КАК ВсеТочкиОбмена
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СерверыОчередейRMQ КАК СервераОчередей
	|		ПО ВсеТочкиОбмена.Владелец = СервераОчередей.Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
