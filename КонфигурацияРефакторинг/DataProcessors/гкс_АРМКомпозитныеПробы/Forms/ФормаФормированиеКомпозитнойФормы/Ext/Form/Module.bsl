#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СформироватьКомпозитнуюПробу") Тогда
		
		ЗаполнитьЗначенияСвойств(Объект, Параметры.ДопПараметры); 
		СтанцияОтправленияПредставление = Параметры.ДопПараметры.СтанцияОтправленияПредставление;
		ТабРегистраций = гкс_ПриемкаНаПЛКСервер.ПолучитьСписокРегистрацийВКомпозит(Параметры.ДопПараметры);
		
		Если ТабРегистраций.Количество() > 0 Тогда
			Объект.ТаблицаРегистраций.Загрузить(ТабРегистраций);
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокРегистраций

&НаКлиенте
Процедура СписокРегистрацийДокументРегистрацииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокРегистраций.ТекущиеДанные;
	ТекущиеДанные.ВесНетто = ПолучитьВесНетто(ТекущиеДанные.ДокументРегистрации);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСформироватьКомпозитнуюПробу(Команда)
	
	СформироватьДокумент();
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьДокумент()
	
	Если Объект.ТаблицаРегистраций.Количество() > 0 Тогда
		
		ДокументОбъект = Документы.гкс_ФормированиеНомераПробы.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.ТочкаМаршрута = Объект.ТочкаМаршрута;
		ДокументОбъект.ТипПробы = Перечисления.гкс_ТипыПроб.Композитная;
		ДокументОбъект.ПроизводственныеСуткиРегистрации = Объект.ПроизводственныеСуткиРегистрации;
		
		Для Каждого Строка Из Объект.ТаблицаРегистраций Цикл
			НоваяРегистрация = ДокументОбъект.СписокРегистраций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяРегистрация, Строка);
		КонецЦикла;
		
		Отказ = Ложь;
		Документы.гкс_ФормированиеНомераПробы.СформироватьНомерПробы(ДокументОбъект, Отказ);
		ЧасовойПоясПЛК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ДокументОбъект.ТочкаМаршрута, "гкс_ЧасовойПоясТочкиМаршрута");
		
		Если ЗначениеЗаполнено(ЧасовойПоясПЛК) Тогда
			ДокументОбъект.МестнаяДата = МестноеВремя(ТекущаяУниверсальнаяДата(), ЧасовойПоясПЛК);
		Иначе
			ДокументОбъект.МестнаяДата = ДокументОбъект.Дата;
		КонецЕсли;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьВесНетто(ДокументРегистрации)
		
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументРегистрации, "ВесНетто");
	
КонецФункции

#КонецОбласти
