#Область ПрограммныйИнтерфейс

// Вызывает механизм отложенной обработки объектов. Используется регламентным заданием "гкс_ОтложеннаяОбработкаОбъектов"
Процедура ОтложеннаяОбработкаОбъектов() Экспорт
				
	Объекты = РегистрыСведений.гкс_ОтложеннаяОбработкаОбъектов.ОбъектыОтложеннойОбработки();	
	Пока Объекты.Следующий() Цикл
				
		НачатьТранзакцию();
		Попытка 
			ВыполнитьОтложеннуюОбработкуОбъекта(Объекты.Объект, 
			                                    Объекты.ДополнительныеСвойства.Получить(), 
			                                    Объекты.Действие);
			
			УдалитьСсылкуНаОбъектИзОтложеннойОбработки(Объекты.Объект);
			
			ЗафиксироватьТранзакцию();
		Исключение	
			ОтменитьТранзакцию();
			
			ТекОшибкаИнформация = ИнформацияОбОшибке();
			
			ОписаниеОшибки = Новый Массив;
			ОписаниеОшибки.Добавить(Объекты.СообщениеОбОшибкеОбработки);
			ОписаниеОшибки.Добавить(ПодробноеПредставлениеОшибки(ТекОшибкаИнформация));
			
			СтруктураЗаписи = СтруктураЗаписиОбъектаОтложеннойОбработки();
			ЗаполнитьЗначенияСвойств(СтруктураЗаписи, Объекты, "Объект, ДатаОбъекта, ДополнительныеСвойства, Действие");	
			СтруктураЗаписи.СообщениеОбОшибкеОбработки = СтрСоединить(ОписаниеОшибки, Символы.ПС);
			СтруктураЗаписи.КоличествоНеудачныхОбработок = Объекты.КоличествоНеудачныхОбработок + 1;
									
			ЗаписатьИнформациюОбОтложеннойОбработке(СтруктураЗаписи);
			
		КонецПопытки;
					
	КонецЦикла;
	
КонецПроцедуры

// Структура записи объекта отложенной обработки.
// 
// Возвращаемое значение:
//  Структура - Структура записи объекта отложенной обработки:
// * ДатаОбъекта - Дата -
// * Объект - Неопределено, ДокументСсылка -
// * КоличествоНеудачныхОбработок - Число -
// * СообщениеОбОшибкеОбработки - Строка -
// * Действие - ПеречислениеСсылка.гкс_ДействияОтложеннойОбработки -
// * ДополнительныеСвойства - Неопределено, Произвольный -
Функция СтруктураЗаписиОбъектаОтложеннойОбработки() Экспорт
	Возврат РегистрыСведений.гкс_ОтложеннаяОбработкаОбъектов.СтруктураЗаписи();
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьОтложеннуюОбработкуОбъекта(СсылкаНаОбъект, ДополнительныеСвойства, Действие) Экспорт
	РегистрыСведений
	    .гкс_ОтложеннаяОбработкаОбъектов
	    .ВыполнитьОтложеннуюОбработкуОбъекта(СсылкаНаОбъект, ДополнительныеСвойства, Действие);
КонецПроцедуры

// Записать информацию об отложенной обработке объекта.
// 
// Параметры:
//  ОписаниеОбъекта - см. гкс_ОтложеннаяОбработкаОбъектов.СтруктураЗаписиОбъектаОтложеннойОбработки
Процедура ЗаписатьИнформациюОбОтложеннойОбработке(ОписаниеОбъекта) Экспорт
	РегистрыСведений.гкс_ОтложеннаяОбработкаОбъектов.ЗаписатьИнформациюОбОтложеннойОбработке(ОписаниеОбъекта);
КонецПроцедуры

Процедура УдалитьСсылкуНаОбъектИзОтложеннойОбработки(СсылкаНаОбъект) Экспорт
	РегистрыСведений.гкс_ОтложеннаяОбработкаОбъектов.УдалитьСсылкуНаОбъектИзОтложеннойОбработки(СсылкаНаОбъект);
КонецПроцедуры

#КонецОбласти