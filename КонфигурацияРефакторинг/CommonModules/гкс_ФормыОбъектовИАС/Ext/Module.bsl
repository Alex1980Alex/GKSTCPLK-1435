#Область ПрограммныйИнтерфейс

Процедура ПриСозданииНаСервереФормыСпискаСообщений(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	УстановитьИменаКомандФормы(Форма);
	УсловноеОформлениеФормыСпискаСообщений(Форма);
	
КонецПроцедуры

Процедура ПриСозданииНаСервереФормыЭлементаСообщения(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	УстановитьИменаКомандФормы(Форма);
	
КонецПроцедуры

Процедура ПриСозданииНаСервереФормыСпискаСостоянийСообщений(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	ЭтоФормаСостоянийВходящихСообщений = Булево(СтрНайти(ИмяФормы,"СостоянияВходящих"));
	ЭтоФормаСостоянийИсходящихСообщений = Булево(СтрНайти(ИмяФормы,"СостоянияИсходящих"));

	Если ЭтоФормаСостоянийВходящихСообщений Тогда
		УсловноеОформлениеФормыСпискаВходящихСообщений(Форма);
	ИначеЕсли ЭтоФормаСостоянийИсходящихСообщений Тогда
		УсловноеОформлениеФормыСпискаИсходящихСообщений(Форма);
	КонецЕсли;
		
КонецПроцедуры

#Область УсловноеОформление

// Добавляет условное оформление фона полей строк динамического списка формы по 
// значению поля "Состояние"
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - 
// 	Состояние - ПеречислениеСсылка.гкс_СостоянияОчередиОтложенногоФормирования - описывает состояние сообщения
//  Цвет - WebЦвета, Цвет - 
//
Процедура ДобавитьУОСостояния(Форма, Состояние, Цвет) Экспорт
	
	ЭлементОформления = Форма.Список.УсловноеОформление.Элементы.Добавить();
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Состояние"); // имя поля
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно; // либо задаем свой
	ЭлементОтбора.ПравоеЗначение = Состояние;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Цвет);
	
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("Состояние");
	ПолеОформления.Использование = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьИменаКомандФормы(Форма)
	
	ВсеЭлементы = Форма.Элементы;
	ИмяФормы = Форма.ИмяФормы;
	ЭтоФормаВходящихСообщений = Булево(СтрНайти(ИмяФормы,"ВходящиеСообщенияRMQ"));
	ЭтоФормаИсходящихСообщений = Булево(СтрНайти(ИмяФормы,"ИсходящиеСообщения"));

	Если ЭтоФормаВходящихСообщений Тогда
		ИмяКнопкиВернутьВОчередь = НСтр("ru='Поставить в очередь обработки вх. сообщений'");
		ИмяКнопкиОбработатьБезОчереди = НСтр("ru='Обработать вх. сообщения'");	
	ИначеЕсли ЭтоФормаИсходящихСообщений Тогда
		ИмяКнопкиВернутьВОчередь = НСтр("ru='Поставить в очередь обработки исх. сообщений'");
		ИмяКнопкиОбработатьБезОчереди = НСтр("ru='Отправить в RabbitMQ'");
	Иначе
		Возврат;
	КонецЕсли;
	
	КнопкаОбработатьБезОчереди = ВсеЭлементы.Найти("ФормаОбщаяКомандагкс_ОбработатьБезОчереди");
	Если НЕ КнопкаОбработатьБезОчереди = Неопределено Тогда
		КнопкаОбработатьБезОчереди.Заголовок = ИмяКнопкиОбработатьБезОчереди;
	КонецЕсли;
	
	КнопкаВернутьвОчередь = ВсеЭлементы.Найти("ФормаОбщаяКомандагкс_ВернутьВОчередь");
	Если НЕ КнопкаВернутьвОчередь = Неопределено Тогда
		КнопкаВернутьвОчередь.Заголовок = ИмяКнопкиВернутьВОчередь;
	КонецЕсли;
	
КонецПроцедуры

#Область УсловноеОформление

Процедура УсловноеОформлениеФормыСпискаСообщений(Форма) 
	
	ИмяФормы = Форма.ИмяФормы;
	ЭтоФормаВходящихСообщений = Булево(СтрНайти(ИмяФормы,"ВходящиеСообщенияRMQ"));
	ЭтоФормаИсходящихСообщений = Булево(СтрНайти(ИмяФормы,"ИсходящиеСообщения"));
	
	Если ЭтоФормаИсходящихСообщений = ЭтоФормаВходящихСообщений Тогда
		Возврат;
	ИначеЕсли ЭтоФормаВходящихСообщений Тогда
		УсловноеОформлениеФормыСпискаВходящихСообщений(Форма);
	ИначеЕсли ЭтоФормаИсходящихСообщений Тогда
		УсловноеОформлениеФормыСпискаИсходящихСообщений(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура УсловноеОформлениеФормыСпискаВходящихСообщений(Форма)
	
	МенеджерСостояний = Перечисления.гкс_СостоянияВходящихСообщенийRMQ;
	
	ДобавитьУОСостояния(Форма, МенеджерСостояний.Зарегистрировано, WebЦвета.БледноЗолотистый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.ВОбработке, WebЦвета.СветлоЖелтый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.Обработано, WebЦвета.ГолубойСоСтальнымОттенком);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.ОшибкаОбработки, WebЦвета.Розовый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.Пропущено, WebЦвета.СеребристоСерый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.ОшибкаОбработкиЗакрыто, WebЦвета.РозовоКоричневый);
	
КонецПроцедуры

Процедура УсловноеОформлениеФормыСпискаИсходящихСообщений(Форма)
	
	МенеджерСостояний = Перечисления.гкс_СостоянияИсходящихСообщенийRMQ;
	
	ДобавитьУОСостояния(Форма, МенеджерСостояний.Подготовлено, WebЦвета.БледноЗолотистый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.Отправлено, WebЦвета.СветлоЖелтый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.Доставлено, WebЦвета.МятныйКрем);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.ДоставленоИОбработано, WebЦвета.ГолубойСоСтальнымОттенком);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.ОшибкаОбработкиПолучателем, WebЦвета.Розовый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.ОшибкаФормированияСостояния, WebЦвета.РозовоКоричневый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.Пропущено, WebЦвета.СеребристоСерый);
	ДобавитьУОСостояния(Форма, МенеджерСостояний.ОшибкаОбработкиЗакрыто, WebЦвета.РозовоКоричневый);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти