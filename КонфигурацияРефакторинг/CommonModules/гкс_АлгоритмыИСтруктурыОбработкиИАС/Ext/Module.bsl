#Область ПрограммныйИнтерфейс

Функция СформироватьГруппыОбработкиОбъектовИзОчереди(ДанныеОчереди) Экспорт
	
	Перем ЭлементовВГруппе, ИмяОчереди, Очередь;
	
	ГруппыОбработки = Новый Массив;
	
	ДанныеОчереди.Свойство("Очередь", Очередь);
	ДанныеОчереди.Свойство("ИмяОчереди", ИмяОчереди);
	ДанныеОчереди.Свойство("КоличествоПотоков", ЭлементовВГруппе);
		
	Если НЕ ЗначениеЗаполнено(ИмяОчереди) ИЛИ НЕ ТипЗнч(Очередь) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
		Возврат ГруппыОбработки;
	КонецЕсли;

	ПовторениеКлючейГруппы = Новый Соответствие;
	ТекущаяГруппа = НоваяГруппаОбработкиСообщений(ИмяОчереди);
	ИмяПоляКлюча = ИмяПоляКлючаПоИмениОчереди(ИмяОчереди);
	ИмяПоляОбъекта = ИмяПоляОбъектаПоИмениОчереди(ИмяОчереди);

	Пока Очередь.Следующий() Цикл
		
		КлючОбъекта = Очередь[ИмяПоляКлюча];
		Если ЕстьПовторениеПоКлючу(ПовторениеКлючейГруппы, КлючОбъекта) Тогда
			
			ДобавитьЭлементОчередиВГруппу(ТекущаяГруппа, Очередь, ИмяПоляКлюча, ИмяПоляОбъекта, Истина);		
			ДобавитьКлючВПовторение(КлючОбъекта, ПовторениеКлючейГруппы);
			Продолжить;
			
		КонецЕсли;
		
		Если ТекущаяГруппа.Количество() = ЭлементовВГруппе Тогда
			
			ГруппыОбработки.Добавить(ТекущаяГруппа);
			ТекущаяГруппа = НоваяГруппаОбработкиСообщений(ИмяОчереди);
			ПовторениеКлючейГруппы = Новый Соответствие;
			
		КонецЕсли;
		
		ДобавитьЭлементОчередиВГруппу(ТекущаяГруппа, Очередь, ИмяПоляКлюча, ИмяПоляОбъекта, Ложь);
		ДобавитьКлючВПовторение(КлючОбъекта, ПовторениеКлючейГруппы);

	КонецЦикла;
	
	Если НЕ ТекущаяГруппа.Количество() = 0 Тогда		
		ГруппыОбработки.Добавить(ТекущаяГруппа);	
	КонецЕсли;

	Возврат ГруппыОбработки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УпорядоченныеГруппыОбработкиСообщений

Процедура ДобавитьЭлементОчередиВГруппу(ГруппаОбработки, 
	                                    ЭлементОчереди, 
										ИмяПоляКлюча, 
										ИмяПоляОбъекта, 
										Повторяющийся)
	
	Если Повторяющийся Тогда
		
		СтрокаОбработки = ГруппаОбработки.Найти(ЭлементОчереди[ИмяПоляКлюча], ИмяПоляКлюча);
		Объект = СтрокаОбработки[ИмяПоляОбъекта];
		Если ТипЗнч(Объект) = Тип("Массив") Тогда
			Объект.Добавить(ЭлементОчереди[ИмяПоляОбъекта]);	
		Иначе
			
			МассивОбработки = Новый Массив;
			МассивОбработки.Добавить(Объект);
			МассивОбработки.Добавить(ЭлементОчереди[ИмяПоляОбъекта]);
			
			СтрокаОбработки[ИмяПоляОбъекта] = МассивОбработки; 

		КонецЕсли;
		
	Иначе
		
		СтрокаОбработки = ГруппаОбработки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаОбработки, ЭлементОчереди);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьКлючВПовторение(КлючОбъекта, ПовторениеКлючейГруппы)
	
	Повторение = ПовторениеКлючейГруппы.Получить(КлючОбъекта);	
	Если ЗначениеЗаполнено(Повторение) Тогда		
		ПовторениеКлючейГруппы[КлючОбъекта]	= Повторение + 1;		
	Иначе		
		ПовторениеКлючейГруппы.Вставить(КлючОбъекта, 1);		
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьПовторениеПоКлючу(ПовторениеКлючейГруппы, Ключ) 
	
	Если ЗначениеЗаполнено(Ключ) И 
		НЕ ПовторениеКлючейГруппы.Получить(Ключ) = Неопределено Тогда		
		Возврат Истина;		
	КонецЕсли;
		
	Возврат Ложь;
	
КонецФункции

Функция НоваяГруппаОбработкиСообщений(ИмяОчереди)
	
	Таблица = Новый ТаблицаЗначений;
	
	Если ИмяОчереди = "ОчередьОбработкиВходящихСообщений" Тогда
		
		Таблица.Колонки.Добавить("Порядок");
		Таблица.Колонки.Добавить("Сообщение");
		Таблица.Колонки.Добавить("КлючОбъекта");
		
	ИначеЕсли ИмяОчереди = "ОчередьОтложенногоФормированияИсходящихСообщений" Тогда
		
		Таблица.Колонки.Добавить("Порядок");
		Таблица.Колонки.Добавить("СсылкаНаОбъект");
		Таблица.Колонки.Добавить("ЭлементОчереди");
		
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

Функция ИмяПоляКлючаПоИмениОчереди(ИмяОчереди)
	
	ИмяПоляКлюча = "";
	Если ИмяОчереди = "ОчередьОбработкиВходящихСообщений" Тогда
		ИмяПоляКлюча = "КлючОбъекта";			
	ИначеЕсли ИмяОчереди = "ОчередьОтложенногоФормированияИсходящихСообщений" Тогда
		ИмяПоляКлюча = "СсылкаНаОбъект";		
	КонецЕсли;
	
	Возврат ИмяПоляКлюча;
	
КонецФункции

Функция ИмяПоляОбъектаПоИмениОчереди(ИмяОчереди)
	
	ИмяПоляОбъекта = "";
	Если ИмяОчереди = "ОчередьОбработкиВходящихСообщений" Тогда
		ИмяПоляОбъекта = "Сообщение";			
	ИначеЕсли ИмяОчереди = "ОчередьОтложенногоФормированияИсходящихСообщений" Тогда
		ИмяПоляОбъекта = "ЭлементОчереди";		
	КонецЕсли;
	
	Возврат ИмяПоляОбъекта;
	
КонецФункции

#КонецОбласти

#КонецОбласти

