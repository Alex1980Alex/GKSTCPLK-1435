
//////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Получает заполненный список обработчиков сообщений, зарегистрированных в системе
//
//  Возвращаемое значение:
//   ТаблицаЗначений. Описание всех активных подписок на очереди.
//
Функция ПолучитьОбработчикиСообщений() Экспорт

	Возврат ПолучитьПодписки(Ложь);
	
КонецФункции

// Получает заполненный список обработчиков сообщений, зарегистрированных в системе
//
//  Возвращаемое значение:
//   ТаблицаЗначений. Описание всех активных подписок на очереди.
//
Функция ПолучитьОбработчикиВызововRPC() Экспорт

	Возврат ПолучитьПодписки(Истина);
	
КонецФункции

// Функция - Возможные свойства сообщения
//
// Параметры:
//  АнглийскиеИмена	 - Булево - Если Истина, возвращаются английские имена свойств, иначе русские имена.
//		Значение по умолчанию: Ложь, т.е. возвращаются русские имена
// 
// Возвращаемое значение:
//   ФиксированнаяСтруктура - структура с именами свойств в качестве ключей
//
Функция ВозможныеСвойстваСообщения(Знач АнглийскиеИмена = Ложь) Экспорт
	
	ИменаСвойствКлиента = Новый Структура;
	
	Если Не АнглийскиеИмена Тогда
		ИменаСвойствКлиента.Вставить("Заголовки", "Заголовки");
		ИменаСвойствКлиента.Вставить("ВремяЖизни", "ВремяЖизни");
		ИменаСвойствКлиента.Вставить("ДоставленоПовторно", "ДоставленоПовторно");
		ИменаСвойствКлиента.Вставить("ИдентификаторДоставки", "ИдентификаторДоставки");
		ИменаСвойствКлиента.Вставить("ИмяПользователя", "ИмяПользователя");
		ИменаСвойствКлиента.Вставить("ИмяПриложения", "ИмяПриложения");
		ИменаСвойствКлиента.Вставить("Кластер", "Кластер");
		ИменаСвойствКлиента.Вставить("КлючМаршрутизации", "КлючМаршрутизации");
		ИменаСвойствКлиента.Вставить("КлючСоответствия", "КлючСоответствия");
		ИменаСвойствКлиента.Вставить("КодировкаТекста", "КодировкаТекста");
		ИменаСвойствКлиента.Вставить("НомерСообщения", "НомерСообщения");
		ИменаСвойствКлиента.Вставить("ОбратныйАдрес", "ОбратныйАдрес");
		ИменаСвойствКлиента.Вставить("ОтметкаВремени", "ОтметкаВремени");
		ИменаСвойствКлиента.Вставить("ПриоритетСообщения", "ПриоритетСообщения");
		ИменаСвойствКлиента.Вставить("РежимДоставки", "РежимДоставки");
		ИменаСвойствКлиента.Вставить("Тип", "Тип");
		ИменаСвойствКлиента.Вставить("ТипСодержимого", "ТипСодержимого");
		ИменаСвойствКлиента.Вставить("ТочкаОбмена", "ТочкаОбмена");
	Иначе
		ИменаСвойствКлиента.Вставить("Headers", "Headers");
		ИменаСвойствКлиента.Вставить("Priority", "Priority");
		ИменаСвойствКлиента.Вставить("ReplyTo", "ReplyTo");
		ИменаСвойствКлиента.Вставить("CorrelationId", "CorrelationId");
		ИменаСвойствКлиента.Вставить("MessageId", "MessageId");
		ИменаСвойствКлиента.Вставить("ContentType", "ContentType");
		ИменаСвойствКлиента.Вставить("ContentEncoding", "ContentEncoding");
		ИменаСвойствКлиента.Вставить("DeliveryMode", "DeliveryMode");
		ИменаСвойствКлиента.Вставить("Expiration", "Expiration");
		ИменаСвойствКлиента.Вставить("Timestamp", "Timestamp");
		ИменаСвойствКлиента.Вставить("Type", "Type");
		ИменаСвойствКлиента.Вставить("AppId", "AppId");
		ИменаСвойствКлиента.Вставить("UserId", "UserId");
		ИменаСвойствКлиента.Вставить("ClusterId", "ClusterId");
		ИменаСвойствКлиента.Вставить("DeliveryTag", "DeliveryTag");
		ИменаСвойствКлиента.Вставить("Exchange", "Exchange");
		ИменаСвойствКлиента.Вставить("Redelivered", "Redelivered");
		ИменаСвойствКлиента.Вставить("RoutingKey", "RoutingKey");
	КонецЕсли;

	ИменаСвойствКлиента = Новый ФиксированнаяСтруктура(ИменаСвойствКлиента);
	
	Возврат ИменаСвойствКлиента;

КонецФункции

Функция ПолучитьПодписки(Знач АсинхронныеСервисы)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПодпискиНаОчередиСообщений.Ссылка,
		|	ПодпискиНаОчередиСообщений.Наименование,
		|	ПодпискиНаОчередиСообщений.ИмяОчереди,
		|	ПодпискиНаОчередиСообщений.СерверОчередей.Код КАК КлючСоединения,
		|	ОбработчикиСобытийRMQ.МетодОбработчик,
		|	ОбработчикиСобытийRMQ.ВерсияПротокола,
		|	ОбработчикиСобытийRMQ.ИмяСервиса,
		|	ОбработчикиСобытийRMQ.ИмяМетода
		|ИЗ
		|	Справочник.ПодпискиНаОчередиСообщений КАК ПодпискиНаОчередиСообщений
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбработчикиСобытийRMQ КАК ОбработчикиСобытийRMQ
		|		ПО ПодпискиНаОчередиСообщений.Обработчик = ОбработчикиСобытийRMQ.Ссылка
		|ГДЕ
		|	ПодпискиНаОчередиСообщений.ПометкаУдаления = ЛОЖЬ
		|	И ПодпискиНаОчередиСообщений.Асинхронно = &Асинхронно
		|	И ПодпискиНаОчередиСообщений.Активно = ИСТИНА";
	
	Запрос.УстановитьПараметр("Асинхронно", АсинхронныеСервисы);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции // ПолучитьПодписки()
