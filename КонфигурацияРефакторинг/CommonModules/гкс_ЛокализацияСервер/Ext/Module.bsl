#Область ПрограммныйИнтерфейс

// Возвращает общий модуль локализации объекта по полному имени объекта метаданных.
//
// Параметры:
//  ПолноеИмя - Строка - полное имя объекта метаданных. Пример: "Справочник.Организации".
//
// Возвращаемое значение:
//  ОбщийМодуль - общий модуль локализации объекта.
//
Функция МодульЛокализации(ПолноеИмя) Экспорт
	
	ИмяМодуляЛокализации = "";
	ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
	Если ЧастиИмени.Количество() > 1 Тогда
		ИмяМодуляЛокализации = ЧастиИмени[1] + "Локализация";
	Иначе
		ИмяМодуляЛокализации = ПолноеИмя + "Локализация";
	КонецЕсли;
	
	КодЛокализацииБазы = КодЛокализацииБазы();
	Если ЗначениеЗаполнено(КодЛокализацииБазы) Тогда
		ИмяМодуляЛокализации = СтрШаблон("%1_%2", ИмяМодуляЛокализации, КодЛокализацииБазы);
	КонецЕсли;	
	
	ОбщийМодульЛокализации = Неопределено;
	
	Если Метаданные.ОбщиеМодули.Найти(ИмяМодуляЛокализации) <> Неопределено Тогда
		ОбщийМодульЛокализации = Вычислить(ИмяМодуляЛокализации); // ВычислитьВБезопасномРежиме не требуется, т.к. проверка надежная.
	КонецЕсли;
	
	Если ТипЗнч(ОбщийМодульЛокализации) = Тип("ОбщийМодуль") Тогда
		Возврат ОбщийМодульЛокализации;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает код локализации.
//
// Возвращаемое значение:
//  Результат - Строка - строка код страны.
//
Функция КодЛокализацииБазы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СтраныМира.КодАльфа2 КАК КодАльфа2
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|ГДЕ
	|	СтраныМира.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Константы.гкс_СтранаЛокализации.Получить());
	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = НРег(ВыборкаДетальныеЗаписи.КодАльфа2);
	Иначе
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
