#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьВариантыТаблоДляОбновления();
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьИнформациюНаЭлектронныхТабло(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьВариантыТаблоДляОбновления()
	
	ЗаписьВОчередь = ЭтотОбъект.Отбор.ЗаписьВОчередь.Значение;
	Если Не ЗначениеЗаполнено(ЗаписьВОчередь) Тогда
		Возврат;
	КонецЕсли;
		
	ЗначенияЗаполнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаписьВОчередь, "Номенклатура, ТочкаМаршрута");	
	
	НастройкиПриемкиПЛКМенеджер = РегистрыСведений.гкс_НастройкиПриемкаПЛКПоТочкамМаршрута;
						
	Если Не НастройкиПриемкиПЛКМенеджер.ИспользуетсяЭлектронноеТабло(ЗначенияЗаполнения.ТочкаМаршрута) Тогда	
		Возврат;		
	КонецЕсли;
	
	ТаблицаВариантовТабло = ТаблицаВариантовТаблоИнициализировать();
	
	ВариантИспользованияТабло = НастройкиПриемкиПЛКМенеджер.ПолучитьВариантИспользованияТаблоЭлектроннойОчереди(
		ЗначенияЗаполнения.ТочкаМаршрута);
							
	Если ВариантИспользованияТабло = Перечисления.гкс_ВариантыИспользованияТаблоЭлектроннойОчереди.БезОтбора Тогда
		ЗначенияЗаполнения.Вставить("Номенклатура", Неопределено);
	КонецЕсли;
	
	ЗначенияЗаполнения.Вставить("ВариантИспользования", ВариантИспользованияТабло);
	ЗначенияЗаполнения.Вставить("ВидПеревозки", Перечисления.гкс_ТипыТранспортныхСредствДоставки.Автомобиль);
	ЗначенияЗаполнения.Вставить("ВидТабло", Перечисления.гкс_ВидыЭлектронныхТабло.НеПрошедшиеРегистрацию);
	
	ТипЗаписиВОчередь = ТипЗнч(ЗаписьВОчередь);
	Если ТипЗаписиВОчередь = Тип("ДокументСсылка.гкс_ЗаписьВОчередьПриемкиПЛК") Тогда
		ЗначенияЗаполнения.Вставить("ТипРегистрации", Перечисления.гкс_ТипРегистрации.Приемка);
		
	ИначеЕсли ТипЗаписиВОчередь = Тип("ДокументСсылка.гкс_ЗаписьВОчередьОтгрузкиПЛК") Тогда
		ЗначенияЗаполнения.Вставить("ТипРегистрации", Перечисления.гкс_ТипРегистрации.Отгрузка);  	
	Иначе
		ВызватьИсключение НСтр("ru = 'Некорректная запись'");
	КонецЕсли;	
	
	Для Каждого Запись Из ЭтотОбъект Цикл							
		НовыйВариант = ТаблицаВариантовТабло.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйВариант, ЗначенияЗаполнения);							
	КонецЦикла;	
	
	ТаблицаВариантовТабло.Свернуть
	("ТочкаМаршрута, Номенклатура, ТипРегистрации, ВидПеревозки, ВидТабло, ВариантИспользования");
	
	ДополнительныеСвойства.Вставить("ВариантыДляТабло", ТаблицаВариантовТабло);

КонецПроцедуры

Функция ТаблицаВариантовТаблоИнициализировать()
	
	Таблица = Новый ТаблицаЗначений;
	КолонкиТаблицы = Таблица.Колонки;
	
	КолонкиТаблицы.Добавить("ТочкаМаршрута",	Новый ОписаниеТипов("СправочникСсылка.гкс_ТочкиМаршрута"));
	КолонкиТаблицы.Добавить("ТипРегистрации",	Новый ОписаниеТипов("ПеречислениеСсылка.гкс_ТипРегистрации"));
	КолонкиТаблицы.Добавить("ВидТабло", 		Новый ОписаниеТипов("ПеречислениеСсылка.гкс_ВидыЭлектронныхТабло"));
	КолонкиТаблицы.Добавить("Номенклатура",		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	КолонкиТаблицы.Добавить("Весы", 			Новый ОписаниеТипов("СправочникСсылка.гкс_ОборудованиеПЛК"));
	
	КолонкиТаблицы.Добавить("ВидПеревозки", 
		Новый ОписаниеТипов("ПеречислениеСсылка.гкс_ТипыТранспортныхСредствДоставки"));
		
	КолонкиТаблицы.Добавить("ВариантИспользования", 
		Новый ОписаниеТипов("ПеречислениеСсылка.гкс_ВариантыИспользованияТаблоЭлектроннойОчереди"));
	
	Возврат Таблица;
	
КонецФункции

Процедура ОбновитьИнформациюНаЭлектронныхТабло(Отказ)
	
	Перем ВариантыДляТабло; 
	
	ДополнительныеСвойства.Свойство("ВариантыДляТабло", ВариантыДляТабло);
	
	Если Отказ Или Не ЗначениеЗаполнено(ВариантыДляТабло) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаВариантыТабло Из ВариантыДляТабло Цикл
		
		ВариантТаблоВСтруктуре = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаВариантыТабло);
		
		гкс_ИнтеграцияСЭлектроннымТаблоСервер.ОбновитьТаблоДляЭлектроннойОчереди(ВариантТаблоВСтруктуре, Отказ);
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура;
		
		Если ЗначениеЗаполнено(ВариантТаблоВСтруктуре.Номенклатура) Тогда
			СтруктураОтбора.Вставить("Номенклатура", ВариантТаблоВСтруктуре.Номенклатура);	
		КонецЕсли;

		ОписанияТаблоДляОбновления = гкс_ИнтеграцияСЭлектроннымТаблоСервер.ПолучитьНастройкиПоВидуТабло(
			ВариантТаблоВСтруктуре.ТочкаМаршрута, ВариантТаблоВСтруктуре.ВидТабло, СтруктураОтбора);

	    Для Каждого ОписаниеТабло Из ОписанияТаблоДляОбновления Цикл
			
			ТаблоОбъект = ОписаниеТабло.Табло.ПолучитьОбъект();
			Событие = "ЭлектронноеТабло.ИзменениеСтатусаЗаписиВОчередиПриемки";
			
			гкс_ИнтеграцияСЭлектроннымТаблоСервер.ОтправитьДанныеНаЭлектронноеТабло(ТаблоОбъект, Событие);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("Недопустимый вызов объекта на клиенте", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
#КонецЕсли