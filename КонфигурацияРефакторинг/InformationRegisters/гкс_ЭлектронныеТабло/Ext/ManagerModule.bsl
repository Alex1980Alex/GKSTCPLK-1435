#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текст запроса для получения данных 
//		для вывода на электронное табло
//
// Параметры:
//  ВидТабло  - Перечисления.гкс_ВидыЭлектронныхТабло - 
//                 вид табло, которые будут обновлять
//
// Возвращаемое значение:
//   Строка   - текст запроса
// 
Функция ТекстЗапросаПоВидуТаблоДляОтображения(ВидТабло) Экспорт
		
	ВидыЭлектронныхТабло = Перечисления.гкс_ВидыЭлектронныхТабло;
	
	Если ВидТабло = ВидыЭлектронныхТабло.НеПрошедшиеРегистрацию Тогда 	
		ТекстЗапроса = ТекстЗапросаОтображениеНаТаблоНепрошедшиеРегистраци();
		
	ИначеЕсли ВидТабло = ВидыЭлектронныхТабло.НазначениеНаВесы Тогда 	
		ТекстЗапроса = ТекстЗапросаОтображениеНаТаблоНазначениеНаВесы();	

	ИначеЕсли ВидТабло = ВидыЭлектронныхТабло.НазначениеНаВыгрузку Тогда
		ТекстЗапроса = ТекстЗапросаОтображениеНаТаблоНазначениеНаВыгрузку();

	ИначеЕсли ВидТабло = ВидыЭлектронныхТабло.ОжиданияПроб Тогда
		ТекстЗапроса = ТекстЗапросаОтображениеНаТаблоОжиданияПроб();
	Иначе
		ТекстЗапроса = "";
	КонецЕсли;
		
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаОтображениеНаТаблоНепрошедшиеРегистраци()

	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 4
	|	ЭлектронныеТабло.ЗаписьВОчередь КАК ЗаписьВОчередь,
	|	ЭлектронныеТабло.Авто КАК Авто,
	|	ЭлектронныеТабло.Номенклатура КАК Номенклатура,
	|	ЭлектронныеТабло.ВидЗерна КАК ВидЗерна,
	|	ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияРегистрации.ПустаяСсылка) КАК Состояние
	|ИЗ
	|	РегистрСведений.гкс_ЭлектронныеТабло КАК ЭлектронныеТабло
	|ГДЕ
	|	ЭлектронныеТабло.ТочкаМаршрута = &ТочкаМаршрута
	|	И ЭлектронныеТабло.ВидТабло = &ВидТабло
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаписьВОчередь";
		
КонецФункции

Функция ТекстЗапросаОтображениеНаТаблоНазначениеНаВесы()

    Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РССостоянияРегистрацииСрезПоследних.ДокументРегистрации КАК ДокументРегистрации,
	|	РССостоянияРегистрацииСрезПоследних.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВТСостоянияРегистрации
	|ИЗ
	|	РегистрСведений.гкс_СостоянияРегистрации.СрезПоследних(&ДатаСреза, ) КАК РССостоянияРегистрацииСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 4
	|	ЭлектронныеТабло.ЗаписьВОчередь КАК ЗаписьВОчередь,
	|	ЭлектронныеТабло.Авто КАК Авто,
	|	ЭлектронныеТабло.ДокументРегистрации КАК ДокументРегистрации,
	|	ЭлектронныеТабло.Номенклатура КАК Номенклатура,
	|	ЭлектронныеТабло.ВидЗерна КАК ВидЗерна,
	|	ЕСТЬNULL(ВТСостоянияРегистрации.Состояние, ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияРегистрации.ПустаяСсылка)) КАК Состояние
	|ИЗ
	|	РегистрСведений.гкс_ЭлектронныеТабло КАК ЭлектронныеТабло
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСостоянияРегистрации КАК ВТСостоянияРегистрации
	|		ПО ЭлектронныеТабло.ДокументРегистрации = ВТСостоянияРегистрации.ДокументРегистрации
	|ГДЕ
	|	ЭлектронныеТабло.Номенклатура = &Номенклатура
	|	И ЭлектронныеТабло.ТочкаМаршрута = &ТочкаМаршрута
	|	И ЭлектронныеТабло.ВидТабло = &ВидТабло
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаписьВОчередь";
		
КонецФункции

Функция ТекстЗапросаОтображениеНаТаблоНазначениеНаВыгрузку()

    Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РССостоянияРегистрацииСрезПоследних.ДокументРегистрации КАК ДокументРегистрации,
	|	РССостоянияРегистрацииСрезПоследних.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВТСостоянияРегистрации
	|ИЗ
	|	РегистрСведений.гкс_СостоянияРегистрации.СрезПоследних(&ДатаСреза, ) КАК РССостоянияРегистрацииСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 4
	|	ЭлектронныеТабло.ЗаписьВОчередь КАК ЗаписьВОчередь,
	|	ЭлектронныеТабло.Авто КАК Авто,
	|	ЭлектронныеТабло.Номенклатура КАК Номенклатура,
	|	ЭлектронныеТабло.ВидЗерна КАК ВидЗерна,
	|	ЕСТЬNULL(ВТСостоянияРегистрации.Состояние, ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияРегистрации.ПустаяСсылка)) КАК Состояние
	|ИЗ
	|	РегистрСведений.гкс_ЭлектронныеТабло КАК ЭлектронныеТабло
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСостоянияРегистрации КАК ВТСостоянияРегистрации
	|		ПО ЭлектронныеТабло.ДокументРегистрации = ВТСостоянияРегистрации.ДокументРегистрации
	|ГДЕ
	|	ЭлектронныеТабло.ТочкаМаршрута = &ТочкаМаршрута
	|	И ЭлектронныеТабло.ВидТабло = &ВидТабло
	|	И ЭлектронныеТабло.ЯмаРазгрузки = &ЯмаРазгрузки
	|	И ЭлектронныеТабло.Номенклатура = &Номенклатура
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаписьВОчередь";

КонецФункции

Функция ТекстЗапросаОтображениеНаТаблоОжиданияПроб()

    Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РССостоянияРегистрацииСрезПоследних.ДокументРегистрации КАК ДокументРегистрации,
	|	РССостоянияРегистрацииСрезПоследних.Состояние КАК Состояние
	|ПОМЕСТИТЬ ВТСостоянияРегистрации
	|ИЗ
	|	РегистрСведений.гкс_СостоянияРегистрации.СрезПоследних(&ДатаСреза, ) КАК РССостоянияРегистрацииСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 4
	|	ЭлектронныеТабло.Авто КАК Авто,
	|	ЭлектронныеТабло.ДокументРегистрации КАК ДокументРегистрации,
	|	ЭлектронныеТабло.ЗаписьВОчередь КАК ЗаписьВОчередь,
	|	ЭлектронныеТабло.ЯмаРазгрузки КАК ЯмаРазгрузки,
	|	ВЫБОР
	|		КОГДА ЭлектронныеТабло.ЯмаРазгрузки = ЗНАЧЕНИЕ(Справочник.гкс_ТочкиМаршрута.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Приоритет,
	|	ЕСТЬNULL(ВТСостоянияРегистрации.Состояние, ЗНАЧЕНИЕ(Перечисление.гкс_СостоянияРегистрации.ПустаяСсылка)) КАК Состояние
	|ИЗ
	|	РегистрСведений.гкс_ЭлектронныеТабло КАК ЭлектронныеТабло
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСостоянияРегистрации КАК ВТСостоянияРегистрации
	|		ПО ЭлектронныеТабло.ДокументРегистрации = ВТСостоянияРегистрации.ДокументРегистрации
	|ГДЕ
	|	ЭлектронныеТабло.ТочкаМаршрута = &ТочкаМаршрута
	|	И ЭлектронныеТабло.ВидТабло = &ВидТабло
	|	И ЭлектронныеТабло.Весы = &Весы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	ЗаписьВОчередь";

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли