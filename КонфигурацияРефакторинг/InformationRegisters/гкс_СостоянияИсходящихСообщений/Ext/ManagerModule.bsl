#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗапросВсеСообщенияСоСтатусом(ПараметрыЗапроса) Экспорт
	
	СОтборомПоДатеПоследнегоСобытия = Истина;
	
	Запрос = Новый Запрос;
	
	Запрос.Параметры.Вставить("Состояние");
	Запрос.Параметры.Вставить("ДатаОтбораПоследнегоСостоянияВМиллисекундах");
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ПараметрыЗапроса);
	
	//@skip-warning
	Если НЕ ЗначениеЗаполнено(Запрос.Параметры["ДатаОтбораПоследнегоСостоянияВМиллисекундах"]) Тогда
		СОтборомПоДатеПоследнегоСобытия = Ложь;	
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапросаВсеСообщенияСоСтатусом(СОтборомПоДатеПоследнегоСобытия);
	
	Возврат Запрос;
	
КонецФункции

Процедура УстановитьСостояниеСообщения(Сообщение, 
	                                   УчастникОбмена, 
									   Состояние, 
	                                   УниверсальнаяДатаСобытияВМиллисекундах, 
									   Текст = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(УчастникОбмена) Тогда		
		УчастникОбмена = гкс_ОчередьСообщенийRMQВызовСервера.ТекущееПриложение();			
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(УниверсальнаяДатаСобытияВМиллисекундах) Тогда		
		УниверсальнаяДатаСобытияВМиллисекундах = 
		ОчередьСообщенийВызовСервера.ТекущаяУниверсальнаяДатаВМиллисекундахНаСервере();			
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Состояние) Тогда		
		Состояние = Перечисления.гкс_СостоянияИсходящихСообщенийRMQ.ОшибкаФормированияСостояния;			
	КонецЕсли;
	
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("Текст", Текст);
	СтруктураЗаписи.Вставить("Сообщение", Сообщение);
	СтруктураЗаписи.Вставить("Состояние", Состояние);
	СтруктураЗаписи.Вставить("УчастникОбмена", УчастникОбмена);
	СтруктураЗаписи.Вставить("УниверсальнаяДатаСобытияВМиллисекундах", УниверсальнаяДатаСобытияВМиллисекундах);
		
	гкс_ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "гкс_СостоянияИсходящихСообщений");
		
КонецПроцедуры

// Получает все состояния исходящего сообщения по участникам обмена.
// 
// Параметры:
//  Сообщение - СправочникСсылка.ИсходящиеСообщения - Ссылка на сообщение, состояния которого необходимо получить
// 
// Возвращаемое значение:
//  Соответствие - Состояния сообщения по участникам, где ключ элемент справочника 
// СправочникСсылка.гкс_УчастникиОбменаRMQ, значение - ПеречислениеСсылка.гкс_СостоянияИсходящихСообщенийRMQ
//
Функция СостоянияСообщенияПоУчастникам(Знач Сообщение) Экспорт
		
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСостоянияСообщенияПоУчастникам();
	Запрос.УстановитьПараметр("Сообщение", Сообщение);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.УчастникОбмена, Выборка.Состояние);
	КонецЦикла;
	
	Возврат Результат;
	 	 
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаСостоянияСообщенияПоУчастникам()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВсеСостоянияИсходящихСообщений.Сообщение КАК Сообщение,
	|	ВсеСостоянияИсходящихСообщений.УчастникОбмена КАК УчастникОбмена,
	|	МАКСИМУМ(ВсеСостоянияИсходящихСообщений.УниверсальнаяДатаСобытияВМиллисекундах) КАК УниверсальнаяДатаСобытияВМиллисекундах
	|ПОМЕСТИТЬ ВТМаксимумыПериодовСостояний
	|ИЗ
	|	РегистрСведений.гкс_СостоянияИсходящихСообщений КАК ВсеСостоянияИсходящихСообщений
	|ГДЕ
	|	ВсеСостоянияИсходящихСообщений.Сообщение = &Сообщение
	|СГРУППИРОВАТЬ ПО
	|	ВсеСостоянияИсходящихСообщений.Сообщение,
	|	ВсеСостоянияИсходящихСообщений.УчастникОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМаксимумыПериодовСостояний.УчастникОбмена КАК УчастникОбмена,
	|	ВсеСостоянияИсходящихСообщений.Состояние КАК Состояние
	|ИЗ
	|	РегистрСведений.гкс_СостоянияИсходящихСообщений КАК ВсеСостоянияИсходящихСообщений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМаксимумыПериодовСостояний КАК ВТМаксимумыПериодовСостояний
	|		ПО ВсеСостоянияИсходящихСообщений.Сообщение = ВТМаксимумыПериодовСостояний.Сообщение
	|		И ВсеСостоянияИсходящихСообщений.УчастникОбмена = ВТМаксимумыПериодовСостояний.УчастникОбмена
	|		И ВсеСостоянияИсходящихСообщений.УниверсальнаяДатаСобытияВМиллисекундах = ВТМаксимумыПериодовСостояний.УниверсальнаяДатаСобытияВМиллисекундах";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВсеСообщенияСоСтатусом(СОтборомПоДатеПоследнегоСобытия)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВсеСостоянияИсходящихСообщений.Сообщение,
	|	ВсеСостоянияИсходящихСообщений.УчастникОбмена,
	|	МАКСИМУМ(ВсеСостоянияИсходящихСообщений.УниверсальнаяДатаСобытияВМиллисекундах) КАК УниверсальнаяДатаСобытияВМиллисекундах
	|ПОМЕСТИТЬ ВТМаксимумыВремениСостоянийСообщений
	|ИЗ
	|	РегистрСведений.гкс_СостоянияИсходящихСообщений КАК ВсеСостоянияИсходящихСообщений
	|СГРУППИРОВАТЬ ПО
	|	ВсеСостоянияИсходящихСообщений.Сообщение,
	|	ВсеСостоянияИсходящихСообщений.УчастникОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТМаксимумыВремениСостоянийСообщений.Сообщение КАК Сообщение,
	|	ВТМаксимумыВремениСостоянийСообщений.УчастникОбмена КАК УчастникОбмена
	|ИЗ
	|	ВТМаксимумыВремениСостоянийСообщений КАК ВТМаксимумыВремениСостоянийСообщений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.гкс_СостоянияИсходящихСообщений КАК ВсеСостоянияИсходящихСообщений
	|		ПО ВТМаксимумыВремениСостоянийСообщений.Сообщение = ВсеСостоянияИсходящихСообщений.Сообщение
	|		И ВТМаксимумыВремениСостоянийСообщений.УчастникОбмена = ВсеСостоянияИсходящихСообщений.УчастникОбмена
	|		И ВТМаксимумыВремениСостоянийСообщений.УниверсальнаяДатаСобытияВМиллисекундах = ВсеСостоянияИсходящихСообщений.УниверсальнаяДатаСобытияВМиллисекундах
	|		И ВсеСостоянияИсходящихСообщений.Состояние = &Состояние
	|		И ВсеСостоянияИсходящихСообщений.УниверсальнаяДатаСобытияВМиллисекундах < &ДатаОтбораПоследнегоСостоянияВМиллисекундах";
	
	Если НЕ СОтборомПоДатеПоследнегоСобытия Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
		     "ВсеСостоянияИсходящихСообщений.УниверсальнаяДатаСобытияВМиллисекундах < &ДатаОтбораПоследнегоСостоянияВМиллисекундах", 
		     "ИСТИНА");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
