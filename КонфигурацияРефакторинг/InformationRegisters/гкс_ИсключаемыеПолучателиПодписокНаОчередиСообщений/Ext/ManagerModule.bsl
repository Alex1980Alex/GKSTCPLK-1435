// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// По подписке.
// 
// Параметры:
//  Подписка - СправочникСсылка.ПодпискиНаОчередиСообщений - подписка для которой требуется получить получателей 
//  к исключению
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.гкс_ПолучателиДанныхRMQ - 
// 
Функция ПоПодписке(Подписка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоПодписке();
	Запрос.УстановитьПараметр("Подписка", Подписка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Получатель");
	
КонецФункции

// Устанавливает исключаемого получателя по подписке.
// 
// Параметры:
//  Получатель  - СправочникСсылка.гкс_ПолучателиДанныхRMQ -
//  Подписка  - СправочникСсылка.ПодпискиНаОчередиСообщений -
//
Процедура УстановитьПолучателяПоПодписке(Получатель, Подписка) Экспорт
	
	гкс_ОбщегоНазначенияКлиентСервер
	     .ПроверитьПараметр("УстановитьПолучателяПоПодписке", 
	                        "Получатель",
	                        Получатель,
	                        Тип("СправочникСсылка.гкс_ПолучателиДанныхRMQ"));
	                        
	гкс_ОбщегоНазначенияКлиентСервер
	     .ПроверитьПараметр("УстановитьПолучателяПоПодписке", 
	                        "Подписка",
	                        Подписка,
	                        Тип("СправочникСсылка.ПодпискиНаОчередиСообщений"));
	
	НЗ = СоздатьНаборЗаписей();
	НЗ.Отбор.Подписка.Установить(Подписка);
	НЗ.Отбор.Получатель.Установить(Получатель);
	
	Запись = НЗ.Добавить();
	Запись.Получатель = Получатель;
	Запись.Подписка = Подписка;
	
	НЗ.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаПоПодписке()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВсеИсключаемыеПолучатели.Получатель
	|ИЗ
	|	РегистрСведений.гкс_ИсключаемыеПолучателиПодписокНаОчередиСообщений КАК ВсеИсключаемыеПолучатели
	|ГДЕ
	|	ВсеИсключаемыеПолучатели.Подписка = &Подписка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
