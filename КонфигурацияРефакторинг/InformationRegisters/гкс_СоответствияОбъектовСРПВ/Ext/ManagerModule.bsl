#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает ссылку на ПЛК (точку маршрута) по идентификатору СРПВ
//
// Параметры:
//  Идентификатор - строка - идентификатор ПЛК в СРПВ
//
// Возвращаемое значение:
//  СправочникСсылка.гкс_ТочкиМаршрута - ссылка на точку маршрута 
//
Функция ТочкаМаршрутаПоИдентификаторуСРПВ(Идентификатор) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоответствияОбъектовСРПВ.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.гкс_СоответствияОбъектовСРПВ КАК СоответствияОбъектовСРПВ
	|ГДЕ
	|	СоответствияОбъектовСРПВ.ТипОбъектаСРПВ = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыОбъектовСРПВ.ПЛК)
	|	И СоответствияОбъектовСРПВ.ИдентификаторСРПВ = &Идентификатор";
		
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
	    Результат = Выборка.Ссылка;
	Иначе	
		Результат = Справочники.гкс_ТочкиМаршрута.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

// Возвращает ссылку на весы (оборудование на ПЛК) по идентификатору СРПВ
//
// Параметры:
//  Идентификатор - строка - идентификатор ПЛК в СРПВ
//
// Возвращаемое значение:
//  СправочникСсылка.гкс_ОборудованиеПЛК - ссылка на весы 
//
Функция ВесыПоИдентификаторуСРПВ(Идентификатор) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоответствияОбъектовСРПВ.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.гкс_СоответствияОбъектовСРПВ КАК СоответствияОбъектовСРПВ
	|ГДЕ
	|	СоответствияОбъектовСРПВ.ТипОбъектаСРПВ = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыОбъектовСРПВ.Весы)
	|	И СоответствияОбъектовСРПВ.ИдентификаторСРПВ = &Идентификатор";
		
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
	    Результат = Выборка.Ссылка;
	Иначе	
		Результат = Справочники.гкс_ОборудованиеПЛК.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ЭтоИдентификаторТекущегоПЛК(Идентификатор) Экспорт

	ТочкаМаршрутаПЛК = ТочкаМаршрутаПоИдентификаторуСРПВ(Идентификатор);
	
	Если ЗначениеЗаполнено(ТочкаМаршрутаПЛК)
		И ТочкаМаршрутаПЛК = Константы.гкс_ТочкаМаршрутаБазы.Получить() Тогда
		
		Возврат Истина;		
	Иначе	
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

#КонецОбласти

#КонецЕсли

