#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ИзСправочникаТабло") Тогда
		Если ЗначениеЗаполнено(Параметры.Табло) Тогда
			ЗаполнитьФорму(Параметры.Табло);
			Если Не ЗначениеЗаполнено(Запись.Табло) Тогда
				Запись.Табло = Параметры.Табло;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Запись.ТочкаМаршрута) Тогда
				Запись.ТочкаМаршрута = Параметры.ТочкаМаршрута;
			ИначеЕсли Запись.ТочкаМаршрута <> Параметры.ТочкаМаршрута Тогда
				ТекстСообщения = НСтр("ru = 'Точка маршрута была изменена %1 на %2, необходимо записать'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, Запись.ТочкаМаршрута, Параметры.ТочкаМаршрута);  
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				Запись.ТочкаМаршрута = Параметры.ТочкаМаршрута;
			КонецЕсли;
			УправлениеФормой();
		КонецЕсли;
	Иначе
		ЭтотОбъект.ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	НаборЗаписей = РегистрыСведений.гкс_НастройкаЭлектронногоТабло.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Табло.Установить(ТекущийОбъект.Табло);
	НаборЗаписей.Записать(Истина);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ВидТаблоПриИзменении(Элемент)
	ОбновитьФормуНаСервере();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ОбновитьФормуНаСервере()
	УправлениеФормой();
	ОчиститьНеиспользуемыеПоля();
КонецПроцедуры

&НаСервере
Процедура ОчиститьНеиспользуемыеПоля()
	Если Запись.ВидТабло = ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.НазначениеНаВесы") Или
		// Гкс+[ERP-2056] Рудаков Н.А. от 13.05.2021. Добавлено:
		Запись.ВидТабло = ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.НеПрошедшиеРегистрацию") Тогда
		// } Гкс-[ERP-2056] Рудаков Н.А. от 13.05.2021. Добавлено:
		Если ЗначениеЗаполнено(Запись.ЯмаРазгрузки) Тогда
			Запись.ЯмаРазгрузки = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(Запись.Весы) Тогда
			Запись.Весы = "";
		КонецЕсли;
	ИначеЕсли Запись.ВидТабло = ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.НазначениеНаВыгрузку")
		И ЗначениеЗаполнено(Запись.Весы) Тогда
		Запись.Весы = "";
	ИначеЕсли Запись.ВидТабло = ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.ОжиданияПроб") Тогда
		Если ЗначениеЗаполнено(Запись.ЯмаРазгрузки) Тогда
			Запись.ЯмаРазгрузки = "";
		КонецЕсли;
		Если ЗначениеЗаполнено(Запись.Номенклатура) Тогда
			Запись.Номенклатура = "";
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	Элементы.Номенклатура.Видимость = Запись.ВидТабло = 
		ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.НазначениеНаВесы") Или 
		// Гкс+[ERP-2056] Рудаков Н.А. от 13.05.2021. Добавлено:
		Запись.ВидТабло = ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.НеПрошедшиеРегистрацию") Или
		// } Гкс-[ERP-2056] Рудаков Н.А. от 13.05.2021. Добавлено:
		Запись.ВидТабло = ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.НазначениеНаВыгрузку");
	Элементы.ЯмаРазгрузки.Видимость = Запись.ВидТабло = 
		ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.НазначениеНаВыгрузку");
	Элементы.Весы.Видимость = Запись.ВидТабло = 
		ПредопределенноеЗначение("Перечисление.гкс_ВидыЭлектронныхТабло.ОжиданияПроб");
		
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьФорму(Табло)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкаЭлектронногоТабло.ТочкаМаршрута КАК ТочкаМаршрута,
		|	НастройкаЭлектронногоТабло.ВидПеревозки КАК ВидПеревозки,
		|	НастройкаЭлектронногоТабло.ВидТабло КАК ВидТабло,
		|	НастройкаЭлектронногоТабло.Табло КАК Табло,
		|	НастройкаЭлектронногоТабло.Номенклатура КАК Номенклатура,
		|	НастройкаЭлектронногоТабло.Весы КАК Весы,
		|	НастройкаЭлектронногоТабло.ЯмаРазгрузки КАК ЯмаРазгрузки
		|ИЗ
		|	РегистрСведений.гкс_НастройкаЭлектронногоТабло КАК НастройкаЭлектронногоТабло
		|ГДЕ
		|	НастройкаЭлектронногоТабло.Табло = &Табло";
	
	Запрос.УстановитьПараметр("Табло", Табло);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаДетальныеЗаписи);
	КонецЕсли;

КонецПроцедуры
#КонецОбласти
