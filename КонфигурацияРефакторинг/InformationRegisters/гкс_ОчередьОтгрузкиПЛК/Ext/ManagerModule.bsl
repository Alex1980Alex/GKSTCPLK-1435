#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выбирает записи в очереди отгрузки за период 
//
// Параметры:
//  Период - Структура - период с свойствами ДатаНачала и ДатаОкончания
//
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - записи в очереди отгрузки
//
Функция ЗаписиВОчередьЗаПериод(Период) Экспорт

	ЗапросОчереди = Новый Запрос;
	ЗапросОчереди.Текст = 
	"ВЫБРАТЬ
	|	ОчередьОтгрузкиПЛК.Номенклатура КАК Номенклатура,
	|	ОчередьОтгрузкиПЛК.НачалоПериода КАК НачалоПериода,
	|	ОчередьОтгрузкиПЛК.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ОчередьОтгрузкиПЛК.Регистратор КАК Регистратор,
	|	ОчередьОтгрузкиПЛК.Контрагент КАК Контрагент,
	|	ОчередьОтгрузкиПЛК.НомерТранспортногоСредства КАК ТранспортноеСредство
	|ПОМЕСТИТЬ ВТ_ОчередьОтгрузки
	|ИЗ
	|	РегистрСведений.гкс_ОчередьОтгрузкиПЛК КАК ОчередьОтгрузкиПЛК
	|ГДЕ
	|	ОчередьОтгрузкиПЛК.НачалоПериода < &ОкончаниеПериода
	|	И ОчередьОтгрузкиПЛК.ОкончаниеПериода > &НачалоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РССтатусыЗаписейВОчередиПриемкиПЛКСрезПоследних.ЗаписьВОчередь КАК ЗаписьВОчередь,
	|	РССтатусыЗаписейВОчередиПриемкиПЛКСрезПоследних.Статус КАК Статус
	|ПОМЕСТИТЬ ВТ_СтатусыЗаписейВОчереди
	|ИЗ
	|	РегистрСведений.гкс_СтатусыЗаписейВОчередиПриемкиПЛК.СрезПоследних(
	|			,
	|			ЗаписьВОчередь В
	|				(ВЫБРАТЬ
	|					ВТ_ОчередьОтгрузки.Регистратор КАК Регистратор
	|				ИЗ
	|					ВТ_ОчередьОтгрузки)) КАК РССтатусыЗаписейВОчередиПриемкиПЛКСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОчередьОтгрузки.НачалоПериода КАК НачалоПериода,
	|	ВТ_ОчередьОтгрузки.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ВТ_ОчередьОтгрузки.Номенклатура КАК Номенклатура,
	|	ВТ_ОчередьОтгрузки.Регистратор КАК Регистратор,
	|	ВТ_ОчередьОтгрузки.Контрагент КАК Контрагент,
	|	ВТ_ОчередьОтгрузки.ТранспортноеСредство КАК ТранспортноеСредство,
	|	ВТ_СтатусыЗаписейВОчереди.Статус КАК Статус
	|ИЗ
	|	ВТ_ОчередьОтгрузки КАК ВТ_ОчередьОтгрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусыЗаписейВОчереди КАК ВТ_СтатусыЗаписейВОчереди
	|		ПО ВТ_ОчередьОтгрузки.Регистратор = ВТ_СтатусыЗаписейВОчереди.ЗаписьВОчередь";
	
	ЗапросОчереди.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	ЗапросОчереди.УстановитьПараметр("ОкончаниеПериода", Период.ДатаОкончания);
	
	РезультатЗапроса = ЗапросОчереди.Выполнить();
	
	ВыборкаОчереди = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаОчереди;
	
КонецФункции

Функция ЕстьЗаписьВОчередь(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	ПодготовитьПараметрыЗапросаЕстьЗаписьВОчередь(Запрос.Параметры, Параметры);
		
	Запрос.Текст = ПолучитьТекстЗапросаЕстьЗаписьВОчередь(Параметры);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ЕстьСвободноеМесто(ДанныеОчереди) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Параметры.Вставить("НачалоПериода");
	Запрос.Параметры.Вставить("ОкончаниеПериода");
	Запрос.Параметры.Вставить("Номенклатура");
	
	Запрос.Текст = ТекстЗапросаЕстьСвободныеМеста();
	Если ДанныеОчереди.Свойство("Ссылка") Тогда 
		
		Запрос.Параметры.Вставить("Ссылка");
		
		Запрос.Текст = Запрос.Текст + "
		|	И НЕ Очередь.Регистратор = &Ссылка";		
		
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ДанныеОчереди);
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

// Периоды очереди с учетом записи в таблице
// 
// Параметры:
//  ПараметрыПериодовОчереди - Структура - 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Периоды по параметрам в таблице
Функция ПериодыПоПараметрамВТаблице(ПараметрыПериодовОчереди) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПериодыПоПараметрам();
	
	Запрос.Параметры.Вставить("Номенклатура");
	Запрос.Параметры.Вставить("НачалоПериода");
	Запрос.Параметры.Вставить("ОкончаниеПериода");
	
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ПараметрыПериодовОчереди);

	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаЕстьЗаписьВОчередь(Параметры)
	
	Если Параметры.Свойство("Периоды") Тогда
		
		ПорНомер = 1;
		ТекстЗапросаРезультат = "";
		Для Каждого Период Из Параметры.Периоды Цикл
			
			ТекстЗапроса = ТекстЗапросаЕстьЗаписьВОчередь();
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоПериода", "&НачалоПериода" + Формат(ПорНомер,"ЧГ=0"));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОкончаниеПериода", "&ОкончаниеПериода" + Формат(ПорНомер,"ЧГ=0"));
			
			Если ПорНомер = 1 Тогда
				ТекстЗапросаРезультат = ТекстЗапроса;
			Иначе
				
				ТекстЗапросаРезультат = ТекстЗапросаРезультат 
				+ "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|" + ТекстЗапроса;
				
			КонецЕсли;
			
			ПорНомер = ПорНомер + 1;
			
		КонецЦикла;
		
		Возврат ТекстЗапросаРезультат;
		
	Иначе	
		
		Возврат ТекстЗапросаЕстьЗаписьВОчередь();
		
	КонецЕсли;
	
КонецФункции

Процедура ПодготовитьПараметрыЗапросаЕстьЗаписьВОчередь(ПараметрыЗапроса, Параметры)
	
	ПараметрыЗапроса.Вставить("Номенклатура");
	
	Если Параметры.Свойство("Периоды") Тогда
		
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Параметры, "Номенклатура");
		ПорНомер = 1;
		Для Каждого Период Из Параметры.Периоды Цикл
			
			ПараметрыЗапроса.Вставить("ОкончаниеПериода" + Формат(ПорНомер,"ЧГ=0"), Период.КонецПериода);
			ПараметрыЗапроса.Вставить("НачалоПериода" + Формат(ПорНомер,"ЧГ=0"), Период.НачалоПериода);	
			ПорНомер = ПорНомер + 1;
			
		КонецЦикла;
		
	Иначе	
		
		ПараметрыЗапроса.Вставить("ОкончаниеПериода");
		ПараметрыЗапроса.Вставить("НачалоПериода");
		
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Параметры);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ТекстыЗапросов

Функция ТекстЗапросаПериодыПоПараметрам()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОчередьОтгрузкиПЛК.НачалоПериода КАК НачалоПериода,
	|	ОчередьОтгрузкиПЛК.ОкончаниеПериода КАК ОкончаниеПериода
	|ИЗ
	|	РегистрСведений.гкс_ОчередьОтгрузкиПЛК КАК ОчередьОтгрузкиПЛК
	|ГДЕ
	|	ОчередьОтгрузкиПЛК.Номенклатура = &Номенклатура
	|	И ОчередьОтгрузкиПЛК.НачалоПериода МЕЖДУ &НачалоПериода И &ОкончаниеПериода";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЕстьСвободныеМеста()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	1 КАК ЕстьЗанятыеМеста
	|ИЗ
	|	РегистрСведений.гкс_ОчередьОтгрузкиПЛК КАК Очередь
	|ГДЕ
	|	Очередь.Номенклатура = &Номенклатура
	|	И Очередь.НачалоПериода < &ОкончаниеПериода
	|	И Очередь.ОкончаниеПериода > &НачалоПериода
	|	И Очередь.Активность";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЕстьЗаписьВОчередь()
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	1 КАК ЕстьЗаписиВОчереди
	|ИЗ
	|	РегистрСведений.гкс_ОчередьОтгрузкиПЛК КАК Очередь
	|ГДЕ
	|	Очередь.Номенклатура = &Номенклатура
	|	И Очередь.НачалоПериода МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1
	|ИЗ
	|	РегистрСведений.гкс_ОчередьОтгрузкиПЛК КАК Очередь
	|ГДЕ
	|	Очередь.Номенклатура = &Номенклатура
	|	И Очередь.ОкончаниеПериода МЕЖДУ &НачалоПериода И &ОкончаниеПериода";
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
