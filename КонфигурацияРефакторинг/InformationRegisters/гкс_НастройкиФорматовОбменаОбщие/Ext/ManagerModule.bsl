#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьНастройкиФормата(Формат) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Формат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипФормата = гкс_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Формат, "Тип"); 
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("Формат", Формат);
	Запрос.УстановитьПараметр("Тип", ТипФормата);
	Запрос.Текст = ТекстЗапросаПолучитьНастройкиФормата();
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Возврат Новый Структура("Тип, Формат, МенеджерФормирования, МенеджерОбработки",
								ТипФормата,
								Формат,
		                        Выборка.МенеджерФормирования, 
								Выборка.МенеджерОбработки);			
	КонецЕсли;
			
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьНастройкиФорматаДляФормированияКлюча(Формат) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Формат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТипФормата = гкс_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Формат, "Тип"); 
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("Формат", Формат);
	Запрос.УстановитьПараметр("Тип", ТипФормата);
	Запрос.Текст = ТекстЗапросаПолучитьНастройкиФорматаДляФормированияКлюча();
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Возврат Новый Структура("Тип, Формат",
		                        Выборка.Тип, 
								Выборка.Формат);			
	КонецЕсли;
			
	Возврат Неопределено;
	
КонецФункции

Процедура УдалитьНастроку(СтруктураЗаписи) Экспорт
	
	гкс_ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(СтруктураЗаписи, "гкс_НастройкиФорматовОбменаОбщие");
	
КонецПроцедуры

Процедура УстановитьНастройку(Настройка) Экспорт
	
	гкс_ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(Настройка, "гкс_НастройкиФорматовОбменаОбщие");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаПолучитьНастройкиФорматаДляФормированияКлюча()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОбщихНастроек.Тип КАК Тип, 
	|	ТаблицаОбщихНастроек.Формат КАК Формат
	|ИЗ
	|	РегистрСведений.гкс_НастройкиФорматовОбменаОбщие КАК ТаблицаОбщихНастроек
	|ГДЕ
	|	ТаблицаОбщихНастроек.Тип = &Тип
	|	И ТаблицаОбщихНастроек.Формат = &Формат";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПолучитьНастройкиФормата()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиФормата.МенеджерФормирования КАК МенеджерФормирования,
	|	НастройкиФормата.МенеджерОбработки КАК МенеджерОбработки
	|ИЗ
	|	(ВЫБРАТЬ
	|		Настройки.МенеджерФормирования КАК МенеджерФормирования,
	|		Настройки.МенеджерОбработки КАК МенеджерОбработки,
	|		1 КАК Приоритет
	|	ИЗ
	|		РегистрСведений.гкс_НастройкиФорматовОбменаОбщие КАК Настройки
	|	ГДЕ
	|		Настройки.Формат = &Формат
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		Настройки.МенеджерФормирования,
	|		Настройки.МенеджерОбработки,
	|		2
	|	ИЗ
	|		РегистрСведений.гкс_НастройкиФорматовОбменаОбщие КАК Настройки
	|	ГДЕ
	|		Настройки.Тип = &Тип
	|		И Настройки.Формат = НЕОПРЕДЕЛЕНО) КАК НастройкиФормата
	|УПОРЯДОЧИТЬ ПО
	|	НастройкиФормата.Приоритет";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли