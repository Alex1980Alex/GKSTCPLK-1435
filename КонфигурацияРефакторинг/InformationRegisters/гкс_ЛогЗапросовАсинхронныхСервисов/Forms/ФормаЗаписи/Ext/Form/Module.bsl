#Область ОбработчикиСобытийФормы	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		
		Отказ = Истина;
		гкс_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Запрещено интерактивное создание записей'"));
		Возврат;
		
	КонецЕсли;
	
	ЭтотОбъект.ТолькоПросмотр = Истина;
	
КонецПроцедуры

#КонецОбласти	

#Область ОбработчикиКомандФормы	

&НаКлиенте
Процедура ПоказатьЗапрос(Команда)
		
	ИзвлечьТекстЗапроса();
	
	Элементы.СтраницаЗапрос.Заголовок = СтрШаблон(НСтр("ru = 'Запрос (%1)'; en = 'Request (%1)'"), 
	РазмерСтрокой(СтрДлина(ТекстЗапрос)));
	
	Элементы.СтраницаЗапрос.Видимость = Истина;
	Элементы.ПоказатьЗапрос.Доступность = Ложь;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗапрос;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОтвет(Команда)
	
	ИзвлечьТекстОтвета();
	
	Элементы.СтраницаОтвет.Заголовок = СтрШаблон(НСтр("ru = 'Ответ (%1)'; en = 'Response (%1)'"), 
	                                             РазмерСтрокой(СтрДлина(ТекстОтвет)));
	
	Элементы.СтраницаОтвет.Видимость = Истина;
	Элементы.ПоказатьОтвет.Доступность = Ложь;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОтвет;
	
КонецПроцедуры

#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИзвлечьТекстЗапроса()
	
	Объект = РеквизитФормыВЗначение("Запись");
	ТекстЗапрос = Объект.ХранилищеЗапроса.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ИзвлечьТекстОтвета()
	
	Объект = РеквизитФормыВЗначение("Запись");
	ТекстОтвет = Объект.ХранилищеОтвета.Получить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазмерСтрокой(Размер)
	
	ДесятьКибиБайт = 10240;
	ДесятьМибиБайт = 10485760;
	ДесятьГибиБайт = 10737418240;
	
	ШаблонОтвета = "%1 %2";
	
	Если Размер >= ДесятьГибиБайт Тогда
		Возврат СтрШаблон(ШаблонОтвета, Цел(Размер / ДесятьГибиБайт), "GB");
	ИначеЕсли Размер >= ДесятьМибиБайт Тогда
		Возврат СтрШаблон(ШаблонОтвета, Цел(Размер / ДесятьМибиБайт), "MB");
	ИначеЕсли Размер >= ДесятьКибиБайт Тогда
		Возврат СтрШаблон(ШаблонОтвета, Цел(Размер / ДесятьКибиБайт), "КB");
	КонецЕсли;
	
	Возврат СтрШаблон(ШаблонОтвета, Строка(Размер), "B");
	
КонецФункции

#КонецОбласти