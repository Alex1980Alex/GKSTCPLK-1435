#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
//
// Параметры:
//  Основание - ОпределяемыйТип.гкс_ДокументыСРПВ -
//  КлючАналитикиСРПВ - СправочникСсылка.гкс_КлючиАналитикиСРПВ -  
//  
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
//
Функция ЗначенияПоУмолчанию(Основание, КлючАналитикиСРПВ) Экспорт
	
	СтруктураЗначенияПоУмолчанию = Новый Структура;
	
	СтруктураЗначенияПоУмолчанию.Вставить("КлючАналитикиСРПВ", КлючАналитикиСРПВ);
	СтруктураЗначенияПоУмолчанию.Вставить("Основание", Основание);
	
	СтруктураЗначенияПоУмолчанию.Вставить("Статус");
	СтруктураЗначенияПоУмолчанию.Вставить("Документ");
	
	СтруктураЗначенияПоУмолчанию.Вставить("МестнаяДата",  '00010101');
	СтруктураЗначенияПоУмолчанию.Вставить("ДатаОперации",  '00010101');
	СтруктураЗначенияПоУмолчанию.Вставить("Оформлен", Ложь);
	СтруктураЗначенияПоУмолчанию.Вставить("НомерВагона", "");
	
	Возврат СтруктураЗначенияПоУмолчанию;
	
КонецФункции

// Осуществляет запись в регистр по переданным данным.
//
// Параметры:
//  ДанныеЗаписи - Структура - данные для записи в регистр (См. ЗначенияПолейЗаписиРегистраПоУмолчанию)
//
Процедура ВыполнитьЗаписьВРегистр(ДанныеЗаписи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ДанныеЗаписи);
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Удаляет запись из регистра по переданному документу.
//
// Параметры:
//  Основание - ОпределяемыйТип.гкс_ДокументыСРПВ -
//  КлючАналитикиСРПВ - СправочникСсылка.гкс_КлючиАналитикиСРПВ -
//
Процедура УдалитьЗаписьРегистра(Основание, КлючАналитикиСРПВ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.КлючАналитикиСРПВ.Установить(КлючАналитикиСРПВ);
	НаборЗаписей.Отбор.Основание.Установить(Основание);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Устанавливает связь документа основного потока с документами СРПВ
//
// Параметры:
//  КлючАналитикиСРПВ - СправочникСсылка.гкс_КлючиАналитикиСРПВ -
//   Статус - ПеречислениеСсылка.гкс_СРПВСтатусыВагонов - 
//	 Документ - ДокументСсылка.гкс_Взвешивание, ДокументСсылка.гкс_РегистрацияНаПЛК - 
//
Процедура ОтразитьСвязьСДокументомСРПВ(КлючАналитикиСРПВ, Статус, Документ) Экспорт
			
	УстановитьПривилегированныйРежим(Истина);
		
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра());
		ЭлементБлокировки.УстановитьЗначение("КлючАналитикиСРПВ",  КлючАналитикиСРПВ);
		ЭлементБлокировки.УстановитьЗначение("Статус", Статус);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КлючАналитикиСРПВ.Установить(КлючАналитикиСРПВ);
		НаборЗаписей.Отбор.Статус.Установить(Статус);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Выбран() Тогда
			
			Для Каждого СтрокаТЧ Из НаборЗаписей Цикл
				СтрокаТЧ.Оформлен = ЗначениеЗаполнено(Документ);
				СтрокаТЧ.Документ = Документ;
			КонецЦикла;
			
			НаборЗаписей.Записать();			
		КонецЕсли;
		
	Исключение
				
		ТекстСообщения = СтрШаблон(
		НСтр("ru = 'Не привязать %1 к документу СРПВ по причине: %2'"),
			Документ, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(НСтр("ru='СРПВ'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.гкс_СРПВСтатусыОформленияВагонов,
			Документ,
			ТекстСообщения);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
		
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолноеИмяРегистра()
	
	Возврат "РегистрСведений.гкс_СРПВСтатусыОформленияВагонов";
	
КонецФункции

#КонецОбласти 

#КонецЕсли

