#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ЗапросВсеСообщенияСоСтатусом(ПараметрыЗапроса) Экспорт
	
	СОтборомПоДатеПоследнегоСобытия = Истина;
	
	Запрос = Новый Запрос;
	
	Запрос.Параметры.Вставить("Состояние");
	Запрос.Параметры.Вставить("ДатаОтбораПоследнегоСостоянияВМиллисекундах");
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ПараметрыЗапроса);
	
	//@skip-warning
	Если НЕ ЗначениеЗаполнено(Запрос.Параметры["ДатаОтбораПоследнегоСостоянияВМиллисекундах"]) Тогда
		СОтборомПоДатеПоследнегоСобытия = Ложь;	
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапросаВсеСообщенияСоСтатусом(СОтборомПоДатеПоследнегоСобытия);
	
	Возврат Запрос;
	
КонецФункции

// Возвращает текущее состояние входящего сообщения
// 
// Параметры:
//  Сообщение - СправочникСсылка.гкс_ВходящиеСообщенияRMQ - Ссылка на сообщение, состояния которого необходимо получить
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.гкс_СостоянияВходящихСообщенийRMQ - текущее состояние сообщения
//
Функция СостояниеСообщения(Знач Сообщение) Экспорт
	
	Результат = Перечисления.гкс_СостоянияВходящихСообщенийRMQ.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСостояниеСообщения();
	Запрос.УстановитьПараметр("Сообщение", Сообщение);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка  = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Результат = Выборка.Состояние;
	
	Возврат Результат 
	
КонецФункции

Процедура ОчиститьСостояниеСообщения(Знач Сообщение) Экспорт
	
	НЗ = СоздатьНаборЗаписей();
	НЗ.Отбор.Сообщение.Установить(Сообщение);
	
	НЗ.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьСостояниеСообщения(Знач Сообщение, 
	                                   Знач Состояние, 
									   Знач ДатаСобытия = Неопределено,
	                                   Знач УниверсальнаяДатаСобытияВМиллисекундах = Неопределено) Экспорт
									   
	Если НЕ ЗначениеЗаполнено(УниверсальнаяДатаСобытияВМиллисекундах) Тогда
		УниверсальнаяДатаСобытияВМиллисекундах = 
			                     ОчередьСообщенийВызовСервера.ТекущаяУниверсальнаяДатаВМиллисекундахНаСервере();
	КонецЕсли;						 
							 
	Если НЕ ЗначениеЗаполнено(ДатаСобытия) Тогда
		ДатаСобытия = гкс_ОчередьСообщенийRMQ.МестноеВремяИБ(УниверсальнаяДатаСобытияВМиллисекундах);
	КонецЕсли;
	
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("Период", ДатаСобытия);
	СтруктураЗаписи.Вставить("Сообщение", Сообщение);
	СтруктураЗаписи.Вставить("Состояние", Состояние);
	СтруктураЗаписи.Вставить("УниверсальнаяДатаСобытияВМиллисекундах", УниверсальнаяДатаСобытияВМиллисекундах);
	
	гкс_ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "гкс_СостоянияВходящихСообщений");
		
КонецПроцедуры

#Область ТекстыЗапросов

Функция ТекстЗапросаСостояниеСообщения()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВсеСостоянияВходящихСообщений.Состояние КАК Состояние
	|ИЗ
	|	РегистрСведений.гкс_СостоянияВходящихСообщений КАК ВсеСостоянияВходящихСообщений
	|ГДЕ
	|	ВсеСостоянияВходящихСообщений.Сообщение = &Сообщение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВсеСостоянияВходящихСообщений.УниверсальнаяДатаСобытияВМиллисекундах УБЫВ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВсеСообщенияСоСтатусом(СОтборомПоДатеПоследнегоСобытия)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВсеСостояния.Сообщение,
	|	МАКСИМУМ(ВсеСостояния.УниверсальнаяДатаСобытияВМиллисекундах) КАК УниверсальнаяДатаСобытияВМиллисекундах
	|ПОМЕСТИТЬ ВТМаксимальныеДатыСостояний
	|ИЗ
	|	РегистрСведений.гкс_СостоянияВходящихСообщений.СрезПоследних() КАК ВсеСостояния
	|СГРУППИРОВАТЬ ПО
	|	ВсеСостояния.Сообщение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ВсеСостояния.Сообщение КАК Сообщение
	|ИЗ
	|	ВТМаксимальныеДатыСостояний КАК ВТМаксимальныеДатыСостояний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.гкс_СостоянияВходящихСообщений.СрезПоследних() КАК ВсеСостояния
	|		ПО ВсеСостояния.Сообщение = ВТМаксимальныеДатыСостояний.Сообщение
	|		И ВТМаксимальныеДатыСостояний.УниверсальнаяДатаСобытияВМиллисекундах = ВсеСостояния.УниверсальнаяДатаСобытияВМиллисекундах
	|		И ВсеСостояния.Состояние = &Состояние
	|		И ВсеСостояния.УниверсальнаяДатаСобытияВМиллисекундах < &ДатаОтбораПоследнегоСостоянияВМиллисекундах";
	
	Если НЕ СОтборомПоДатеПоследнегоСобытия Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
		     "ВсеСостояния.УниверсальнаяДатаСобытияВМиллисекундах < &ДатаОтбораПоследнегоСостоянияВМиллисекундах", 
		     "ИСТИНА");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
