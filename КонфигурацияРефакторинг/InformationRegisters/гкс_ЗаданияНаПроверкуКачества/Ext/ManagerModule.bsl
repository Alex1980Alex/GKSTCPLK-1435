#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция УточненныеПоказателиПоПараметрам(ПараметрыОтбораПоказателей) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаУточненныеПоказателиДляРегистрации(); 
	
	Запрос.Параметры.Вставить("Дата");
	Запрос.Параметры.Вставить("Номенклатура");
	Запрос.Параметры.Вставить("ТочкаМаршрута");
	Запрос.Параметры.Вставить("ДокументРегистрации");
	Запрос.Параметры.Вставить("ТранспортноеСредство");
	
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ПараметрыОтбораПоказателей);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаУточненныеПоказателиДляРегистрации() 
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СвязьРегистрацииИТранспортногоДокумента.ТранспортныйДокумент КАК ТранспортныйДокумент,
	|	&ТранспортноеСредство КАК ТранспортноеСредство,
	|	&ТочкаМаршрута КАК ТочкаМаршрута,
	|	&Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТОтбор
	|ИЗ
	|	РегистрСведений.гкс_СвязьРегистрацииИТранспортногоДокумента КАК СвязьРегистрацииИТранспортногоДокумента
	|ГДЕ
	|	СвязьРегистрацииИТранспортногоДокумента.ДокументРегистрации = &ДокументРегистрации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТочкаМаршрута,
	|	Номенклатура,
	|	ТранспортныйДокумент,
	|	ТранспортноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаданияНаПроверкуКачестваСрезПоследних.ПоказательАнализа КАК ПоказательАнализа,
	|	ЗаданияНаПроверкуКачестваСрезПоследних.ВыполнитьАнализ КАК ВыполнитьАнализ
	|ИЗ
	|	РегистрСведений.гкс_ЗаданияНаПроверкуКачества.СрезПоследних(&Дата, (ТочкаМаршрута, Номенклатура,
	|		ТранспортныйДокумент, ТранспортноеСредство) В
	|		(ВЫБРАТЬ
	|			ВТОтбор.ТочкаМаршрута,
	|			ВТОтбор.Номенклатура,
	|			ВТОтбор.ТранспортныйДокумент,
	|			ВТОтбор.ТранспортноеСредство
	|		ИЗ
	|			ВТОтбор)) КАК ЗаданияНаПроверкуКачестваСрезПоследних";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли