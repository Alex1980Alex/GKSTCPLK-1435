#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	гкс_ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ПроверитьЗаполнениеОбъектаПередЗаписью(Отказ);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	гкс_ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры    

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	гкс_ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	гкс_ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверяемыеРеквизиты.Добавить("ТочкаМаршрута");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеОбъектаПередЗаписью(Отказ)
	
	ТекстОшибки = "";
	Если ПересеченияНормативныхПоказателейСуществуют(ТекстОшибки) Тогда
		
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
														  
	КонецЕсли;
	   	
КонецПроцедуры

Функция ПересеченияНормативныхПоказателейСуществуют(ТекстОшибки)
	
	Пересечения = Ложь;
	
	ЕстьНазначенияНаЯмуРазгрузки = 
	          гкс_ПриемкаТранспорта.НоменклатураНазначенаНаЯмуДляТочкиМаршрута(Номенклатура, ТочкаМаршрута);
	
	Если ЕстьНазначенияНаЯмуРазгрузки Тогда
		Возврат Пересечения;
	КонецЕсли;

	ТаблицаПересечений = гкс_ПриемкаНаПЛКСервер.ПолучитьПересекаемостьНормативныхПоказателейСерификации(Ссылка);

	Если ТаблицаПересечений.Количество() > 0 Тогда

		ШаблонСообщения = НСтр("ru='Существуют пересечения:
							   | Документ: %1 - Номенклатура: %2'");

		ТекстОшибки = НСтр("ru='Существуют пересечения: '");
		Для Каждого Строка Из ТаблицаПересечений Цикл
			ТекстОшибки = ТекстОшибки + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
				                                                                                Строка.Регистратор, 
				                                                                                Строка.Номенклатура);
		КонецЦикла;

		Пересечения = Истина;

	КонецЕсли;

	Возврат Пересечения;
		
КонецФункции
 
#КонецОбласти
	
#КонецЕсли

