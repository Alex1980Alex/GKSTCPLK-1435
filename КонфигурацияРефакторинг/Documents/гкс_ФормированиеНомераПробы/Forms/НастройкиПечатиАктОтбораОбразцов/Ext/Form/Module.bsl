
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда 
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Печать") Тогда
		Элементы.ФормаСохранить.Заголовок = НСтр("ru = 'Печать'");
	КонецЕсли;

	КлючОбъекта = Документы.гкс_ФормированиеНомераПробы.КлючОбъектаПользовательскихНастроек();
	КлючНастроек = "НастройкиПечатиАктОтбораПроб";
	
	Если Параметры.Свойство("НастройкиПечати") Тогда
		НастройкиПечати = Параметры.НастройкиПечати;
		
	Иначе
		НастройкиПечати = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
								КлючОбъекта, КлючНастроек);
		Если НастройкиПечати = Неопределено Тогда
			НастройкиПечати = НастройкиПечатиПоУмолчанию();
		КонецЕсли;
		
	КонецЕсли;
	
	Если НастройкиПечати <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиПечати);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	НастройкиПечати = НастройкиПечатиПоУмолчанию();
	ЗаполнитьЗначенияСвойств(НастройкиПечати, ЭтотОбъект);
	СохранитьНастройкиНаСервере(НастройкиПечати); 
	Закрыть(НастройкиПечати);

КонецПроцедуры

#КонецОбласти                          

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НастройкиПечатиПоУмолчанию()
	
	Возврат Документы.гкс_ФормированиеНомераПробы.НастройкиПечатиПоУмолчанию();
	
КонецФункции

&НаСервере
Процедура СохранитьНастройкиНаСервере(НастройкиПечати)

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючОбъекта, КлючНастроек, НастройкиПечати);
	
КонецПроцедуры

#КонецОбласти

	