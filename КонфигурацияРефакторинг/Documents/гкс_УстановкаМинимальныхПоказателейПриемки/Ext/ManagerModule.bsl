#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("ЛабораторияИКачество");
		
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = гкс_ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если НЕ ДопПараметры.ПолучитьТекстыЗапроса Тогда
		// Создадим запрос инициализации движений		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ, ДопПараметры);
		// Сформируем текст запроса
		УстановитьПоЗначениямДокумента = Ложь;
		ДополнительныеСвойства = ДопПараметры.ДополнительныеСвойства;
		Если ТипЗнч(ДополнительныеСвойства) = Тип("Структура") 
			И ДополнительныеСвойства.Свойство("ПоЗначениямДокумента") Тогда
		    УстановитьПоЗначениямДокумента = ДополнительныеСвойства.ПоЗначениямДокумента;
		КонецЕсли;
		
		СформироватьТекстЗапросаТаблицаМинимальныеПоказателиКачестваПриемки(
			ТекстыЗапроса, Регистры, УстановитьПоЗначениямДокумента);
	КонецЕсли;
	
	// Получим таблицы для движений
	Возврат гкс_ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции
	
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	МенеджерВременныхТаблиц = Неопределено;
	
	Если ДопПараметры <> Неопределено
		И ДопПараметры.Свойство("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц) Тогда
		
		МенеджерВременныхТаблиц = ?(МенеджерВременныхТаблиц = Неопределено,
									Новый МенеджерВременныхТаблиц,
									МенеджерВременныхТаблиц);
		
	Иначе
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ДокументСсылка, "Дата, Номенклатура, Показатель");
		
	Запрос.УстановитьПараметр("Период", РеквизитыДокумента.Дата);
	Запрос.УстановитьПараметр("Номенклатура", РеквизитыДокумента.Номенклатура);
	Запрос.УстановитьПараметр("Показатель", РеквизитыДокумента.Показатель);
	
КонецПроцедуры

Процедура СформироватьТекстЗапросаТаблицаМинимальныеПоказателиКачестваПриемки(ТекстыЗапроса, 
																			  Регистры, ПоДокументу = Ложь)
	
	ИмяРегистра = "гкс_МинимальныеПоказателиКачестваПриемки";
	
	Если НЕ гкс_ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ПоДокументу Тогда
		ТекстЗапроса = ТекстЗапросаМинимальныеПоказателиКачестваПриемкиПоДокументу();
	Иначе         
		ТекстЗапроса = ТекстЗапросаМинимальныеПоказателиКачестваПриемки();
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
КонецПроцедуры

Функция ТекстЗапросаМинимальныеПоказателиКачестваПриемкиПоДокументу()
	
	Возврат 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.Контрагент КАК Контрагент,
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.ЗначениеПоказателя КАК ЗначениеПоказателя,
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.ОстатокДляПриемки КАК ОстатокДляПриемки,
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.ШаблонКомментария КАК ШаблонКомментария,
	|	УстановкаМинимальныхПоказателейПриемки.Показатель КАК Показатель,
	|	УстановкаМинимальныхПоказателейПриемки.Номенклатура КАК Номенклатура,
	|	УстановкаМинимальныхПоказателейПриемки.Дата КАК Период,
	|	ИСТИНА КАК Действует
	|ИЗ
	|	Документ.гкс_УстановкаМинимальныхПоказателейПриемки.КачественныеПоказатели КАК УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_УстановкаМинимальныхПоказателейПриемки КАК УстановкаМинимальныхПоказателейПриемки
	|		ПО УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.Ссылка = УстановкаМинимальныхПоказателейПриемки.Ссылка
	|ГДЕ
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.Ссылка = &Ссылка";
	
КонецФункции

Функция ТекстЗапросаМинимальныеПоказателиКачестваПриемки()
	
	Возврат 
	"ВЫБРАТЬ
	|	УстановкаМинимальныхПоказателейПриемки.Номенклатура КАК Номенклатура,
	|	УстановкаМинимальныхПоказателейПриемки.Показатель КАК Показатель,
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.Контрагент КАК Контрагент,
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.ЗначениеПоказателя КАК ЗначениеПоказателя,
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.ОстатокДляПриемки КАК ОстатокДляПриемки,
	|	УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.ШаблонКомментария КАК ШаблонКомментария,
	|	УстановкаМинимальныхПоказателейПриемки.Дата КАК Период,
	|	ИСТИНА КАК Действует
	|ПОМЕСТИТЬ ВТ_НовыеЗначения
	|ИЗ
	|	Документ.гкс_УстановкаМинимальныхПоказателейПриемки.КачественныеПоказатели КАК УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_УстановкаМинимальныхПоказателейПриемки КАК УстановкаМинимальныхПоказателейПриемки
	|		ПО УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.Ссылка = УстановкаМинимальныхПоказателейПриемки.Ссылка
	|			И (УстановкаМинимальныхПоказателейПриемкиКачественныеПоказатели.Ссылка = &Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МинимальныеПоказателиКачестваПриемкиСрезПоследних.Номенклатура КАК Номенклатура,
	|	МинимальныеПоказателиКачестваПриемкиСрезПоследних.Показатель КАК Показатель,
	|	МинимальныеПоказателиКачестваПриемкиСрезПоследних.Контрагент КАК Контрагент,
	|	МинимальныеПоказателиКачестваПриемкиСрезПоследних.ОстатокДляПриемки КАК ОстатокДляПриемки,
	|	МинимальныеПоказателиКачестваПриемкиСрезПоследних.ШаблонКомментария КАК ШаблонКомментария,
	|	&Период КАК Период
	|ПОМЕСТИТЬ ВТ_СтарыеЗначения
	|ИЗ
	|	РегистрСведений.гкс_МинимальныеПоказателиКачестваПриемки.СрезПоследних(
	|			&Период,
	|			Номенклатура = &Номенклатура
	|				И Показатель = &Показатель) КАК МинимальныеПоказателиКачестваПриемкиСрезПоследних
	|ГДЕ
	|	МинимальныеПоказателиКачестваПриемкиСрезПоследних.Действует
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтарыеЗначения.Номенклатура КАК Номенклатура,
	|	ВТ_СтарыеЗначения.Показатель КАК Показатель,
	|	ВТ_СтарыеЗначения.Контрагент КАК Контрагент,
	|	0 КАК ЗначениеПоказателя,
	|	ВТ_СтарыеЗначения.ОстатокДляПриемки КАК ОстатокДляПриемки,
	|	ВТ_СтарыеЗначения.ШаблонКомментария КАК ШаблонКомментария,
	|	ВТ_СтарыеЗначения.Период КАК Период,
	|	ЛОЖЬ КАК Действует
	|ИЗ
	|	ВТ_СтарыеЗначения КАК ВТ_СтарыеЗначения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НовыеЗначения КАК ВТ_НовыеЗначения
	|		ПО ВТ_СтарыеЗначения.Номенклатура = ВТ_НовыеЗначения.Номенклатура
	|			И ВТ_СтарыеЗначения.Показатель = ВТ_НовыеЗначения.Показатель
	|			И ВТ_СтарыеЗначения.Контрагент = ВТ_НовыеЗначения.Контрагент
	|ГДЕ
	|	ВТ_НовыеЗначения.Номенклатура ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_НовыеЗначения.Номенклатура,
	|	ВТ_НовыеЗначения.Показатель,
	|	ВТ_НовыеЗначения.Контрагент,
	|	ВТ_НовыеЗначения.ЗначениеПоказателя,
	|	ВТ_НовыеЗначения.ОстатокДляПриемки,
	|	ВТ_НовыеЗначения.ШаблонКомментария,
	|	ВТ_НовыеЗначения.Период,
	|	ВТ_НовыеЗначения.Действует
	|ИЗ
	|	ВТ_НовыеЗначения КАК ВТ_НовыеЗначения";
		
КонецФункции
	
#КонецОбласти


#КонецЕсли
	