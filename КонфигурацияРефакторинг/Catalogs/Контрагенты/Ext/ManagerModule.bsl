#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НайтиПоПолномуНаименованию(СтрокаПоиска) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НаименованиеПолное", СтрокаПоиска);
	Запрос.Текст = ТекстЗапросаНайтиПоПолномуНаименованию();

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;

КонецФункции

// Выполняет поиск контрагента по ИНН и КПП
// 
// Параметры:
//  СтруктураПоиска - Структура - Структура с ключами
//  	* ИНН - Строка - 
//  	* КПП - Строка - 
// 
// Возвращаемое значение:
//  Неопределено, СправочникСсылка.Контрагенты - Неопределено, если не найдено или ссылка на элемент
Функция НайтиПоИННКПП(СтруктураПоиска) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", СтруктураПоиска.ИНН);
	Запрос.УстановитьПараметр("КПП", СтруктураПоиска.КПП);
	Запрос.Текст = ТекстЗапросаНайтиПоИННКПП();

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.Ссылка;

КонецФункции

// Получение адреса контрагента по виду
// Параметры:
// Контрагент - СправочникСсылка.Контрагенты
// ВидАдреса - Перечисление
// ВозвращаемоеЗначение:
// - Структура
Функция АдресКонтрагентыПоВидуКонтактнойИнформации(Контрагент, Дата, ВидАдреса) Экспорт
	
	СтруктураАдреса = СтруктураАдресаИзРеквизитов(Контрагент); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаАдресаПоВидуАдреса();	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ВидАдреса", ВидАдреса);
	Запрос.УстановитьПараметр("Дата", Дата);
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();  
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураАдреса, Выборка);
	КонецЕсли;	                                           
	
	Возврат СтруктураАдреса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаНайтиПоПолномуНаименованию()

	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.НаименованиеПолное ПОДОБНО &НаименованиеПолное";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаНайтиПоИННКПП()
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Контрагенты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.ИНН = &ИНН
	|	И Контрагенты.КПП = &КПП";

	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаАдресаПоВидуАдреса()
	
	Возврат
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтрагентыКонтактнаяИнформация.Ссылка КАК Ссылка,
	|	КонтрагентыКонтактнаяИнформация.НомерСтроки КАК НомерСтроки,
	|	КонтрагентыКонтактнаяИнформация.Тип КАК Тип,
	|	КонтрагентыКонтактнаяИнформация.Вид КАК Вид,
	|	КонтрагентыКонтактнаяИнформация.Представление КАК Представление,
	|	КонтрагентыКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей,
	|	КонтрагентыКонтактнаяИнформация.Страна КАК Страна,
	|	КонтрагентыКонтактнаяИнформация.Регион КАК Регион,
	|	КонтрагентыКонтактнаяИнформация.Город КАК Город,
	|	КонтрагентыКонтактнаяИнформация.АдресЭП КАК АдресЭП,
	|	КонтрагентыКонтактнаяИнформация.ДоменноеИмяСервера КАК ДоменноеИмяСервера,
	|	КонтрагентыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона,
	|	КонтрагентыКонтактнаяИнформация.НомерТелефонаБезКодов КАК НомерТелефонаБезКодов,
	|	КонтрагентыКонтактнаяИнформация.ВидДляСписка КАК ВидДляСписка,
	|	КонтрагентыКонтактнаяИнформация.ДействуетС КАК ДействуетС,
	|	КонтрагентыКонтактнаяИнформация.Значение КАК Значение
	|ПОМЕСТИТЬ ДанныеАдреса
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
	|ГДЕ
	|	КонтрагентыКонтактнаяИнформация.Ссылка = &Контрагент
	|	И КонтрагентыКонтактнаяИнформация.Вид = &ВидАдреса
	|	И КонтрагентыКонтактнаяИнформация.ДействуетС <= &Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеАдреса.Вид КАК Вид,
	|	МАКСИМУМ(ДанныеАдреса.ДействуетС) КАК ДействуетС
	|ПОМЕСТИТЬ МаксимальнаяДата
	|ИЗ
	|	ДанныеАдреса КАК ДанныеАдреса
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеАдреса.Вид
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеАдреса.Ссылка КАК Ссылка,
	|	ДанныеАдреса.НомерСтроки КАК НомерСтроки,
	|	ДанныеАдреса.Тип КАК Тип,
	|	ДанныеАдреса.Вид КАК Вид,
	|	ДанныеАдреса.Представление КАК Представление,
	|	ДанныеАдреса.ЗначенияПолей КАК ЗначенияПолей,
	|	ДанныеАдреса.Страна КАК Страна,
	|	ДанныеАдреса.Регион КАК Регион,
	|	ДанныеАдреса.Город КАК Город,
	|	ДанныеАдреса.АдресЭП КАК АдресЭП,
	|	ДанныеАдреса.ДоменноеИмяСервера КАК ДоменноеИмяСервера,
	|	ДанныеАдреса.НомерТелефона КАК НомерТелефона,
	|	ДанныеАдреса.НомерТелефонаБезКодов КАК НомерТелефонаБезКодов,
	|	ДанныеАдреса.ВидДляСписка КАК ВидДляСписка,
	|	ДанныеАдреса.ДействуетС КАК ДействуетС,
	|	ДанныеАдреса.Значение КАК Значение
	|ИЗ
	|	ДанныеАдреса КАК ДанныеАдреса
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальнаяДата КАК МаксимальнаяДата
	|		ПО ДанныеАдреса.Вид = МаксимальнаяДата.Вид
	|			И ДанныеАдреса.ДействуетС = МаксимальнаяДата.ДействуетС";
	
КонецФункции

#КонецОбласти

Функция СтруктураАдресаИзРеквизитов(Контрагент)
	
	СтруктураАдреса = Новый Структура;
	
	МетаданныеКонтрагент = Контрагент.Метаданные();
	ТЧКонтактнаяИнформация = МетаданныеКонтрагент.ТабличныеЧасти.КонтактнаяИнформация;
	Для Каждого Реквизит Из ТЧКонтактнаяИнформация.Реквизиты Цикл
		СтруктураАдреса.Вставить(Реквизит.Имя, "");
	КонецЦикла;	
	
	Возврат СтруктураАдреса;
	
КонецФункции

#КонецОбласти

#КонецЕсли