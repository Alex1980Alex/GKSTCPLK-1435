#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Полный адрес.
// 
// Параметры:
//  Ресурс - СправочникСсылка.гкс_РесурсыВебСервисов - Сервис
// 
// Возвращаемое значение:
//  Строка - полный адрес ресурса, включая родительские адреса
Функция ПолныйАдрес(Ресурс) Экспорт
	
	УРЛРесурса = "";

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ресурс);
	Запрос.Текст = ТекстЗапросаПолныйАдрес();
	
	РезультатЗапроса = Запрос.Выполнить();	
	Пока НЕ РезультатЗапроса.Пустой() Цикл
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		УРЛРесурса = Выборка.URL + УРЛРесурса;
		
		Запрос.УстановитьПараметр("Ссылка", Выборка.КорневойУзел);
		РезультатЗапроса = Запрос.Выполнить();	
			
	КонецЦикла;         
	
	Подключение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ресурс, "Подключение");
	Если НЕ ЗначениеЗаполнено(Подключение) Тогда
		ВызватьИсключение НСтр("ru='Не заполнено подключение. Вызов невозможен'");
	КонецЕсли;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Подключение, "Сервер,Порт,ИспользоватьЗащищенноеСоединение,");
	
	Схема = "http";
	Если Реквизиты.ИспользоватьЗащищенноеСоединение Тогда
		Схема = "https";
	КонецЕсли;
	
	Результат = Схема + "://" + Реквизиты.Сервер + ":" + Реквизиты.Порт + УРЛРесурса;   
	
	
	Возврат Результат;
	
КонецФункции

// Подключение к сервису.
// 
// Параметры:
//  Сервис - СправочникСсылка.гкс_РесурсыВебСервисов - Сервис
// 
// Возвращаемое значение:
//  СправочникСсылка.гкс_ПодключениеКВебСервисам - Подключение к сервису
Функция ПодключениеКСервису(Ресурс) Экспорт
		
	Результат = Справочники.гкс_ПодключениеКВебСервисам.ПустаяСсылка();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ресурс);
	Запрос.Текст = ТекстЗапросаПодключениеКСервису();
	
	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	Возврат Выборка.Подключение;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Метод.
// 
// Параметры:
//  Сервис - СправочникСсылка.гкс_РесурсыВебСервисов - 
// 
// Возвращаемое значение:
//  Строка - 
Функция Метод(Сервис) Экспорт
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сервис, "Метод");
КонецФункции

Функция ЗаголовкиКакСоотвествие(Сервис) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Сервис);
	Запрос.Текст = ТекстЗапросаЗаголовкиСервисаКакСоотвествие();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Заголовок, Выборка.Значение);	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаЗаголовкиСервисаКакСоотвествие()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Заголовки.Заголовок,
	|	Заголовки.Значение
	|ИЗ
	|	Справочник.гкс_РесурсыВебСервисов.Заголовки КАК Заголовки
	|ГДЕ
	|	Заголовки.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстЗапросаПолныйАдрес()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|   Сервисы.Родитель КАК КорневойУзел,
	|	ЕстьNull(Сервисы.Родитель.URL, """") КАК РодительURL,
	|	Сервисы.URL КАК URL
	|ИЗ
	|	Справочник.гкс_РесурсыВебСервисов КАК Сервисы
	|ГДЕ
	|	Сервисы.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПодключениеКСервису()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВсеСервисы.Подключение КАК Подключение
	|ИЗ
	|	Справочник.гкс_РесурсыВебСервисов КАК ВсеСервисы
	|ГДЕ
	|	ВсеСервисы.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции
	
#КонецОбласти

#КонецОбласти

#КонецЕсли
