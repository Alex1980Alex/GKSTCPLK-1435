#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Создает новый элемент справочника, заполняет по переданному параметру, записывает.
//
// Параметры:
//  ДанныеЗаполнения - см. ИнициализироватьСтруктураНовогоЭлемента()
//
Функция НовыйЭлемент(ДанныеЗаполнения) Экспорт

	Продукция = СоздатьЭлемент();
	Продукция.Заполнить(ДанныеЗаполнения);

	Попытка			
		Продукция.Записать();		
	Исключение
		Комментарий = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации("ИнтеграцияWarehouse.ПолучитьКачественныеПоказатели",
		                         УровеньЖурналаРегистрации.Ошибка, 
		                         Метаданные.Справочники.гкс_ПоказателиКачестваWarehouse, 
		                         Неопределено, 
		                         Комментарий);
								 
		ИсключениеОшибкаСозданияНовогоЭлемента(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Продукция.Ссылка;
			
КонецФункции

// Ищет элемент справочника по реквизиту "ИдентификаторWarehouse".
//
// Параметры:
//  Идентификатор - Строка - идентификатор Warehouse
//
Функция ПродукцияПоИдентификатору(Идентификатор) Экспорт
	
	Запрос 			= Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторWarehouse", Идентификатор);
	Запрос.Текст 	= 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПродукцияWarehouse.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.гкс_ПродукцияWarehouse КАК ПродукцияWarehouse
		|ГДЕ
		|	ПродукцияWarehouse.ИдентификаторWarehouse = &ИдентификаторWarehouse";
	
	РезультатЗапроса 	= Запрос.Выполнить();
	Выборка 			= РезультатЗапроса.Выбрать();
	
	ПродукцияСсылка 	= ПустаяСсылка();
	
	Если Выборка.Следующий() Тогда
		ПродукцияСсылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ПродукцияСсылка;
			
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру элемента справочника.
//
// Возвращаемое значение:
//  Структура - содержит все реквизиты справочника:
// 		* Наименование - Строка - имя показателя
// 		* НаименованиеКороткое - Строка - имя показателя
// 		* ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения -
// 		* Стандарт - Строка - способ измерения
// 		* Точность - Число - точность измерения
// 		* ЗависимыйПоказатель - СправочникСсылка.гкс_ПоказателиКачестваWarehouse - связанный показатель
// 		* ВерхнийПределЗависимогоПоказателя - Число - пределы
// 		* НижнийПределЗависимогоПоказателя - Число - пределы
// 		* КодWarehouse - Строка - код справочного элемента в веб-сервесе
// 		* ИдентификаторWarehouse - Строка - УИД в веб-сервесе
//
Функция ИнициализироватьСтруктураНовогоЭлемента() Экспорт
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Наименование", 						"");
	СтруктураЗаполнения.Вставить("НаименованиеКороткое", 				"");
	СтруктураЗаполнения.Вставить("НаименованиеEN", 						"");
	СтруктураЗаполнения.Вставить("НаименованиеКороткоеEN", 				"");
	СтруктураЗаполнения.Вставить("НормативныйДокумент", 				"");
	СтруктураЗаполнения.Вставить("ИдентификаторWarehouse", 				"");
	
	Возврат СтруктураЗаполнения;

КонецФункции

Процедура ИсключениеОшибкаСозданияНовогоЭлемента(ПодробноеПредставлениеОшибки)
	
	ТекстОшибки = 
	    СтрШаблон(НСтр("ru = 'Ошибка создания нового элемента справочника ""Продукция Warehouse"".%1%2'"), 
	    "|",
	    ПодробноеПредставлениеОшибки);
	
	ВызватьИсключение ТекстОшибки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли