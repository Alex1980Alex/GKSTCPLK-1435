#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьПривязкуПользователейКДругимСчитывателям(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПривязкуПользователейКДругимСчитывателям(Отказ)
	
	ШаблонТекстаСообщенияПользователю = НСтр("ru='Пользователь %1 уже указан в другом считывателе %2'");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = ТекстЗапросаПроверитьПривязкуПользователейКДругимСчитывателям();
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаСообщенияПользователю,
			                                                                     ВыборкаДетальныеЗаписи.ПользовательПредставление,
																				 ВыборкаДетальныеЗаписи.ДругойСчитывательПредставление);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ВыборкаДетальныеЗаписи.ДругойСчитыватель);
		
	КонецЦикла;

	
КонецПроцедуры

#Область ТекстыЗапросов

Функция ТекстЗапросаПроверитьПривязкуПользователейКДругимСчитывателям()
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПользователиСчитывателя.Ссылка) КАК ПользовательПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПользователиДругихСчитывателей.Ссылка) КАК ДругойСчитывательПредставление,
	|	ПользователиДругихСчитывателей.Ссылка КАК ДругойСчитыватель
	|ИЗ
	|	Справочник.гкс_СчитывателиUHFRFID.Пользователи КАК ПользователиСчитывателя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гкс_СчитывателиUHFRFID.Пользователи КАК ПользователиДругихСчитывателей
	|		ПО ПользователиСчитывателя.Пользователь = ПользователиДругихСчитывателей.Пользователь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.гкс_СчитывателиUHFRFID КАК ДругиеСчитыватели
	|		ПО (ПользователиДругихСчитывателей.Ссылка = ДругиеСчитыватели.Ссылка)
	|ГДЕ
	|	ПользователиСчитывателя.Ссылка = &Ссылка
	|	И НЕ ПользователиДругихСчитывателей.Ссылка = &Ссылка
	|	И НЕ ДругиеСчитыватели.ПометкаУдаления";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте'");
#КонецЕсли