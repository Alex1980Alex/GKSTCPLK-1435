
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокШрифтов();
	
	Если НЕ Объект.Ссылка.Пустая() Тогда
		
		ЗаполнитьНастройкиЗаголовка();
		ЗаполнитьНастройки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	НастройкаТабло = ТекущийОбъект.НастройкаТабло;
	НастройкаТабло.Очистить();
	Если ТекущийОбъект.ЕстьЗаголовок Тогда
		НоваяСтрока = НастройкаТабло.Добавить();
		НоваяСтрока.ВысотаСтроки = ЗаголовокВысота;
		НоваяСтрока.Ширина = ЗаголовокШирина;
		НоваяСтрока.КоординатаХ = ЗаголовокКоординатаХ;
		НоваяСтрока.КоординатаУ = ЗаголовокКоординатаУ;
		НоваяСтрока.Шрифт = ЗаголовокШрифт;
		НоваяСтрока.РазмерШрифта = ЗаголовокРазмер;
		НоваяСтрока.Выравнивание = ЗаголовокВыравнивание;
		НоваяСтрока.ЭтоЗаголовок = Истина;
	КонецЕсли;
	Для Каждого СтрокаНастройки Из ТаблицаНастройкаТабло Цикл
		НоваяСтрока = НастройкаТабло.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНастройки);
		НоваяСтрока.ЭтоЗаголовок = Ложь;
	КонецЦикла;
	// Гкс+[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:
	Если ТекущийОбъект.ЕстьЗаголовок И 
		Не ТекущийОбъект.Автозаголовок И Не ЗначениеЗаполнено(ТекущийОбъект.Заголовок) Тогда
		ТекущийОбъект.Автозаголовок = Истина;
	КонецЕсли;
	// } Гкс-[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ПроверитьКорректностьНастроекСтрокТабло(Отказ);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АвтозаголовокПриИзменении(Элемент)
	УстановитьВидимостьЗаголовка();
	Если Объект.Автозаголовок Тогда
		Объект.Заголовок = "";
	КонецЕсли;
КонецПроцедуры

// Гкс+[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:
&НаКлиенте
Процедура АвтоРаспределениеСтрокПриИзменении(Элемент)
	АвтоРаспределение = Объект.АвтоРаспределениеСтрок;
	УстановитьВидимостьКолонокТаблицыНастроек(АвтоРаспределение);
	Элементы.ЗаголовокКоординатаУ.Видимость = Не АвтоРаспределение;
	Элементы.ЗаголовокВысота.Видимость = Не АвтоРаспределение;
	Если АвтоРаспределение Тогда
		ОчиститьКолонкиТаблицыНастроек();
		ЗаголовокКоординатаУ = 0;
		ЗаголовокВысота = 0;
	КонецЕсли;
	
КонецПроцедуры
// } Гкс-[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьТестовоеТабло(Команда)
	Если Не Модифицированность Тогда
		ЗаполнитьТестовоеТаблоНаСервере();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Необходимо записать'");
		Сообщение.Сообщить();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаголовокТабло(Команда)
	Если Не Модифицированность Тогда
		СформироватьЗаголовокТаблоНаСервере();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Необходимо записать'");
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НастройкаТабло(Команда)
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Табло", Объект.Ссылка);
	ПараметрыОткрытия.Вставить("ИзСправочникаТабло", Истина);
	ПараметрыОткрытия.Вставить("ТочкаМаршрута", Объект.ТочкаМаршрута);
	ОткрытьФорму("РегистрСведений.гкс_НастройкаЭлектронногоТабло.ФормаЗаписи", ПараметрыОткрытия, ЭтотОбъект, 
	ЭтотОбъект, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаТабло(Команда)
	Если Не Модифицированность Тогда
		ОтправитьНаТаблоНаСервере();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Необходимо записать'");
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеНастроек

&НаСервере
Процедура ЗаполнитьСписокШрифтов()
	
	СписокШрифтов = Справочники.гкс_ЭлектронныеТабло.ПолучитьСписокШрифтов();
	Для Каждого СтрокаШрифт Из СписокШрифтов Цикл
		
		Элементы.ЗаголовокШрифт.СписокВыбора.Добавить(СтрокаШрифт);
		Элементы.ТаблицаНастройкаТаблоШрифт.СписокВыбора.Добавить(СтрокаШрифт);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиЗаголовка()
	
	Если НЕ Объект.ЕстьЗаголовок Тогда
		Возврат;	
	КонецЕсли;
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	НастройкиЗаголовка = СправочникОбъект.НастройкиЗаголовка();
	Если НЕ ЗначениеЗаполнено(НастройкиЗаголовка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокВысота = НастройкиЗаголовка.Height;
	ЗаголовокШирина = НастройкиЗаголовка.Width;
	ЗаголовокКоординатаХ = НастройкиЗаголовка.CoordX;
	ЗаголовокКоординатаУ = НастройкиЗаголовка.CoordY;
	ЗаголовокШрифт = НастройкиЗаголовка.Font;
	ЗаголовокРазмер = НастройкиЗаголовка.Size;
	ЗаголовокВыравнивание = НастройкиЗаголовка.Alignment;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройки()
	
	Если Объект.КоличествоСтрокТабло = 0 Тогда
		Возврат;				
	КонецЕсли;
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	НастройкиСтрок = СправочникОбъект.НастройкиЗаполненияСтрок();
	Если НЕ ЗначениеЗаполнено(НастройкиСтрок) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаНастроек Из НастройкиСтрок Цикл
		
		НоваяНастройка = ТаблицаНастройкаТабло.Добавить();
		
		НоваяНастройка.ВысотаСтроки = СтрокаНастроек.Height;
		НоваяНастройка.Ширина = СтрокаНастроек.Width;
		НоваяНастройка.КоординатаХ = СтрокаНастроек.CoordX;
		НоваяНастройка.КоординатаУ = СтрокаНастроек.CoordY;
		НоваяНастройка.Шрифт = СтрокаНастроек.Font;
		НоваяНастройка.РазмерШрифта = СтрокаНастроек.Size;
		НоваяНастройка.Выравнивание = СтрокаНастроек.Alignment;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПроверитьКорректностьНастроекСтрокТабло(Отказ)
	КоличествоСтрокНастроек = ТаблицаНастройкаТабло.Количество();
	Если КоличествоСтрокНастроек > 1 И КоличествоСтрокНастроек <> Объект.КоличествоСтрокТабло Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Количество строк настроек в табличной части 
		| должно равняться 0, 1 или реквизиту Количество строк табло'");
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтправитьНаТаблоНаСервере()
	
	Отказ = Ложь;
	
	ОбъектКОбмену = Объект.Ссылка.ПолучитьОбъект();
	ОбъектКОбмену.ДополнительныеСвойства.Вставить("ЭтоТест", Истина);
	гкс_ОчередьСообщенийRMQ.СоздатьСообщенияПоСобытиюОбъекта("ЗаписьОбъекта", ОбъектКОбмену);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовокТаблоНаСервере()
	
	Объект.Заголовок = РеквизитФормыВЗначение("Объект").ПолучитьЗаголовокТабло(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормой()
	Элементы.ГруппаАвтоЗаголовок.Видимость = Объект.ЕстьЗаголовок;
	Элементы.ГруппаПараметрыЗаголовока.Видимость = Объект.ЕстьЗаголовок;
	Элементы.НастройкаТабло.ОтборСтрок = Новый ФиксированнаяСтруктура("ЭтоЗаголовок", Ложь);
	УстановитьВидимостьЗаголовка();
	УстановитьВидимостьКолонокТаблицыНастроек(Объект.АвтоРаспределениеСтрок);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьЗаголовка()
	Элементы.Заголовок.Видимость = Не Объект.Автозаголовок;
	Элементы.СформироватьЗаголовокТабло.Видимость = Не Объект.Автозаголовок;
КонецПроцедуры

// Гкс+[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:
&НаКлиенте
Процедура УстановитьВидимостьКолонокТаблицыНастроек(АвтоРаспределение)
	Элементы.ТаблицаНастройкаТаблоВысотаСтроки.Видимость = Не АвтоРаспределение;
	Элементы.ТаблицаНастройкаТаблоКоординатаУ.Видимость = Не АвтоРаспределение;
КонецПроцедуры
// } Гкс-[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:

// Гкс+[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:
&НаСервере
Процедура ОчиститьКолонкиТаблицыНастроек()
	Для Каждого Строка Из ТаблицаНастройкаТабло Цикл
		Строка.ВысотаСтроки = 0;
		Строка.КоординатаУ = 0;
	КонецЦикла;
КонецПроцедуры
// } Гкс-[ERP-1505] Рудаков Н.А. от 25.01.2021. Добавлено:

&НаКлиенте
Процедура ЕстьЗаголовокПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТестовоеТаблоНаСервере()
	
	ОбъектТабло = РеквизитФормыВЗначение("Объект");
	ОбъектТабло.ЗаполнитьТаблоТестовымиДанными();
	ЗначениеВРеквизитФормы(ОбъектТабло, "Объект");

КонецПроцедуры

#КонецОбласти
