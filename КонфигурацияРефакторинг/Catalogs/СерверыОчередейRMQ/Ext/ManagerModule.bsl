#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Создает элемент справочника со значениями "по умолчанию"
// и связывает его с предопределенным элементов "Основной".
Процедура СоздатьЭлементСервераПоУмолчаниюЕслиОнНеСоздан() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСоздатьЭлементСервераПоУмолчаниюЕслиОнНеСоздан();
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		СерверПоУмолчанию = СоздатьЭлемент();
		
		СерверПоУмолчанию.Заполнить("ПОУМОЛЧАНИЮ");	
		СерверПоУмолчанию.Записать();	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция - Получить параметры сервера по переданному псевдониму/ключу соединения
//
// Параметры:
//  Псевдоним	 - Строка - псевдоним/ключ соединения
// 
// Возвращаемое значение: 
//  Структура   - ключи: АдресСервера, ВиртуальныйХост, Порт, Логин, Пароль, БезопасноеСоединение, ПутьКФайлуСертификатаУдостоверяющегоЦентра, ПутьКФайлуЗакрытогоКлюча, ПутьКФайлуСертификатаКлиента, ПроверкаСоответствияИмениСервера    
//
Функция ПолучитьПараметрыСервера(Знач Псевдоним) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СерверыОчередей.Ссылка КАК Ссылка,
	|	СерверыОчередей.АдресСервера КАК АдресСервера,
	|	ВЫБОР
	|		КОГДА СерверыОчередей.Порт = 0
	|			ТОГДА 5672
	|		ИНАЧЕ СерверыОчередей.Порт
	|	КОНЕЦ КАК Порт,
	|	ВЫБОР
	|		КОГДА СерверыОчередей.Логин = """"
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СерверыОчередей.Логин
	|	КОНЕЦ КАК Логин,
	|	СерверыОчередей.Пароль КАК Пароль,
	|	ВЫБОР
	|		КОГДА СерверыОчередей.ВиртуальныйХост = """"
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ СерверыОчередей.ВиртуальныйХост
	|	КОНЕЦ КАК ВиртуальныйХост,
	|	СерверыОчередей.БезопасноеСоединение КАК БезопасноеСоединение,
	|	СерверыОчередей.ПутьКФайлуСертификатаУдостоверяющегоЦентра КАК ПутьКФайлуСертификатаУдостоверяющегоЦентра,
	|	СерверыОчередей.ПутьКФайлуЗакрытогоКлюча КАК ПутьКФайлуЗакрытогоКлюча,
	|	СерверыОчередей.ПутьКФайлуСертификатаКлиента КАК ПутьКФайлуСертификатаКлиента,
	|	СерверыОчередей.ПроверкаСоответствияИмениСервера КАК ПроверкаСоответствияИмениСервера
	|ИЗ
	|	Справочник.СерверыОчередейRMQ КАК СерверыОчередей
	|ГДЕ
	|	СерверыОчередей.Код = &Псевдоним";
	
	Запрос.УстановитьПараметр("Псевдоним", Псевдоним);
	
	Результат = Запрос.Выполнить();
	Параметры = Новый Структура;
	Для Каждого Колонка Из Результат.Колонки Цикл
		Параметры.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Параметры, Выборка);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Функция - Возвращает ключ соединения по ссылке на сервер очереди
//
// Параметры:
//  СерверСсылка - СправочникСсылка.СерверыОчередейRMQ - ссылка на сервер очереди
// 
// Возвращаемое значение: 
//  Строка - ключ соединения или пустая строка, если не найден сервер
//
Функция КлючСоединения(Знач СерверСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СерверыОчередей.Код КАК Код
	|ИЗ
	|	Справочник.СерверыОчередейRMQ КАК СерверыОчередей
	|ГДЕ
	|	СерверыОчередей.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", СерверСсылка);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		Рез = Выборка.Код;
	Иначе
		Рез = "";
	КонецЕсли;
	
	Возврат Рез;
	
КонецФункции

// Функция - Возвращает ключ соединения с основным сервером очередей
// 
// Возвращаемое значение:
//  Строка - Ключ соединения
//
Функция КлючСоединенияОсновной() Экспорт
	Возврат КлючСоединения(Справочники.СерверыОчередейRMQ.Основной);	
КонецФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаСоздатьЭлементСервераПоУмолчаниюЕслиОнНеСоздан()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СерверыОчередейRMQ.Ссылка КАК Сервер
	|ИЗ
	|	Справочник.СерверыОчередейRMQ КАК СерверыОчередейRMQ
	|ГДЕ
	|	СерверыОчередейRMQ.Предопределенный
	|	И СерверыОчередейRMQ.ИмяПредопределенныхДанных = ""Основной""";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли