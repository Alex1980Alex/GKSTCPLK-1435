#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("Код");
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	СписокТиповТранспортаСНомерами = Новый Массив;
	СписокТиповТранспортаСНомерами.Добавить(Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЖДТранспорт);
	СписокТиповТранспортаСНомерами.Добавить(Перечисления.гкс_ТипыТранспортныхСредствДоставки.Автомобиль);

	Если СписокТиповТранспортаСНомерами.Найти(ВидПеревозки) <> Неопределено Тогда
		ПроверяемыеРеквизиты.Добавить("НомерТранспортногоСредства");
	КонецЕсли;
		
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьРеквизитыПередЗаписью();
	ПроверитьНомерВагона(Отказ);
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ЭтоНовый() И Не ЗначениеЗаполнено(Код) Тогда
		УстановитьНовыйКод();
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		
		ТекстИсключения = НСтр("ru='Поле ""Наименование"" не заполнено';en='The Name field is not filled'");
		ВызватьИсключение ТекстИсключения; 
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтоСправочникПЛК = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыиФункции

Процедура ЗаполнитьРеквизитыПередЗаписью()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидПеревозки = Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЖДТранспорт Тогда
		Наименование = НомерТранспортногоСредства;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ЭтоСправочникПЛК = Истина;
	КонецЕсли;
	
	ЗаполнитьПолеПоискаГосНомер();
	
	НормироватьНомерТранспортногоСредства();
		
КонецПроцедуры

Процедура НормироватьНомерТранспортногоСредства()
	
	Если НЕ ЗначениеЗаполнено(НомерТранспортногоСредства) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВидПеревозки = Перечисления.гкс_ТипыТранспортныхСредствДоставки.Автомобиль Тогда
    	Возврат;
	КонецЕсли;
	
	НомерТранспортногоСредства = СокрЛП(НомерТранспортногоСредства);
	НомерТранспортногоСредства = СтрЗаменить(НомерТранспортногоСредства, " ", "");
	
КонецПроцедуры

Процедура ЗаполнитьПолеПоискаГосНомер()
	
	СтрокаПреобразования = НомерТранспортногоСредства;
	Если НЕ ЗначениеЗаполнено(СтрокаПреобразования) Тогда
		СтрокаПреобразования = Наименование;		
	КонецЕсли;
	
	ПолеПоискаГосНомер = 
			Справочники.ТранспортныеСредства.ПолучитьГосНомерДляПоискаПоСтроке(СтрокаПреобразования);
	
КонецПроцедуры

Процедура ПроверитьНомерВагона(Отказ)
	
	Если НЕ ВидПеревозки = Перечисления.гкс_ТипыТранспортныхСредствДоставки.ЖДТранспорт Тогда
		Возврат;
	КонецЕсли;
	
	ДлинаНомераТС = 8;
	
	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерТранспортногоСредства) Тогда
		ОбщегоНазначения.СообщитьПользователю(
		НСтр("ru='В номере транспортного средства должны быть только цифры';"),,,, Отказ);
		
		Возврат;
	КонецЕсли;
	
	Если НЕ СтрДлина(НомерТранспортногоСредства) = ДлинаНомераТС Тогда
		ОбщегоНазначения.СообщитьПользователю(
		СтрШаблон(
		НСтр("ru='В номере транспортного средства должно быть %1 цифр';"), ДлинаНомераТС)
		,,,, Отказ);
		
		Возврат;
	КонецЕсли;
	
	КонтрольнаяСумма = 0;
	
	Для Счетчик = 1 По ДлинаНомераТС - 1 Цикл
		
		Значение = Число(Сред(НомерТранспортногоСредства, Счетчик, 1));
		
		Если Счетчик % 2 = 0 Тогда
			Коэффициент = 1;
		Иначе 
			Коэффициент = 2;
		КонецЕсли;
		
		Значение = Значение * Коэффициент;
		Значение = Цел(Значение / 10) + Значение % 10;
		
		КонтрольнаяСумма = КонтрольнаяСумма + Значение;
		
	КонецЦикла;
	
	Если КонтрольнаяСумма % 10 = 0 Тогда
		КонтрольнаяЦифра = 0;
	Иначе
		КонтрольнаяЦифра = 10 - (КонтрольнаяСумма % 10);
	КонецЕсли;
	
	Если НЕ КонтрольнаяЦифра = Число(Сред(НомерТранспортногоСредства, ДлинаНомераТС, 1)) Тогда
		ОбщегоНазначения.СообщитьПользователю(
		СтрШаблон(
		НСтр("ru='Проверьте номер вагона, контрольная цифра %1';"), КонтрольнаяЦифра)
		,,,, Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#КонецЕсли
