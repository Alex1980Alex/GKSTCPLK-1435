#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения основных реквизитов переданной подписки на события
//
// Параметры:
//  Подписка - СправочникСсылка.ПодпискиНаОчередиСообщений - 
//
// Возвращаемое значение:
//  Структура - содержит значения (КлючЗначение) основных реквизитов подписки
//
Функция ДанныеПодписки(Знач Подписка) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеПодписки();
	Запрос.УстановитьПараметр("Ссылка", Подписка);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();

	Результат = Новый Структура("ИмяОчереди, КлючСоединения, Подписка");
	ЗаполнитьЗначенияСвойств(Результат, Выборка);

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Поиск подписчика по имени очереди
//
// Параметры:
//  ИмяОчереди - Строка - имя очереди, как в RMQ
//  КлючСоединения - Строка - имя сервера 
// 
// Возвращаемое значение:
//  СправочникСсылка.ПодпискиНаОчередиСообщений. ПустаяСсылка, если не найдено.
//
Функция НайтиПодпискуНаОчередь(Знач ИмяОчереди, Знач КлючСоединения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодпискиНаОчередиСообщений.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПодпискиНаОчередиСообщений КАК ПодпискиНаОчередиСообщений
		|ГДЕ
		|	ПодпискиНаОчередиСообщений.ИмяОчереди = &ИмяОчереди
		|	И ПодпискиНаОчередиСообщений.СерверОчередей.Код = &Код";
	
	Запрос.УстановитьПараметр("ИмяОчереди", ИмяОчереди);
	Запрос.УстановитьПараметр("Код", КлючСоединения);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Выборка.Следующий();

	Возврат Выборка.Ссылка;
	
КонецФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаДанныеПодписки()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВсеПодписки.ИмяОчереди КАК ИмяОчереди,
	|	ЕСТЬNULL(СерверыОчередейRMQ.Код, """") КАК КлючСоединения,
	|	ВсеПодписки.Ссылка КАК Подписка
	|ИЗ
	|	Справочник.ПодпискиНаОчередиСообщений КАК ВсеПодписки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерверыОчередейRMQ КАК СерверыОчередейRMQ
	|		ПО ВсеПодписки.СерверОчередей = СерверыОчередейRMQ.Ссылка
	|ГДЕ
	|	ВсеПодписки.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

