#Если ТолстыйКлиентОбычноеПриложение ИЛИ Сервер ИЛИ ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ПолучитьИсториюВыполненияЗаданияЗаПериод(Знач ИдентификаторЗадания, Знач ПериодОтбор = "ЧАС") Экспорт
	
	ОтборЗаданий = ПолучитьОтборЗаданийИсторииПоПараметрам(ИдентификаторЗадания, ПериодОтбор);
	ВсеФоновыеЗаданияПоОтбору = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий);
	
	ИсторияВыполнения = СформироватьТаблицуИсторияВыполненияПоОтобраннымЗаданиям(ВсеФоновыеЗаданияПоОтбору);
	
	Возврат ИсторияВыполнения;
	
КонецФункции

Функция СоздатьЗаданиеПоНастройкам(Знач Настройки) Экспорт
	
	ОбъектЗадание = СоздатьЭлемент();
	ОбъектЗадание.Заполнить(Настройки);
	
	ОбъектЗадание.Записать();
	
	Возврат ОбъектЗадание.Ссылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИнициализироватьТаблицуИсторияВыполнения()
	
	ИсторияВыполнения = Новый ТаблицаЗначений;
	ИсторияВыполнения.Колонки.Добавить("Начало", гкс_ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ИсторияВыполнения.Колонки.Добавить("Конец", гкс_ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ИсторияВыполнения.Колонки.Добавить("Состояние", гкс_ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ИсторияВыполнения.Колонки.Добавить("ОписаниеОшибки", гкс_ОбщегоНазначения.ОписаниеТипаСтрока(0));
	
	Возврат ИсторияВыполнения;
	
КонецФункции

Функция ПолучитьОтборЗаданийИсторииПоПараметрам(ИдентификаторЗадания, ПериодОтбор)
	
	Результат = Новый Структура;
	
	НачалоОтбор = ТекущаяДатаСеанса();
	Если ВРег(ПериодОтбор) = "ЧАС" Тогда
		НачалоОтбор = НачалоЧаса(НачалоОтбор);	
	КонецЕсли;
	
	Результат.Вставить("Начало", НачалоОтбор);
	Результат.Вставить("Ключ", ИдентификаторЗадания);
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьТаблицуИсторияВыполненияПоОтобраннымЗаданиям(ОтобранныеЗадания)
	
	ИсторияВыполнения = ИнициализироватьТаблицуИсторияВыполнения();
	
	Для Каждого ФЗ Из ОтобранныеЗадания Цикл
		
		ЗаписьИстории = ИсторияВыполнения.Добавить();
		
		ЗаписьИстории.Начало = ФЗ.Начало;
		ЗаписьИстории.Конец = ФЗ.Конец;
		ЗаписьИстории.Состояние = ФЗ.Состояние;
		
		ИнформацияОбОшибке = ФЗ.ИнформацияОбОшибке;
		Если НЕ Неопределено = ИнформацияОбОшибке Тогда
			ЗаписьИстории.ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КонецЕсли;

	КонецЦикла;
	
	Возврат ИсторияВыполнения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
