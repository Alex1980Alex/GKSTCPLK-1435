#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПрочитатьРасписаниеИзОбъекта(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИнициализироватьРеквизитыФормы();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьЗаголовокРасписания();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.РасписаниеЗадания = Новый ХранилищеЗначения(ТекущееРасписаниеЗадания, Новый СжатиеДанных(9));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РасписаниеЗадания(Команда)
	
	//@skip-warning
	ОбратныйВызов = Новый ОписаниеОповещения("ФормированиеРасписанияЗаданияЗавершение", ЭтотОбъект);
	
	ДиалогФормированияРасписания = Новый ДиалогРасписанияРегламентногоЗадания(ТекущееРасписаниеЗадания);
	ДиалогФормированияРасписания.Показать(ОбратныйВызов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИсториюВыполнения(Команда)
	ЗаполнитьИсториюВыполненияНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИсториюВыполненияНаКлиенте()
	
	Если Объект.Ссылка.Пустая() ИЛИ НЕ ЗначениеЗаполнено(Объект.РегламентноеЗадание) Тогда
	
		гкс_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		               НСтр("ru='Связанного задания не существует. Невозможно получить историю выполнения'"));
		Возврат;		
	
	КонецЕсли;
	
	ЗаполнитьИсториюВыполненияНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьРеквизитыФормы()
	ЗаполнитьТекущееРасписаниеЕслиОноНеЗадано();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокРасписания()
	Элементы.РасписаниеЗадания.Заголовок = Строка(ТекущееРасписаниеЗадания);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРасписаниеИзОбъекта(ТекущийОбъект)
	ТекущееРасписаниеЗадания = ТекущийОбъект.РасписаниеЗадания.Получить();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекущееРасписаниеЕслиОноНеЗадано()
	
	Если Неопределено = ТекущееРасписаниеЗадания Тогда
		ТекущееРасписаниеЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбратныеВызовы

&НаКлиенте
Процедура ФормированиеРасписанияЗаданияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Неопределено = Результат Тогда
		Возврат;
	КонецЕсли;
	
	ТекущееРасписаниеЗадания = Результат;
	УстановитьЗаголовокРасписания();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИсториюВыполненияНаСервере()
	ИсторияВыполнения.Загрузить(ПолучитьИсториюВыполненияЗадания(Объект.РегламентноеЗадание));
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИсториюВыполненияЗадания(ИДЗадания)
	
	Возврат 
	Справочники
	      .гкс_ЗаданияОбработкиОчередиВходящихСообщенийRMQ
	      .ПолучитьИсториюВыполненияЗаданияЗаПериод(ИДЗадания);
    
КонецФункции

#КонецОбласти

#КонецОбласти

