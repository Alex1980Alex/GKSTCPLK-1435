
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем СоставляющиеКлюча;
	
	Параметры.Свойство("КлючМаршрутизации", Ключ);
	  
	Если ЗначениеЗаполнено(Ключ) Тогда
		
		СоставляющиеКлюча = СтрРазделить(Ключ, ".");
		
	КонецЕсли;
	
	ЗаполнитьСписокУчастниковОбмена(СоставляющиеКлюча);
		
КонецПроцедуры

#КонецоБласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ОповеститьОВыборе(Ключ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокУчастниковОбмена(Знач СоставляющиеКлюча = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	УчастникиОбмена.Очистить();
	
	Если НЕ ЗначениеЗаполнено(СоставляющиеКлюча) Тогда		
		СоставляющиеКлюча = Новый Массив;	
	КонецЕсли;
	
	ЗначениеОтправлятьВсем = "all"; 
	
	Если НЕ СоставляющиеКлюча.Найти(ЗначениеОтправлятьВсем) = Неопределено Тогда		
		ОтправлятьВсем = Истина;		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВсеУчастники.Наименование КАК Наименование,
	|	ВсеУчастники.Наименование В (&МассивИмен) КАК Выбрано
	|ИЗ
	|	Справочник.гкс_УчастникиОбменаRMQ КАК ВсеУчастники
	|ГДЕ
	|	НЕ ВсеУчастники.ЭтаБаза";
	
	Запрос.УстановитьПараметр("МассивИмен", СоставляющиеКлюча);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл		
		УчастникиОбмена.Добавить(Выборка.Наименование, 
		                         Выборка.Наименование, 
								 Выборка.Выбрано);		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОтправлятьВсемПриИзменении(Элемент)
	
	Слово = "all";
	
	Если ОтправлятьВсем Тогда
		
		ДобавитьСловоВКлюч(Слово);
		
	Иначе
		
		УдалитьСловоИзКлюча(Слово);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСловоВКлюч(Слово)
	
	Составляющие = СтрРазделить(Ключ, ".", Ложь);
	Составляющие.Добавить(Слово);
	
	Если Составляющие.Количество() = 1 Тогда		
		Разделитель = "";
	Иначе		
		Разделитель = ".";		
	КонецЕсли;
	
	Ключ = СтрСоединить(Составляющие, Разделитель);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСловоИзКлюча(Слово)
	
	Составляющие = СтрРазделить(Ключ, ".", Ложь);
	
	Индекс = Составляющие.Найти(Слово);
	
	Если НЕ Индекс = Неопределено Тогда	
		Составляющие.Удалить(Индекс);			
	КонецЕсли;
		
	Ключ = СтрСоединить(Составляющие, ".");
	
КонецПроцедуры

&НаКлиенте
Процедура УчастникиОбменаПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.УчастникиОбмена.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Слово = ТекущиеДанные.Значение;
	
	Если ТекущиеДанные.Пометка Тогда
		
		ДобавитьСловоВКлюч(Слово);
		
	Иначе
		
		УдалитьСловоИзКлюча(Слово);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
