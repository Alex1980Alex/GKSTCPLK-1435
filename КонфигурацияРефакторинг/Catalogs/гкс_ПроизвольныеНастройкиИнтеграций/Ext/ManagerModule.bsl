#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// См.описание гкс_ОчередьСообщенийRMQВызовСервера.ПолучитьСвойстваПроизвольнойНастройки
//
Функция ПолучитьСвойства(СсылкаНаНастройку, ТипСвойств = Неопределено) Экспорт
	
	Если ТипСвойств = Неопределено ИЛИ ТипСвойств = "Свойства" Тогда
		Возврат Свойства(СсылкаНаНастройку);
	ИначеЕсли ТипСвойств = "ДопСвойства" Тогда
		Возврат ДопСвойства(СсылкаНаНастройку);
	ИначеЕсли ТипСвойств = "Все" Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Свойства", Свойства(СсылкаНаНастройку));
		Результат.Вставить("ДопСвойства", ДопСвойства(СсылкаНаНастройку));
		
		Возврат Результат;
		
	КонецЕсли;	
	
	Возврат Неопределено;
	
КонецФункции

// См.описание гкс_ОчередьСообщенийRMQВызовСервера.ЗаписатьСвойстваПроизвольнойНастройки
//
Процедура ЗаписатьСвойства(СсылкаНаНастройку, Свойства = Неопределено) Экспорт
	
	НастройкаОбъект = СсылкаНаНастройку.ПолучитьОбъект();

	НастройкаОбъект.Свойства.Очистить();
	НастройкаОбъект.ДопСвойства = Новый ХранилищеЗначения(Неопределено);
	
	Если НЕ Свойства = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(НастройкаОбъект, Свойства);
	КонецЕсли;
	
	НастройкаОбъект.Записать();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Свойства(СсылкаНаНастройку)
	
	Свойства = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПолучитьСвойства();
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаНастройку); 
	
	Выборка = Запрос.Выполнить().Выбрать();	
	Пока Выборка.Следующий() Цикл
		Свойства.Вставить(Выборка.Ключ, Выборка.Значение); 
	КонецЦикла;
	
	Возврат Свойства;
			
КонецФункции

Функция ДопСвойства(СсылкаНаНастройку)
	
	Возврат гкс_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаНастройку, "ДопСвойства").Получить();
	
КонецФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаПолучитьСвойства() 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПроизвольныеНастройки.Ключ КАК Ключ,
	|	ПроизвольныеНастройки.Значение КАК Значение
	|ИЗ
	|	Справочник.гкс_ПроизвольныеНастройкиИнтеграций.Свойства КАК ПроизвольныеНастройки
	|ГДЕ
	|	ПроизвольныеНастройки.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
