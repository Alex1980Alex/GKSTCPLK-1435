#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	гкс_ФормыОбъектовИАС.ПриСозданииНаСервереФормыСпискаСообщений(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьИдентификаторов();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ИзменениеСостоянийСообщений" И Параметр = "Справочник.ИсходящиеСообщения")
		ИЛИ ИмяСобытия = "ПовторноСформированныСообщенияПоОбъектамСобытий" Тогда
		Элементы.Список.Обновить();	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьИдентификаторы(Команда)

	Элементы.ПоказатьИдентификаторы.Пометка = НЕ Элементы.ПоказатьИдентификаторы.Пометка;
	УстановитьВидимостьИдентификаторов();
	 	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "Состояние" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Отбор = Новый Структура("Сообщение", Элементы.Список.ТекущаяСтрока);
				
		ОткрытьФорму("РегистрСведений.гкс_СостоянияИсходящихСообщений.ФормаСписка",
		Новый Структура("Отбор, ИсторияСообщения", Отбор, Истина),
		ЭтотОбъект,
		ЭтотОбъект.УникальныйИдентификатор,
		,
		,
		Новый ОписаниеОповещения("ОбновитьСписок", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьСписок(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьИдентификаторов()
	
	Элементы.ИдентификаторСообщения.Видимость = Элементы.ПоказатьИдентификаторы.Пометка;
	Элементы.КорневойИдентификаторСообщения.Видимость = Элементы.ПоказатьИдентификаторы.Пометка;
		
КонецПроцедуры

#КонецОбласти
