#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ТипИзмеряемойВеличины <> Перечисления.ТипыИзмеряемыхВеличин.Упаковка Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмерения");
		МассивНепроверяемыхРеквизитов.Добавить("Родитель");
		МассивНепроверяемыхРеквизитов.Добавить("КоличествоУпаковок");
		МассивНепроверяемыхРеквизитов.Добавить("НоменклатураМногооборотнаяТара");
		МассивНепроверяемыхРеквизитов.Добавить("ХарактеристикаМногооборотнаяТара");
		МассивНепроверяемыхРеквизитов.Добавить("ТипУпаковки");
		
		Если ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
			Или Не ЗначениеЗаполнено(ТипИзмеряемойВеличины) Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Числитель");
			МассивНепроверяемыхРеквизитов.Добавить("Знаменатель");
			
		КонецЕсли;
	
	КонецЕсли;
	
	ПроверитьКратностьЧисловыхРеквизитов(Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет корректность ввода числовых реквизитов. Защищает от ошибки SQL: Переполнение поля.
//
// Параметры:
//  Отказ	 - Булево	 - устанавливается значение Истина, если проверка не пройдена.
//
Процедура ПроверитьКратностьЧисловыхРеквизитов(Отказ)
	
	СообщениеКратностьБолее = НСтр("ru = 'Кратность более 10 000 000 / 1';
									|en = 'Multiplier more than 10 000 000 / 1'");
	СообщениеКратностьМенее = НСтр("ru = 'Кратность менее 1 / 99 999 999';
									|en = 'Multiplier less than 1 / 99 999 999'");
	Если ЗначениеЗаполнено(Знаменатель) Тогда
		Если Числитель / Знаменатель < 0.0000001 Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				СообщениеКратностьБолее,
				,
				"Объект.Знаменатель",
				,
				Отказ);
			
		ИначеЕсли Числитель / Знаменатель > 99999999 Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				СообщениеКратностьМенее,
				,
				"Объект.Числитель",
				,
				Отказ);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на сервере.'");
#КонецЕсли