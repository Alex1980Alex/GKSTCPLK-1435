#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
		
	ПолучитьДанныеОбъекта(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Модифицированность Тогда
		ЗаписатьДанныеОбъекта(ТекущийОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция РазрешеноРучноеРедактированиеСообщений()
	
	Возврат гкс_ОчередьСообщенийRMQ.РазрешеноРучноеРедактированиеСообщений();
	
КонецФункции

&НаСервере
Процедура ЗаписатьДанныеОбъекта(ТекущийОбъект)
	
	Сжатие = Новый СжатиеДанных(9);
	ДвоичныеДанные = гкс_ОбщегоНазначения.КонвертироватьСтрокуВДвоичныеДанныеFastInfoset(ДанныеОбъекта);
	ТекущийОбъект.ДанныеОбъекта = Новый ХранилищеЗначения(ДвоичныеДанные, Сжатие);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДанныеОбъекта(ТекущийОбъект)
	
	ДвоичныеДанные = ТекущийОбъект.ДанныеОбъекта.Получить();
	Если НЕ ДвоичныеДанные = Неопределено Тогда
		ЭтотОбъект.ДанныеОбъекта = гкс_ОбщегоНазначения.КонвертироватьДвоичныеДанныеFastInfosetВСтроку(ДвоичныеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступностьЭлементов()
	
	ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений = РазрешеноРучноеРедактированиеСообщений(); 
	
	ЭтотОбъект.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
	ЭтотОбъект.Элементы.ДанныеОбъекта.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСформированныеСообщения

&НаКлиенте
Процедура СформированныеСообщенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущееСообщение = Элементы.СформированныеСообщения.ТекущиеДанные;
	Если ТекущееСообщение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТекущееСообщение.Сообщение);
	
КонецПроцедуры

#КонецОбласти