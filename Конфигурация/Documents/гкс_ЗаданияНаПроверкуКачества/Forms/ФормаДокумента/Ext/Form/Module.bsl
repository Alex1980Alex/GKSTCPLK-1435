
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныйДокументПриИзменении(Элемент)
	
	ЗаполнитьТабличныеЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортныйДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	СтруктураОтбораПоЗаданию = Новый Структура;
	СтруктураОтбораПоЗаданию.Вставить("ТочкаМаршрута", Объект.ТочкаМаршрута);
	СтруктураОтбораПоЗаданию.Вставить("Номенклатура", Объект.Номенклатура);
	СтруктураОтбораПоЗаданию.Вставить("Дата", Объект.Дата);
	
	ПараметрыОткрытия = Новый Структура("СтруктураОтбораПоЗаданию", СтруктураОтбораПоЗаданию);
	
	ОткрытьФорму("Документ.гкс_ТранспортныйДокумент.ФормаВыбора", ПараметрыОткрытия, Элемент);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьКачественныеПоказатели(Команда)
	
	ЗаполнитьТабличнуюЧастьАнализыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаТранспортВыбратьВсе(Команда)
	
	УстановитьФлаги("ТранспортныеСредства", "Учитывать", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаТранспортСнятьВсе(Команда)
	
	УстановитьФлаги("ТранспортныеСредства", "Учитывать", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаАнализыВыбратьВсе(Команда)
	
	УстановитьФлаги("Анализы", "ВыполнитьАнализ", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаАнализыСнятьВсе(Команда)
	
	УстановитьФлаги("Анализы", "ВыполнитьАнализ", Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьТабличныеЧасти()
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) И 
		ЗначениеЗаполнено(Объект.ТочкаМаршрута) И 
		ЗначениеЗаполнено(Объект.ТранспортныйДокумент) Тогда
		ЗаполнитьТабличныеЧастиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличныеЧастиНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьТабличнуюЧастьТранспортныеСредства();
	ДокументОбъект.ЗаполнитьТабличнуюЧастьАнализы();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьАнализыНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьТабличнуюЧастьАнализы();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлаги(ИмяТаблицы, ИмяПоля, ЗначениеФлага)
	
	Для Каждого Строка Из Объект[ИмяТаблицы] Цикл
		Строка[ИмяПоля] = ЗначениеФлага;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды 

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти



