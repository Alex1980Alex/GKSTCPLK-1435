
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, ПараметрКоманды);
	
	Если ПараметрКоманды.Количество() > 1 Тогда
		ТекстАнализ = НСтр("ru = 'выделенных лабораторных анализов'");
	Иначе		
		ТекстАнализ = НСтр("ru = 'лабораторного анализа'");
	КонецЕсли;
	ТекстВопроса = СтрШаблон(НСтр("ru = 'Изменить статус %1 на ""Отбор пробы""?'"), ТекстАнализ);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(РезультатВопроса, МассивЛабАнализов) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда		
		
		Задание = ЗапуститьФоновоеИзменениеСтатусаНаСервере(МассивЛабАнализов, Новый УникальныйИдентификатор);
		
		НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		
		Обработчик = Новый ОписаниеОповещения("ПослеФоновогоИзменениеСтатуса", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Задание, Обработчик, НастройкиОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеФоновогоИзменениеСтатуса(Задание, ДополнительныеПараметры) Экспорт
	
	Если Задание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Задание.Статус = "Выполнено" Тогда
		
		ТекстСообщения = НСтр("ru = 'Изменение статуса успешно выполнено.'");
		ОбновитьПовторноИспользуемыеЗначения();
		
	ИначеЕсли Задание.Статус = "Ошибка" Тогда
		
		ТекстОшибки = НСтр("ru = 'Не удается выполнить изменение статуса.'");
		ТекстСообщения = ТекстОшибки 
			+ Символы.ПС 
			+ НСтр("ru = 'Техническая информация:'") 
			+ Задание.ПодробноеПредставлениеОшибки;
			
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ТекстСообщения", ТекстСообщения);
	Оповестить("ИзмененСтатусЛабораторногоАнализа", ПараметрыОповещения);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеИзменениеСтатусаНаСервере(Знач МассивЛабАнализов, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Изменение статуса лабораторного анализа'");
	
	ФоновоеЗадание = ДлительныеОперации.ВыполнитьВФоне(
		"Документы.гкс_ЛабораторныйАнализ.ВыполнитьИзменениеСтатусаЛабораторногоАнализаНаОтборПроб",
		МассивЛабАнализов, 
		ПараметрыВыполнения);
		
	Возврат ФоновоеЗадание;
	
КонецФункции

