
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает массив документов "Основание для движения запасов" по реестру
// которые уже прошли оформление. 
//
// Параметры:
//  РеестрНакладных - ДокументСсылка.гкс_РеестрНакладныхЗПП3 - ссылка на документ, по которому необходимо 
//  														   получить данные.
//
// Возвращаемое значение:
//  Результат - Массив - массив документов "Основание для движения запасов"
//
Функция ОформленныеОснованияДвиженияЗапасовРеестра(РеестрНакладных) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОформленныеПартии.ОснованиеДвиженияЗапасов КАК ОснованиеДвиженияЗапасов
	|ИЗ
	|	РегистрСведений.гкс_ОформленныеПартии КАК ОформленныеПартии
	|ГДЕ
	|	ОформленныеПартии.РеестрНакладных = &Ссылка
	|	И НЕ ОформленныеПартии.ОснованиеДвиженияЗапасов = ЗНАЧЕНИЕ(Документ.гкс_ОснованиеДляДвиженияЗапасов.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("Ссылка", РеестрНакладных);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда	
		ДанныеЗапроса = РезультатЗапроса.Выгрузить();
		Результат = ДанныеЗапроса.ВыгрузитьКолонку("ОснованиеДвиженияЗапасов");
	Иначе
		Результат = Неопределено;
	КонецЕсли;

	Возврат Результат;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ДокументРегистрации.Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Процедура добавляет запись в регистр по переданным значениям структуры.
Процедура ДобавитьЗапись(Запись, Загрузка = Ложь) Экспорт
	
    ОбменДаннымиСлужебный.ДобавитьЗаписьВРегистрСведений(Запись, "гкс_ОформленныеПартии", Загрузка);
	
КонецПроцедуры

// Удаляет набор записей в регистре по переданным значениям структуры.
//
// Параметры:
//  Запись - Структура - структура, по значениям которой необходимо удалить набор записей.
//  Загрузка - Булево - признак, выполнять загрузку в режиме обмена или нет
// 
Процедура УдалитьЗапись(Запись, Загрузка = Ложь) Экспорт
	
    ОбменДаннымиСлужебный.УдалитьНаборЗаписейВРегистреСведений(Запись, "гкс_ОформленныеПартии", Загрузка);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти
	
#КонецЕсли