#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает количество образцов проб по параметрам
// Номенклатура - СправочникСсылка.Номенклатура
// ТочкаМаршрута - СправочникСсылка.гкс_ТочкиМаршрута
// ВидПеревозки - ПеречислениеСсылка.гкс_ТипыТранспортныхСредствДоставки
// ВидПробы - ПеречислениеСсылка.гкс_НазначенияИспользованияКачества, ПеречислениеСсылка.гкс_ТипыПроб
// Возвращаемое значение:
// - Количество
Функция КоличествоОбразцовПробПоПараметрамОтбора(Номенклатура, ТочкаМаршрута, ВидПеревозки, ВидАнализа) Экспорт
	
	КоличествоПроб = 0;
	
	Запрос = Новый Запрос;             
	Если ТипЗнч(ВидАнализа) = Тип("ПеречислениеСсылка.гкс_ТипыПроб") Тогда
		ТипПробы = ВидАнализа;                       
	Иначе
		ТипПробы = ТипПробыПоНазначениюИспользованияАнализа(ВидАнализа);
	КонецЕсли;	
		
	Запрос.Текст = ТекстЗапросаНастройкиФормирвоанияПроб();
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ВидПеревозки", ВидПеревозки);
	Запрос.УстановитьПараметр("ТипПробы", ТипПробы);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КоличествоПроб = Выборка.КоличествоЭтикеток; 
	КонецЕсли;	
	
	Возврат КоличествоПроб;
	
КонецФункции	

#КонецОбласти                                              

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаНастройкиФормирвоанияПроб()
	Возврат
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НастройкиПечатиФормированияПроб.КоличествоЭтикеток КАК КоличествоЭтикеток,
		|	НастройкиПечатиФормированияПроб.ОбъемПробы КАК ОбъемПробы,
		|	НастройкиПечатиФормированияПроб.ГОСТ КАК ГОСТ
		|ИЗ
		|	РегистрСведений.гкс_НастройкиПечатиФормированияПроб КАК НастройкиПечатиФормированияПроб
		|ГДЕ
		|	НастройкиПечатиФормированияПроб.ТочкаМаршрута = &ТочкаМаршрута
		|	И НастройкиПечатиФормированияПроб.Номенклатура = &Номенклатура
		|	И НастройкиПечатиФормированияПроб.ВидПеревозки = &ВидПеревозки
		|	И НастройкиПечатиФормированияПроб.ТипПробы = &ТипПробы";
	
КонецФункции  

Функция ТипПробыПоНазначениюИспользованияАнализа(ВидАнализа)
	
	Если ВидАнализа = Перечисления.гкс_НазначенияИспользованияКачества.Приемка Тогда
		ТипПробы = Перечисления.гкс_ТипыПроб.Единичная;
	Иначе
		ТипПробы = Перечисления.гкс_ТипыПроб.Композитная;
	КонецЕсли;
	
	Возврат ТипПробы;
	
КонецФункции	
#КонецОбласти                                              

#КонецЕсли
