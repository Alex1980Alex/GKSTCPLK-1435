#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает настройки обмена для переданного формата в виде структуры.
// Свойства структуры и их значения совпадают с измерениями регистра "РегистрСведений.гкс_НастройкиФорматовОбменаED"
// 
// Параметры:
//  Формат - СправочникСсылка.гкс_ФорматыОбменаИАС -
//  ДатаСреза - Дата - 
//
// Возвращаемое значение:
//  Структура  - преобразованный срез настроек 
//  Неопределено - настройки для формата не заданы
// 
Функция ПолучитьНастройки(Формат, ДатаСреза = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаНастройкиИнтеграционногоАдаптера();
	Запрос.УстановитьПараметр("Формат", Формат);
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат гкс_ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Выборка);		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьНастройкиФорматаДляФормированияКлюча(Формат) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПолучитьНастройкиФорматаДляФормированияКлюча();
	Запрос.УстановитьПараметр("Формат", Формат);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат гкс_ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Выборка);		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура УдалитьЗапись(Формат) Экспорт
	
	Отбор = Новый Структура("Формат", Формат);
	гкс_ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(Отбор, "гкс_НастройкиФорматовОбменаED"); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаПолучитьНастройкиФорматаДляФормированияКлюча()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаНастроек.Период КАК Период,
	|	ТаблицаНастроек.Формат КАК Формат
	|ИЗ
	|	РегистрСведений.гкс_НастройкиФорматовОбменаED КАК ТаблицаНастроек
	|ГДЕ
	|	ТаблицаНастроек.Формат = &Формат";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаНастройкиИнтеграционногоАдаптера()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкиУниверсальногоОбмена.МенеджерЗагрузки КАК МенеджерЗагрузки,
	|	НастройкиУниверсальногоОбмена.МенеджерВыгрузки КАК МенеджерВыгрузки,
	|	ЕСТЬNULL(Модели.Ссылка, ЗНАЧЕНИЕ(Справочник.гкс_МоделиДанныхXDTO.ПустаяСсылка)) КАК МодельДанных,
	|	ЕСТЬNULL(Модели.ПространствоИмен, """") КАК ПространствоИмен,
	|	НастройкиУниверсальногоОбмена.Период КАК Период
	|ИЗ
	|	РегистрСведений.гкс_НастройкиФорматовОбменаED.СрезПоследних(&ДатаСреза, Формат = &Формат) КАК
	|		НастройкиУниверсальногоОбмена
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.гкс_МоделиДанныхXDTO КАК Модели
	|		ПО НастройкиУниверсальногоОбмена.МодельДанных = Модели.Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
