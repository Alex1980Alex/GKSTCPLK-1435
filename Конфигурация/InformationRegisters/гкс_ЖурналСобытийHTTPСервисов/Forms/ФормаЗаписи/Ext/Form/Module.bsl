
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПолныеПрава = Пользователи.ЭтоПолноправныйПользователь(, Истина);
	
	Если Не ЕстьПолныеПрава Тогда 
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьЗапрос(Команда)
	
	ИмяКоманды = Команда.Имя;
	ОбработатьКомандуОтображения(ИмяКоманды);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОтвет(Команда)
	
	ИмяКоманды = Команда.Имя;
	ОбработатьКомандуОтображения(ИмяКоманды);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Сжатие9 = Новый СжатиеДанных(9);
 			
	ТекущийОбъект.ХранилищеЗапроса = Новый ХранилищеЗначения(ТекстЗапроса, Сжатие9);	
	ТекущийОбъект.ХранилищеОтвета = Новый ХранилищеЗначения(ТекстОтвета, Сжатие9);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьКомандуОтображения(ИмяКоманды)
	
	Если ИмяКоманды = "ПоказатьЗапрос" Тогда	
		ИзвлечьТекстЗапроса();
		Элементы.ГруппаЗапрос.Заголовок = 
			НСтр("ru = 'Запрос'; en = 'Request'") + "("+ РазмерСтрокой(СтрДлина(ТекстЗапроса)) + ")";
		Элементы.ГруппаЗапрос.Видимость = Истина;
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаЗапрос;
		Элементы.ФормаПоказатьЗапрос.Доступность = Ложь;
	ИначеЕсли  ИмяКоманды = "ПоказатьОтвет" Тогда 
		ИзвлечьТекстОтвета();
		Элементы.ГруппаОтвет.Заголовок = 
			НСтр("ru = 'Ответ'; en = 'Response'") + "("+ РазмерСтрокой(СтрДлина(ТекстОтвета)) + ")";
		Элементы.ГруппаОтвет.Видимость = Истина;
		Элементы.Страницы.ТекущаяСтраница = Элементы.ГруппаОтвет;
		Элементы.ФормаПоказатьОтвет.Доступность = Ложь;
	КонецЕсли; 
		
КонецПроцедуры 
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИзвлечьТекстЗапроса()
	
	Объект = РеквизитФормыВЗначение("Запись");
	ТекстЗапроса = Объект.ХранилищеЗапроса.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ИзвлечьТекстОтвета()
	
	Объект = РеквизитФормыВЗначение("Запись");
	ТекстОтвета = Объект.ХранилищеОтвета.Получить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазмерСтрокой(Размер)
	
	ПредельнаяДлина = 1024;
	КБ = ПредельнаяДлина;
	МБ = ПредельнаяДлина * ПредельнаяДлина;
	ГБ = ПредельнаяДлина * ПредельнаяДлина * ПредельнаяДлина;
	
	Если Размер >= 10 * КБ Тогда
		Возврат "" + Цел(Размер / КБ) + "К";
	ИначеЕсли Размер >= 10 * МБ Тогда
		Возврат "" + Цел(Размер / МБ) + "M";
	ИначеЕсли Размер >= 10 * ГБ Тогда
		Возврат "" + Цел(Размер / ГБ) + "G";
	КонецЕсли;
	
	Возврат Строка(Размер);
	
КонецФункции

#КонецОбласти