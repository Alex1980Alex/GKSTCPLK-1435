#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗафиксироватьПроблемуОбработкиОбъекта(Знач СтруктураПроблемы) Экспорт
	
	ТипПроблемыПоУмолчанию = Справочники.гкс_ТипыПроблемОбменаДанными.Неструктурированная;
	
	Если НЕ СтруктураПроблемы.Свойство("Период") Тогда		
		СтруктураПроблемы.Вставить("Период", ТекущаяДатаСеанса());		
	КонецЕсли;
	
	Если НЕ СтруктураПроблемы.Свойство("ТипПроблемы") Тогда		
		СтруктураПроблемы.Вставить("ТипПроблемы", ТипПроблемыПоУмолчанию);	
	КонецЕсли;
	
	гкс_ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураПроблемы, "гкс_ПроблемыОбменаДанными");
		
КонецПроцедуры

Процедура ОчиститьПроблемыОбъекта(Объект) Экспорт
	
	СтрукутраЗаписи = Новый Структура;
	СтрукутраЗаписи.Вставить("ПроблемныйОбъект", Объект);
	
	гкс_ОбменДаннымиСервер.УдалитьНаборЗаписейВРегистреСведений(СтрукутраЗаписи,
	                                                            "гкс_ПроблемыОбменаДанными");
	
КонецПроцедуры

Функция ЕстьПроблемыПоОбъекту(ПроверяемыйОбъект) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьПроблемы
		|ИЗ
		|	РегистрСведений.гкс_ПроблемыОбменаДанными КАК гкс_ПроблемыОбменаДанными
		|ГДЕ
		|	гкс_ПроблемыОбменаДанными.ПроблемныйОбъект = &ПроблемныйОбъект";
	
	Запрос.УстановитьПараметр("ПроблемныйОбъект", ПроверяемыйОбъект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

Функция СтруктураЗаписи() Экспорт
	
	ОписаниеЗаписи = Новый Структура;
	
	ОписаниеЗаписи.Вставить("ПроблемныйОбъект");
	ОписаниеЗаписи.Вставить("Текст");
	ОписаниеЗаписи.Вставить("ТипПроблемы");
	ОписаниеЗаписи.Вставить("ПредставлениеОбъекта");

	Возврат ОписаниеЗаписи;
	
КонецФункции

#КонецОбласти

#КонецЕсли
