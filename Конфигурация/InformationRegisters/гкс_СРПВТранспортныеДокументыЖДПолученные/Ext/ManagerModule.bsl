
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс 

Функция ДанныеЗаписиПоКлючуАналитики(КлючАналитикиСРПВ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТранспортныеДокументыЖДПолученные.КлючАналитикиСРПВ КАК КлючАналитикиСРПВ,
		|	ТранспортныеДокументыЖДПолученные.Прибытие КАК Прибытие,
		|	ТранспортныеДокументыЖДПолученные.ТранспортныйДокумент КАК ТранспортныйДокумент,
		|	ТранспортныеДокументыЖДПолученные.ДатаПрибытия КАК ДатаПрибытия,
		|	ТранспортныеДокументыЖДПолученные.Подобран КАК Подобран,
		|	ТранспортныеДокументыЖДПолученные.НомерВагона КАК НомерВагона,
		|	ТранспортныеДокументыЖДПолученные.Зарегистрирован КАК Зарегистрирован,
		|	ТранспортныеДокументыЖДПолученные.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрСведений.гкс_СРПВТранспортныеДокументыЖДПолученные КАК ТранспортныеДокументыЖДПолученные
		|ГДЕ
		|	ТранспортныеДокументыЖДПолученные.КлючАналитикиСРПВ = &КлючАналитикиСРПВ";
	
	Запрос.УстановитьПараметр("КлючАналитикиСРПВ", КлючАналитикиСРПВ);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Результат = Новый Структура;
	
	Для каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Результат.Вставить(Колонка.Имя, Выборка[Колонка.Имя]);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции	

#Область ДляВызоваИзДругихПодсистем

// Процедура добавляет запись в регистр по переданным значениям структуры.
Процедура ДобавитьЗапись(Запись, Загрузка = Ложь) Экспорт
	
    ОбменДаннымиСлужебный.ДобавитьЗаписьВРегистрСведений(Запись, "гкс_СРПВТранспортныеДокументыЖДПолученные", Загрузка);
	
КонецПроцедуры

// Удаляет набор записей в регистре по переданным значениям структуры.
//
// Параметры:
//  Запись - Структура - структура, по значениям которой необходимо удалить набор записей.
//  Загрузка - Булево - признак, выполнять загрузку в режиме обмена или нет
// 
Процедура УдалитьЗапись(Запись, Загрузка = Ложь) Экспорт
	
    ОбменДаннымиСлужебный.УдалитьНаборЗаписейВРегистреСведений(Запись, "гкс_СРПВТранспортныеДокументыЖДПолученные", Загрузка);
	
КонецПроцедуры

#КонецОбласти

// Возвращает текст запроса ДанныхДокументаДляПроведения.
//
// Возвращаемое значение:
// 	Строка - Текст запроса.
//
Функция ТекстЗапросаДанныхДокументаДляПроведения() Экспорт
	
	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ИдентификаторОтправки КАК КлючАналитикиСРПВ,
	|	&Период КАК ДатаПрибытия,
	|	&Ссылка КАК Прибытие,
	|	&НомерВагона КАК НомерВагона,
	|	&ТочкаМаршрута КАК ПЛК
	|ПОМЕСТИТЬ ВТ_ОсновныеДанныеПроведения
	|ИЗ
	|	Документ.гкс_СРПВПрибытиеНаПуть КАК СРПВПрибытиеНаПуть
	|ГДЕ
	|	СРПВПрибытиеНаПуть.Ссылка = &Ссылка
	|	И &Груженый
	|	И &Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерВагона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&НомерВагона КАК НомерВагона,
	|	ЖДНакладныеВагонаСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЖДНакладныеВагонаСрезПоследних.ЖДНакладная КАК ЖДНакладная,
	|	ЖДНакладныеВагонаСрезПоследних.ПунктРазгрузки
	|ПОМЕСТИТЬ ВТ_ЖДНакладныеПредварительно
	|ИЗ
	|	РегистрСведений.гкс_ЖДНакладныеВагона.СрезПоследних(&Период, НомерВагона = &НомерВагона
	|	И ДатаПогрузки <= &Период) КАК ЖДНакладныеВагонаСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПунктРазгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ЖДНакладныеПредварительно.НомерВагона КАК НомерВагона,
	|	ВТ_ЖДНакладныеПредварительно.Номенклатура КАК Номенклатура,
	|	ВТ_ЖДНакладныеПредварительно.ЖДНакладная КАК ЖДНакладная
	|ПОМЕСТИТЬ ВТ_ЖДНакладные
	|ИЗ
	|	ВТ_ЖДНакладныеПредварительно КАК ВТ_ЖДНакладныеПредварительно,
	|	РегистрСведений.гкс_СРПВСписокПунктовРазгрузки КАК СРПВСписокПунктовРазгрузки
	|ГДЕ
	|	ВТ_ЖДНакладныеПредварительно.ПунктРазгрузки В (СРПВСписокПунктовРазгрузки.ПунктРазгрузки)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ЖДНакладные.ЖДНакладная КАК ЖДНакладная,
	|	ВТ_ЖДНакладные.НомерВагона КАК НомерВагона,
	|	ВТ_ЖДНакладные.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТ_ЖДНакладныеДляПодбора
	|ИЗ
	|	ВТ_ЖДНакладные КАК ВТ_ЖДНакладные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гкс_СРПВТранспортныеДокументыЖДПолученные КАК ТранспортныеДокументыЖДПолученные
	|		ПО ВТ_ЖДНакладные.ЖДНакладная = ТранспортныеДокументыЖДПолученные.ТранспортныйДокумент
	|		И ВТ_ЖДНакладные.НомерВагона = ТранспортныеДокументыЖДПолученные.НомерВагона
	|ГДЕ
	|	ЕСТЬNULL(ТранспортныеДокументыЖДПолученные.КлючАналитикиСРПВ, ЗНАЧЕНИЕ(Справочник.гкс_КлючиАналитикиСРПВ.ПустаяСсылка)) = &ИдентификаторОтправки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ЖДНакладные.ЖДНакладная,
	|	ВТ_ЖДНакладные.НомерВагона,
	|	ВТ_ЖДНакладные.Номенклатура
	|ИЗ
	|	ВТ_ЖДНакладные КАК ВТ_ЖДНакладные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.гкс_СРПВТранспортныеДокументыЖДПолученные КАК ТранспортныеДокументыЖДПолученные
	|		ПО ВТ_ЖДНакладные.ЖДНакладная = ТранспортныеДокументыЖДПолученные.ТранспортныйДокумент
	|		И ВТ_ЖДНакладные.НомерВагона = ТранспортныеДокументыЖДПолученные.НомерВагона
	|ГДЕ
	|	ТранспортныеДокументыЖДПолученные.КлючАналитикиСРПВ ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерВагона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОсновныеДанныеПроведения.КлючАналитикиСРПВ КАК КлючАналитикиСРПВ,
	|	ВТ_ОсновныеДанныеПроведения.ДатаПрибытия КАК ДатаПрибытия,
	|	ВТ_ОсновныеДанныеПроведения.Прибытие КАК Прибытие,
	|	ВТ_ОсновныеДанныеПроведения.НомерВагона КАК НомерВагона,
	|	ЕСТЬNULL(ВТ_ЖДНакладныеДляПодбора.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(ВТ_ЖДНакладныеДляПодбора.ЖДНакладная, ЗНАЧЕНИЕ(Справочник.ТранспортныеСредства.ПустаяСсылка)) КАК
	|		ТранспортныйДокумент,
	|	ВЫБОР
	|		КОГДА ВТ_ЖДНакладныеДляПодбора.ЖДНакладная ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Подобран
	|ИЗ
	|	ВТ_ОсновныеДанныеПроведения КАК ВТ_ОсновныеДанныеПроведения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЖДНакладныеДляПодбора КАК ВТ_ЖДНакладныеДляПодбора
	|		ПО ВТ_ОсновныеДанныеПроведения.НомерВагона = ВТ_ЖДНакладныеДляПодбора.НомерВагона";
	
КонецФункции	

#КонецОбласти
	
#КонецЕсли