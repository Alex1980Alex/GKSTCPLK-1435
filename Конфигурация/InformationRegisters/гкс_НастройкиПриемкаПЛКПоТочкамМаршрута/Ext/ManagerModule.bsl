#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВсеНастройкиТочкиМаршрута(ТочкаМаршрута) Экспорт
	
	НастройкиПоУмолчанию = СтруктураНастроек();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаВсеНастройкиТочкиМаршрута();
	
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(НастройкиПоУмолчанию, Выборка);
	КонецЕсли;
	
	Возврат НастройкиПоУмолчанию;
	
КонецФункции

// Возвращает установленное в настройках для ПЛК время ожидания ТС
//
// Параметры:
//  ТочкаМаршрута - СправочникСсылка.гкс_ТочкиМаршрута - точка маршрута для которой получаем параметр
//
// Возвращаемое значение:
//   число - количество минут
//
Функция ВремяОжиданияТранспортногоСредства(ТочкаМаршрута) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиПриемкиПЛК.ВремяОжидания КАК ВремяОжидания
	|ИЗ
	|	РегистрСведений.гкс_НастройкиПриемкаПЛКПоТочкамМаршрута КАК НастройкиПриемкиПЛК
	|ГДЕ
	|	НастройкиПриемкиПЛК.ТочкаМаршрута = &ТочкаМаршрута";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
	    Результат = Выборка.ВремяОжидания;
	Иначе	
		Результат = 0;
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

Функция ИспользуетсяЭлектронноеТабло(ТочкаМаршрута) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	1 КАК Результат
	|ИЗ
	|	РегистрСведений.гкс_НастройкиПриемкаПЛКПоТочкамМаршрута КАК ТаблицаНастроек
	|ГДЕ
	|	ТаблицаНастроек.ТочкаМаршрута = &ТочкаМаршрута
	|	И ТаблицаНастроек.ИспользуютсяЭлектронныеТабло";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
		
КонецФункции

// Возвращает варианты использования электронного табло
//
// Параметры:
//  ТочкаМаршрута	 - СправочникСсылка.гкс_ТочкиМаршрута - точка маршрута
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.гкс_ВариантыИспользованияТаблоЭлектроннойОчереди -  вариант, указанный в настройках
//		Если в настройках вариант не указан возвращается вариант = ОтборПоНоменклатуре
//
Функция ПолучитьВариантИспользованияТаблоЭлектроннойОчереди(ТочкаМаршрута) Экспорт
	
	Результат = Перечисления.гкс_ВариантыИспользованияТаблоЭлектроннойОчереди.ОтборПоНоменклатуре;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаНастроек.ВариантИспользованияТаблоЭлектроннойОчереди КАК Результат
	|ИЗ
	|	РегистрСведений.гкс_НастройкиПриемкаПЛКПоТочкамМаршрута КАК ТаблицаНастроек
	|ГДЕ
	|	ТаблицаНастроек.ТочкаМаршрута = &ТочкаМаршрута";

	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат = ?(ЗначениеЗаполнено(Выборка.Результат), Выборка.Результат, Результат);
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ТочкаМаршрута)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураНастроек()
	
	СтруктураНастроек = Новый Структура;
	
	СтруктураНастроек.Вставить("ВремяОжидания");
	СтруктураНастроек.Вставить("СтарыйСтатус");
	СтруктураНастроек.Вставить("НовыйСтатус");
	СтруктураНастроек.Вставить("ПутьКДокументамСАРА");
	СтруктураНастроек.Вставить("ИспользуютсяЭлектронныеТабло");
	СтруктураНастроек.Вставить("ЗначениеДопустимогоОтклоненияБрутто");
	СтруктураНастроек.Вставить("АвтоматическоеФормированиеДокументов");
	СтруктураНастроек.Вставить("ВариантИспользованияТаблоЭлектроннойОчереди");
	
	Возврат СтруктураНастроек;
	
КонецФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаВсеНастройкиТочкиМаршрута()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.ВремяОжидания,
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.СтарыйСтатус,
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.НовыйСтатус,
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.ПутьКДокументамСАРА,
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.ИспользуютсяЭлектронныеТабло,
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.ЗначениеДопустимогоОтклоненияБрутто,
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.АвтоматическоеФормированиеДокументов,
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.ВариантИспользованияТаблоЭлектроннойОчереди
	|ИЗ
	|	РегистрСведений.гкс_НастройкиПриемкаПЛКПоТочкамМаршрута КАК гкс_НастройкиПриемкаПЛКПоТочкамМаршрута
	|ГДЕ
	|	гкс_НастройкиПриемкаПЛКПоТочкамМаршрута.ТочкаМаршрута = &ТочкаМаршрута";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

