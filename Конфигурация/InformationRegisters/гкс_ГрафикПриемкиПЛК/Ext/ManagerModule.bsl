
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Выбирает записи в графике приемки за период 
//
// Параметры:
//  Период - Структура - период с свойствами ДатаНачала и ДатаОкончания
//
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - записи графика приемки
//
Функция ЗаписиГрафикаЗаПериод(Период, УсловиеПроезда) Экспорт
	
	ЗапросГрафика = Новый Запрос;
	ЗапросГрафика.Текст = 
	"ВЫБРАТЬ
	|	гкс_ГрафикПриемкиПЛК.ТочкаМаршрута КАК ТочкаМаршрута,
	|	гкс_ГрафикПриемкиПЛК.УсловиеПроезда КАК УсловиеПроезда,
	|	гкс_ГрафикПриемкиПЛК.НачалоПериода КАК НачалоПериода,
	|	гкс_ГрафикПриемкиПЛК.ОкончаниеПериода КАК ОкончаниеПериода,
	|	гкс_ГрафикПриемкиПЛК.Номенклатура КАК Номенклатура,
	|	гкс_ГрафикПриемкиПЛК.Регистратор КАК Регистратор,
	|	гкс_ГрафикПриемкиПЛК.КоличествоТранспортныхСредствВЧас КАК КоличествоТранспортныхСредствВЧас,
	|	гкс_ГрафикПриемкиПЛК.КоличествоТранспортныхСредствВЧасПоЭО КАК КоличествоТранспортныхСредствВЧасПоЭО
	|ИЗ
	|	РегистрСведений.гкс_ГрафикПриемкиПЛК КАК гкс_ГрафикПриемкиПЛК
	|ГДЕ
	|	гкс_ГрафикПриемкиПЛК.НачалоПериода <= &ОкончаниеПериода
	|	И гкс_ГрафикПриемкиПЛК.ОкончаниеПериода >= &НачалоПериода
	|	И гкс_ГрафикПриемкиПЛК.УсловиеПроезда В(&УсловияПроезда)";
	
	ЗапросГрафика.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	ЗапросГрафика.УстановитьПараметр("ОкончаниеПериода", Период.ДатаОкончания);
	ЗапросГрафика.УстановитьПараметр("УсловияПроезда", гкс_ЭлектроннаяОчередь.ОтборПоУсловиямПроезда(УсловиеПроезда));
	
	РезультатЗапроса = ЗапросГрафика.Выполнить();
	
	ВыборкаГрафика = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаГрафика;
	
КонецФункции

Функция КоличествоТранспортныхСредствВЧас(Дата, Номенклатура = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				|	ГрафикПриемкиПЛК.КоличествоТранспортныхСредствВЧас КАК КоличествоТранспортныхСредствВЧас,
				|	МАКСИМУМ(ГрафикПриемкиПЛК.НачалоПериода) КАК НачалоПериода
				|ИЗ
				|	РегистрСведений.гкс_ГрафикПриемкиПЛК КАК ГрафикПриемкиПЛК
				|ГДЕ
				|	ГрафикПриемкиПЛК.Активность
				|	//ЕстьНоменклатура И ГрафикПриемкиПЛК.Номенклатура = &Номенклатура
				|	И ГрафикПриемкиПЛК.НачалоПериода <= &НачалоПериода
				|
				|СГРУППИРОВАТЬ ПО
				|	ГрафикПриемкиПЛК.КоличествоТранспортныхСредствВЧас
				|
				|УПОРЯДОЧИТЬ ПО
				|	НачалоПериода УБЫВ";
		
	Запрос.УстановитьПараметр("НачалоПериода", Дата);
	
	Если Номенклатура <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ЕстьНоменклатура", ""); 
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Возврат ВыборкаДетальныеЗаписи.КоличествоТранспортныхСредствВЧас;
	КонецЕсли;
	
КонецФункции

// Проверяет есть ли место в графике по заданным параметрам
//
// Параметры:
//  ДанныеГрафика - Структура - структура параметров
//
// Возвращаемое значение:
//  Булево - есть или нет.
//
Функция ЕстьСвободноеМесто(Период, ТочкаМаршрута, Номенклатура, УсловиеПроезда, График = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =  
	"ВЫБРАТЬ
	|	1 КАК ЕстьЗанятыеМеста
	|ИЗ
	|	РегистрСведений.гкс_ГрафикПриемкиПЛК КАК График
	|ГДЕ
	|	График.НачалоПериода < &ОкончаниеПериода
	|	И График.ОкончаниеПериода > &НачалоПериода
	|	И График.Активность
	|	И График.Номенклатура = &Номенклатура
	|	И График.ТочкаМаршрута = &ТочкаМаршрута
	|	И График.УсловиеПроезда В(&УсловияПроезда)";
		
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
    Запрос.УстановитьПараметр("УсловияПроезда", гкс_ЭлектроннаяОчередь.ОтборПоУсловиямПроезда(УсловиеПроезда));
		
	Если ЗначениеЗаполнено(График) Тогда 
		
		Запрос.УстановитьПараметр("Ссылка", График);		
		Запрос.Текст = Запрос.Текст + "
		|	И НЕ График.Регистратор = &Ссылка";
		
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции

// Проверяет заполнен ли график по заданным параметрам
//
// Параметры:
//  ДанныеГрафика - Структура - структура параметров
//
// Возвращаемое значение:
//  Булево - заполнен или нет.
//
Функция ГрафикЗаполнен(Период, ТочкаМаршрута, Номенклатура, УсловиеПроезда) Экспорт
	
	Запрос = Новый Запрос;
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК ГрафикСуществует
	|ИЗ
	|	РегистрСведений.гкс_ГрафикПриемкиПЛК КАК График
	|ГДЕ
	|	График.НачалоПериода <= &НачалоПериода
	|	И График.ОкончаниеПериода >= &ОкончаниеПериода
	|	И График.Активность
	|	И График.Номенклатура = &Номенклатура
	|	И График.ТочкаМаршрута = &ТочкаМаршрута
	|	И График.УсловиеПроезда В(&УсловияПроезда)";	
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ТочкаМаршрута", ТочкаМаршрута);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("УсловияПроезда", гкс_ЭлектроннаяОчередь.ОтборПоУсловиямПроезда(УсловиеПроезда));
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция СтатистикаПоТранспортнымСредствамДляЗаполнения(ДанныеОбъекта) Экспорт
	
	Результат = Новый Структура("КоличествоТранспортныхСредствВЧас, КоличествоТранспортныхСредствВЧасПоЭО", 0, 0);
	
	Запрос = Новый Запрос;	
	Запрос.Текст = ТекстЗапросаСтатистикаПоТранспортнымСредствамДляЗаполнения();
	
	Запрос.УстановитьПараметр("НачалоПериода", ДанныеОбъекта.НачалоПериода);
	Запрос.УстановитьПараметр("ТочкаМаршрута", ДанныеОбъекта.ТочкаМаршрута);	
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаСтатистикаПоТранспортнымСредствамДляЗаполнения()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	гкс_ГрафикПриемкиПЛК.КоличествоТранспортныхСредствВЧас КАК КоличествоТранспортныхСредствВЧас,
	|	гкс_ГрафикПриемкиПЛК.КоличествоТранспортныхСредствВЧасПоЭО КАК КоличествоТранспортныхСредствВЧасПоЭО
	|ИЗ
	|	РегистрСведений.гкс_ГрафикПриемкиПЛК КАК гкс_ГрафикПриемкиПЛК
	|ГДЕ
	|	гкс_ГрафикПриемкиПЛК.ТочкаМаршрута = &ТочкаМаршрута
	|	И гкс_ГрафикПриемкиПЛК.ОкончаниеПериода <= &НачалоПериода
	|
	|УПОРЯДОЧИТЬ ПО
	|	гкс_ГрафикПриемкиПЛК.ОкончаниеПериода УБЫВ";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли