#Область ПрограммныйИнтерфейс

// Метод вызывается в качестве асинхронного сервиса
//
Функция ОбработатьЗапросКСервису(Знач Запрос) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПространствоИменXDTO", Метаданные.ПакетыXDTO.гкс_AsynxExchangeServices.ПространствоИмен);
	
	ПроверитьКорректностьЗапроса(Запрос);
	
	Если ТипЗнч(Запрос) = Тип("Структура") Тогда   		
		Параметры.Вставить("Запрос", Запрос["Сообщение"]);		
	Иначе
		Параметры.Вставить("Запрос", Запрос);
	КонецЕсли;
		
	Менеджер = Обработки.гкс_ОбработчикСообщенийАсинхронногоСервиса;
	
	ОбработчикЗапроса = Менеджер.ПолучитьОбработчик(Параметры);
	ОбработчикЗапроса.Обработать();
	
	РезультатЗапрос = ОбработчикЗапроса.Результат();
	
	Возврат Менеджер.СериализоватьИзXDTO(РезультатЗапрос);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПрограммныйИнтерфейс

Функция ОбработатьЗапрос(Знач ПараметрыЗапроса) Экспорт
	
	Перем Запрос, ПространствоИмен;
	
	ПараметрыЗапроса.Свойство("Запрос", Запрос);
	ПараметрыЗапроса.Свойство("ПространствоИмен", ПространствоИмен);
	
	ПереопределяемыйМодуль = гкс_ОбщегоНазначения.ОбщийМодуль("гкс_АсинхронныеСервисыПереопределяемый");
	
	Попытка	
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(Запрос);
		ЧтениеXML.ПерейтиКСодержимому();
		Запрос = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		
		Результат = ПереопределяемыйМодуль.ОбработатьЗапрос(Запрос);
		Если Результат = Неопределено Тогда		
			Результат = СформироватьОшибку(НСтр("ru = 'Неизвестное сообщение!'"),
			Строка(Запрос.Тип()));
		КонецЕсли;
		
	Исключение		
		Инфо = ИнформацияОбОшибке();		
		Результат = СформироватьОшибку(НСтр("ru = 'Ошибка при обработке сообщения!'"),
		ПодробноеПредставлениеОшибки(Инфо));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьОшибку(Знач Тема, Знач Текст) Экспорт
	
	Менеджер = гкс_ОбщегоНазначения.ОбщийМодуль("Обработки.гкс_ОбработчикСообщенийАсинхронногоСервиса");
	ПереопределяемыйМодуль = гкс_ОбщегоНазначения.ОбщийМодуль("гкс_АсинхронныеСервисыПереопределяемый");

	Ошибка = ПереопределяемыйМодуль.СформироватьОшибку(Тема, Текст);
	Если НЕ Ошибка = Неопределено Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	Ошибка = Менеджер.СоздатьОбъект("ASError", Метаданные.ПакетыXDTO.гкс_AsynxExchangeServices.ПространствоИмен);	
	Ошибка.Description = Текст; 
	Ошибка.Subject = Тема;
	
	Возврат Ошибка;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьКорректностьЗапроса(Запрос) 
	
	ЭтоСтруктура = (ТипЗнч(Запрос) = Тип("Структура"));
	ЭтоСтрока = (ТипЗнч(Запрос) = Тип("Строка"));
	
	Если НЕ (ЭтоСтруктура ИЛИ ЭтоСтрока) Тогда
		ВызватьИсключение НСтр("ru='Ошибка формата запроса. Неподдерживаемый тип'");
	КонецЕсли;
	
	Если ЭтоСтруктура И НЕ Запрос.Свойство("Сообщение") Тогда
		 ВызватьИсключение НСтр("ru='Ошибка формата запроса. Не найдено поле ""Сообщение""'");
	КонецЕсли;
	 		
КонецПроцедуры

#КонецОбласти