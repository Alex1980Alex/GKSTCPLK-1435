
#Область ПрограммныйИнтерфейс

// Получить единицу измерения веса по-умолчанию в приемке.
// 
// Возвращаемое значение:
//  Произвольный, СправочникСсылка.УпаковкиЕдиницыИзмерения - установленное значение константы гкс_ЕдиницаИзмеренияВеса
Функция ПолучитьЕдиницуИзмеренияВесаПриемки() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Константы.гкс_ЕдиницаИзмеренияВеса.Получить();
	Возврат Результат;
	
КонецФункции

// Функция возвращает единицу измерения для траснпортного документа по-умолчанию - Тонна
// 
// Возвращаемое значение:
//	СправочникСсылка.УпаковкиЕдиницыИзмерения - единица измерения Тонна
Функция ПолучитьЕдиницуИзмеренияТранспортногоДокумента() Экспорт
	Возврат гкс_ОбщегоНазначенияВызовСервера.ПроизвольныйПредопределенныйОбъект("ЕдиницаИзмеренияТранспортногоДокумента");
КонецФункции

// Получить коэффициент пересчета одной ед.измерения в другую.
// 
// Параметры:
//  ЕдиницаДо - СправочникСсылка.УпаковкиЕдиницыИзмерения - Ед.изм До пересчета
//  ЕдиницаПосле - СправочникСсылка.УпаковкиЕдиницыИзмерения - Ед.изм После пересчета
// 
// Возвращаемое значение:
// 	Число - коэффициент пересчета ЕдиницыДо в ЕдиницуПосле
//	1 - если не задана одна из единиц или их типы измеряемой величины не совпадают 
Функция ПолучитьКоэффициентПересчетаЕдиниц(ЕдиницаДо, ЕдиницаПосле) Экспорт
	
	Коэффициент = 1;
	Если Не ЗначениеЗаполнено(ЕдиницаДо) Или НЕ ЗначениеЗаполнено(ЕдиницаПосле) Тогда
		Возврат Коэффициент;
	КонецЕсли;
	
	ИменаРеквизитов = "Числитель, Знаменатель, ТипИзмеряемойВеличины";
	РеквизитыДо = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЕдиницаДо, ИменаРеквизитов);
	РеквизитыПосле = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЕдиницаПосле, ИменаРеквизитов);
	
	Если НЕ РеквизитыДо.ТипИзмеряемойВеличины = РеквизитыПосле.ТипИзмеряемойВеличины Тогда
		Возврат Коэффициент;
	КонецЕсли;
		
	КоэффициентДо = ?(РеквизитыДо.Знаменатель = 0, 1, РеквизитыДо.Числитель / РеквизитыДо.Знаменатель);
	КоэффициентПосле = ?(РеквизитыПосле.Знаменатель = 0, 1, РеквизитыПосле.Числитель / РеквизитыПосле.Знаменатель);
	
	Возврат КоэффициентДо / КоэффициентПосле;	
	
КонецФункции

#КонецОбласти
