#Область ПрограммныйИнтерфейс

Функция Контекст() Экспорт
	
	ДанныеКонтекста = ДанныеКонтекста();
	Если ДанныеКонтекста = Неопределено Тогда
		ДанныеКонтекста = ИнициализироватьКонтекст();	
	КонецЕсли;
	
	Возврат ДанныеКонтекста;
	
КонецФункции

Функция ДанныеКонтекста(ВыбрасыватьИсключениеПриНеинициализированномКонтексте = Ложь) Экспорт
	
	Адрес = АдресКонтекста();
	
	Если ЭтоАдресВременногоХранилища(Адрес) Тогда
		Возврат ПолучитьИзВременногоХранилища(Адрес);
	КонецЕсли;
	
	Если ВыбрасыватьИсключениеПриНеинициализированномКонтексте Тогда
		ВызватьИсключение НСтр("ru='Контекст адаптера не инициализирован'");
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура УстановитьЗначениеКонтекста(Знач ИмяКонтекста, Знач Значение) Экспорт
	
	ДанныеКонтекста = ДанныеКонтекста();
	ДанныеКонтекста.Вставить(ИмяКонтекста, Значение);
	
КонецПроцедуры

Процедура УдалитьЗначениеКонтекстаПоИмени(Знач ИмяКонтекста) Экспорт
	
	Контекст = Контекст();
	Контекст.Удалить(ИмяКонтекста);
	
КонецПроцедуры

Процедура УдалитьКонтекст() Экспорт
	гкс_КонтекстОбщий.УдалитьКонтекст();
КонецПроцедуры

#Область РаботаСВидамиКонтекста

Функция ИмяКонтекстаПолученныхПотоков() 
	Возврат гкс_КонтекстОбщий.ИмяКонтекстаПолученныхПотоков();
КонецФункции

Процедура УдалитьЗначениеКонтекстаПолученныхПотоков() Экспорт
	
	Контекст = Контекст();
	Контекст.Удалить(ИмяКонтекстаПолученныхПотоков());
	
КонецПроцедуры

Процедура ДобавитьПотокВКонтекстПолученныхПотоков(Поток) Экспорт
		
	КонтекстПолученныхПотоков = ЗначениеКонтекста(ИмяКонтекстаПолученныхПотоков());
	Если КонтекстПолученныхПотоков = Неопределено Тогда
		
		КонтекстПолученныхПотоков = Новый Соответствие();
		КонтекстПолученныхПотоков.Вставить(Поток, Истина);
		
		УстановитьЗначениеКонтекста(ИмяКонтекстаПолученныхПотоков(), КонтекстПолученныхПотоков);
		
	КонецЕсли;
	
	гкс_ОбщегоНазначенияКлиентСервер.Проверить(ТипЗнч(КонтекстПолученныхПотоков) = Тип("Соответствие"));
	Если КонтекстПолученныхПотоков.Получить(Поток) = Неопределено Тогда
		КонтекстПолученныхПотоков.Вставить(Поток, Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолученныеПотокиИзКонтекста() Экспорт
	Возврат ЗначениеКонтекста(ИмяКонтекстаПолученныхПотоков());
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИнициализироватьКонтекст()
	
	Контекст = гкс_КонтекстОбщий.ОписаниеКонтекста();
	АдресХранилища = ПоместитьВоВременноеХранилище(Контекст, Новый УникальныйИдентификатор());
	
	СохранитьАдресКонтекста(АдресХранилища);
	
	Возврат Контекст;

КонецФункции

Функция АдресКонтекста()
	Возврат гкс_КонтекстОбщий.АдресКонтекста();
КонецФункции

Процедура СохранитьАдресКонтекста(АдресХранилища)
	гкс_КонтекстОбщий.СохранитьАдресКонтекста(АдресХранилища);
КонецПроцедуры

Функция ЗначениеКонтекста(ИмяКонтекста)
	
	ДанныеКонтекста = ДанныеКонтекста(Истина);
	Возврат гкс_ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеКонтекста, ИмяКонтекста);
	
КонецФункции

#КонецОбласти