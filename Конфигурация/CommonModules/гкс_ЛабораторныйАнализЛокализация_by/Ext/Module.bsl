#Область ПрограммныйИнтерфейс

#Область ПодключаемыеКоманды

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт возврата		
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор 	= "ПФ_MXL_АктВозврата_by";
	КомандаПечати.Представление 	= НСтр("ru = 'Акт возврата'");
    КомандаПечати.МенеджерПечати 	= "Обработка.гкс_ПечатьАктВозврата_Беларусь";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Обработчик  		= "гкс_УправлениеПечатьюКлиент_by.ВыполнитьКомандуПечати_АктВозврата";
		
КонецПроцедуры

#КонецОбласти

#Область Печать

// Функция получает данные для формирования Акта возврата Беларусь
// Возвращает данные, необходимые для печатной формы.
// 
// Параметры:
//	МассивОбъектов - Массив из ДокументСсылка.гкс_РегистрацияНаПЛК - Массив ссылок на документы РегистрацияНаПЛК, по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура:
//	 * РезультатПоШапке - РезультатЗапроса
//	 * РезультатПоТабличнойЧасти - РезультатЗапроса
// 
Функция ДанныеДляПечатнойФормыАктВозврата(МассивОбъектов) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЛабораторныйАнализ.Ссылка КАК ЛабАнализ,
	|	ЛабораторныйАнализ.Номер КАК НомерЛабАнализ,
	|	ЛабораторныйАнализ.Дата КАК ДатаЛабАнализ,
	|	РегистрацияНаПЛК.Водитель КАК Водитель,
	|	ЛабораторныйАнализ.Номенклатура.НаименованиеДляПечати КАК Номенклатура,
	|	РегистрацияНаПЛК.ТранспортноеСредство КАК ТранспортноеСредство,
	|	РегистрацияНаПЛК.ВесНетто КАК Количество,
	|	РегистрацияНаПЛК.НомерДокументаПоставщика КАК НомерТН,
	|	РегистрацияНаПЛК.Контрагент КАК Поставщик,
	|	ЕСТЬNULL(НормативнаяСертификацияНоменклатуры.ГОСТ, """") КАК НомерСТБ,
	|	ЕСТЬNULL(НормативнаяСертификацияНоменклатуры.Ссылка, """") КАК НормативнаяСертификация,
	|	ЕСТЬNULL(СправочникНоменклатура.ПродуктоваяГруппа, """") КАК ПродуктоваяГруппа
	|ПОМЕСТИТЬ ВТ_ОсновныеДанные
	|ИЗ
	|	Документ.гкс_ЛабораторныйАнализ КАК ЛабораторныйАнализ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.гкс_РегистрацияНаПЛК КАК РегистрацияНаПЛК
	|		ПО ЛабораторныйАнализ.гкс_ДокументРегистрации = РегистрацияНаПЛК.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_НормативнаяСертификацияНоменклатуры КАК НормативнаяСертификацияНоменклатуры
	|		ПО ЛабораторныйАнализ.Номенклатура = НормативнаяСертификацияНоменклатуры.Номенклатура
	|		И
	|			(НормативнаяСертификацияНоменклатуры.гкс_НазначениеИспользованияКачества = ЗНАЧЕНИЕ(Перечисление.гкс_НазначенияИспользованияКачества.Приемка))
	|		И
	|			(НормативнаяСертификацияНоменклатуры.гкс_ВидПеревозки = ЗНАЧЕНИЕ(Перечисление.гкс_ТипыТранспортныхСредствДоставки.Автомобиль))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ЛабораторныйАнализ.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ЛабораторныйАнализ.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЛабАнализ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ЛабораторныйАнализКачественныеПоказатели.Показатель, """") КАК Показатель,
	|	ЕСТЬNULL(ЛабораторныйАнализКачественныеПоказатели.Значение, """") КАК Значение,
	|	ВТ_ОсновныеДанные.ЛабАнализ КАК ЛабАнализ,
	|	ВТ_ОсновныеДанные.НормативнаяСертификация КАК НормативнаяСертификация
	|ПОМЕСТИТЬ ВТ_ЛабАнализ
	|ИЗ
	|	ВТ_ОсновныеДанные КАК ВТ_ОсновныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_ЛабораторныйАнализ.КачественныеПоказатели КАК ЛабораторныйАнализКачественныеПоказатели
	|		ПО ВТ_ОсновныеДанные.ЛабАнализ = ЛабораторныйАнализКачественныеПоказатели.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЛабАнализ.Показатель КАК Показатель,
	|	ЕСТЬNULL(НормативнаяСертификацияНоменклатурыАнализы.МаксЗначениеПоказателя, """") КАК НормЗначение,
	|	ВТ_ЛабАнализ.Значение КАК ФактЗначение,
	|	ВТ_ЛабАнализ.ЛабАнализ КАК ЛабАнализ
	|ИЗ
	|	ВТ_ЛабАнализ КАК ВТ_ЛабАнализ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.гкс_НормативнаяСертификацияНоменклатуры.Анализы КАК
	|			НормативнаяСертификацияНоменклатурыАнализы
	|		ПО ВТ_ЛабАнализ.НормативнаяСертификация = НормативнаяСертификацияНоменклатурыАнализы.Ссылка
	|		И ВТ_ЛабАнализ.Показатель = НормативнаяСертификацияНоменклатурыАнализы.ПоказательАнализа
	|ИТОГИ
	|ПО
	|	ЛабАнализ";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатПакетаЗапросов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
		
	Возврат Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти", 
							РезультатПакетаЗапросов[0], РезультатПакетаЗапросов[2]);
	
КонецФункции

#КонецОбласти

#КонецОбласти