#Область ПрограммныйИнтерфейс

// Выполняет чтение сообщения обмена и его последующую загрузку. 
// См. также Обработки.гкс_ЗагрузчикСообщенийRMQ.ПрочитатьСообщение
// 
// Параметры:
// 	Сообщение - Строка - текст сообщения
//	ПараметрыОбработки - Структура - структура, содержащая входящие вспомогательные данные для обработки. После 
//	обработки сообщения в нее помещается результат. Оставлено для совместимости.
//
Процедура ПрочитатьСообщение(Сообщение, ПараметрыОбработки) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("гкс_ИспользоватьИнтеграциюСРПВ") Тогда
		Возврат;	
	КонецЕсли;
								
	КомпонентыОбмена = гкс_ОбменДаннымиXDTOСервер.ИнициализироватьКомпонентыОбмена("Получение");
   	КомпонентыОбмена.СостояниеОбменаДанными.ДатаНачала = ТекущаяДатаСеанса();
	КомпонентыОбмена.ЭтоОбменЧерезПланОбмена = Ложь;
		
	ТипКонтракта = ПараметрыОбработки.Параметры.ТипКонтракта;
								
	СообщениеПолное = ПривестиСообщениеКВидуДляЧтения(Сообщение, ТипКонтракта);													
	ТипКонтрактаXDTO = гкс_ИнтеграцияСРПВ.ТипКонтрактаXDTO(ТипКонтракта);
	
	РезультатЧтенияКонтракта = ПривестиКТипуОбъекта(СообщениеПолное, ТипКонтрактаXDTO);
	
	Если РезультатЧтенияКонтракта.Отказ Тогда		
		гкс_ОбменДаннымиXDTOСервер.ЗаписатьВПротоколВыполнения(КомпонентыОбмена, РезультатЧтенияКонтракта.ТекстОшибки);	
		ВызватьИсключение РезультатЧтенияКонтракта.ТекстОшибки;
	КонецЕсли;	
		
	КомпонентыОбмена.Вставить("ОбъектXDTO", РезультатЧтенияКонтракта.Объект);	
	гкс_ИнтеграцияСРПВ.ОбработатьВходящееСообщение(РезультатЧтенияКонтракта.Объект, КомпонентыОбмена);
											
	Если КомпонентыОбмена.Свойство("ЗагруженныеОбъектыСеансаОбмена") 
		И ЗначениеЗаполнено(КомпонентыОбмена.ЗагруженныеОбъектыСеансаОбмена) Тогда
		
		ПараметрыОбработки.Вставить("ЗагруженныеОбъекты", КомпонентыОбмена.ЗагруженныеОбъектыСеансаОбмена);
	КонецЕсли;
	    		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет попытку приведения строки в формате JSON к указанному типу объекта XDTO.
// Параметры:
//  ТекстОбъекта - Строка - Строковое описание объекта в формате JSON.
//  ТипОбъекта   - ТипОбъектаXDTO - Тип объекта.
// 
// Возвращаемое значение:
//  Структура - Результат выполнения приведения.
//              Содержит ключи: Объект, Отказ, ТекстОшибки.
//
Функция ПривестиКТипуОбъекта(ТекстОбъекта, ТипОбъекта)

	РезультатПриведения = Новый Структура("Объект, Отказ, ТекстОшибки", Неопределено, Ложь, "");

	Попытка
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(ТекстОбъекта);
		РезультатПриведения.Объект = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON, ТипОбъекта);
		ЧтениеJSON.Закрыть();
	Исключение
		РезультатПриведения.Отказ = Истина;
		РезультатПриведения.ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;

	Возврат РезультатПриведения;

КонецФункции

Функция ПривестиСообщениеКВидуДляЧтения(ТекстСообщения, ТипКонтракта)
	
	ПространствоИмен = гкс_ИнтеграцияСРПВ.ОсновноеПространствоИмен();
		
	Возврат СтрШаблон("{""#ns"":""%1"", ""#type"":""%2"", ""#value"":%3}", 
						ПространствоИмен, ТипКонтракта, ТекстСообщения);
	
КонецФункции

#КонецОбласти