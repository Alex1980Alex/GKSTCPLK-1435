////////////////////////////////////////////////////////////////////////////////
// Электронная очередь клиент: клиентские процедуры и функции для работы с электронной очередью
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

Процедура ДанныеЭлектроннойЗаявкиWord(Форма, ОписаниеПомещенногоФайла, ОповещениеПослеОбработки) Экспорт
	
	ДлительнаяОперация = гкс_ЭлектроннаяОчередьСервер.ВыполнитьДлительнуюОперацию(Форма.УникальныйИдентификатор, 
		ОписаниеПомещенногоФайла.Адрес);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("Форма", Форма);
	ПараметрыОповещения.Вставить("ОповещениеОЗавершении", ОповещениеПослеОбработки);
	ПараметрыОповещения.Вставить("ПоказыватьСообщения", Истина);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗавершитьПолучениеДанныхЭлектроннойЗаявкиWord", 
		ЭтотОбъект, ПараметрыОповещения);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗавершитьПолучениеДанныхЭлектроннойЗаявкиWord(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		Если ДополнительныеПараметры.ПоказыватьСообщения Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить чтение файла документа заявки:
	                  |%1'"), Результат.КраткоеПредставлениеОшибки);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	Если ДополнительныеПараметры.ОповещениеОЗавершении <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
