#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда 

#Область ПрограммныйИнтерфейс

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов
//  ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати
//  КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм
//  ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати
//  ПараметрыВывода - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыВывода
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	СтруктураТипов = гкс_ОбщегоНазначенияПЛК.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_АктРасхожденияВеса_by") Тогда
		
		ТабличныйДокумент = 
			ПечатнаяФормаАктаРасхожденияВеса(СтруктураТипов, ОбъектыПечати, ПараметрыПечати);
		 
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПФ_MXL_АктРасхожденияВеса_by",
			НСтр("ru='Акт расхождения веса'"), 
			ТабличныйДокумент);
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#КонецОбласти	
	
#Область СлужебныеПроцедурыИФункции

#Область Печать_АктРасхожденияВеса

Функция ПечатнаяФормаАктаРасхожденияВеса(СтруктураТипов, ОбъектыПечати, ПараметрыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПФ_MXL_АктРасхожденияВеса_by";
	
	УстановитьПривилегированныйРежим(Истина);
			
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл

		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		МенеджерОбъекта = гкс_ЛокализацияСервер.МодульЛокализации(СтруктураОбъектов.Ключ);
		Если МенеджерОбъекта = Неопределено Тогда
			ТекстСообщения = НСтр("ru = 'Печатная форма «Акт расхождения веса» недоступна для текущей локализации. "
					+ "Обратитесь к администратору системы.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		ДанныеДляПечати = МенеджерОбъекта.
			ДанныеДляПечатнойФормыАктРасхожденияВеса(ПараметрыПечати, СтруктураОбъектов.Значение);
		
		Если Не ДанныеДляПечати = Неопределено Тогда
			ЗаполнитьТабличныйДокументАктРасхожденияВеса(ТабличныйДокумент, 
				ДанныеДляПечати, ОбъектыПечати, ПараметрыПечати);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

// Процедура заполняет табличный документ "акт расхождения веса"
//
Процедура ЗаполнитьТабличныйДокументАктРасхожденияВеса(ТабличныйДокумент, 
													   ДанныеДляПечати, ОбъектыПечати, ПараметрыПечати)	
	Если Не ДанныеДляПечати.Свойство("Разница") Или ДанныеДляПечати.Разница = 0 Тогда 
		Возврат;
	КонецЕсли;	
	
	Макет = УправлениеПечатью.МакетПечатнойФормы(
		"Обработка.гкс_ПечатьАктРасхожденияВеса.ПФ_MXL_АктРасхожденияВеса", 
		гкс_ЛокализацияСервер.КодЛокализацииБазы()); 
		
	ДанныеДополнительные = Новый Структура;
	ПараметрыПечати.Свойство("ДанныеПечати", ДанныеДополнительные);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ПараметрыОбласти = ОбластьШапка.Параметры;
	
	ПараметрыОбласти.Заполнить(ДанныеДляПечати);
	ПараметрыОбласти.Заполнить(ДанныеДополнительные);
	
	ПараметрыОбласти.Дата = Формат(ДанныеДляПечати.ДатаДокумента,"ДФ=dd.MM.yyyy");
	ПараметрыОбласти.НомерНакладной = СокрЛП(ДанныеДляПечати.НомерНакладной);
	ПараметрыОбласти.ДатаНакладной = Формат(ДанныеДляПечати.ДатаНакладной, "ДФ=dd.MM.yyyy");
		
	ПараметрыОбласти.ИзлишекНедостача	= ?(ДанныеДляПечати.Разница > 0, "(излишек)", "(недостача)");
	ПараметрыОбласти.Разница = ?(ДанныеДляПечати.Разница > 0, 1, -1) * ДанныеДляПечати.Разница;
		
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Если ДанныеДляПечати.ЭтоАвтомобиль Тогда
		ОбластьВодитель = Макет.ПолучитьОбласть("Водитель");
		ОбластьВодитель.Параметры.Водитель = ДанныеДляПечати.Водитель;
		ТабличныйДокумент.Вывести(ОбластьВодитель);
	КонецЕсли;

	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
