#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЗначенияЗаполнения") Тогда	
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры["ЗначенияЗаполнения"], "ТочкаМаршрута, ВидПеревозки, ТипРегистрации"); 
	КонецЕсли;
	
	ДатаУстановки = КонецДня(КонецДня(ТекущаяДатаСеанса()) + 1);
	
	ОбновитьСписокНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаУстановкиПриИзменении(Элемент)
	
	ОбновитьСписокНоменклатуры();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Установить(Команда)
	
	УстановитьНаСервере();
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокНоменклатуры()
	
	СписокНоменклатуры.Очистить();
	
	НоменклатураНаПЛК = гкс_ЭлектроннаяОчередь.РабочаяНоменклатураНаДату(ТипРегистрации, ВидПеревозки, ДатаУстановки);
	
	Если НоменклатураНаПЛК <> Неопределено Тогда	
		СписокНоменклатуры.Загрузить(НоменклатураНаПЛК);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьНаСервере()
	
	ПараметрыНоменклатуры = Новый Структура;
	ПараметрыНоменклатуры.Вставить("Период", ДатаУстановки);
	ПараметрыНоменклатуры.Вставить("ТочкаМаршрута", ТочкаМаршрута);
	ПараметрыНоменклатуры.Вставить("ВидПеревозки", ВидПеревозки);
	ПараметрыНоменклатуры.Вставить("ТипРегистрации", ТипРегистрации);
	ПараметрыНоменклатуры.Вставить("Номенклатура", НоменклатураДляУстановки());
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.гкс_НоменклатураПЛК.УстановитьНоменклатуруПоПараметрам(ПараметрыНоменклатуры);
	
КонецПроцедуры

&НаСервере
Функция НоменклатураДляУстановки() 
	
	ТекущийСписокНоменклатуры = гкс_ЭлектроннаяОчередь.РабочаяНоменклатураНаДату(
		ТипРегистрации, ВидПеревозки, ДатаУстановки);
	
	НоменклатураДляУстановки = СписокНоменклатуры.Выгрузить();
	НоменклатураДляУстановки.Колонки.Добавить("Отключено", Новый ОписаниеТипов("Булево"));
	НоменклатураДляУстановки.Колонки.Добавить("ВидЗерна", Новый ОписаниеТипов("Строка"));
	НоменклатураДляУстановки.ЗаполнитьЗначения(Ложь, "Отключено"); 
	
	Для Каждого ЭлементСтрока Из НоменклатураДляУстановки Цикл
		
		Если ЗначениеЗаполнено(ЭлементСтрока.Номенклатура.ПродуктоваяГруппа) Тогда
			ВидЗерна = гкс_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ЭлементСтрока.Номенклатура.ПродуктоваяГруппа, "Наименование");
		Иначе
			ВидЗерна = "";
		КонецЕсли;
		
		ЭлементСтрока.ВидЗерна = ВидЗерна;		
	КонецЦикла;
	
	Если ТекущийСписокНоменклатуры <> Неопределено Тогда
		Для Каждого ТекСтрока Из ТекущийСписокНоменклатуры Цикл
			Если НоменклатураДляУстановки.Найти(ТекСтрока.Номенклатура) = Неопределено Тогда
				НоваяНоменклатура = НоменклатураДляУстановки.Добавить();
				НоваяНоменклатура.Номенклатура = ТекСтрока.Номенклатура;
				НоваяНоменклатура.Отключено = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат НоменклатураДляУстановки;
	
КонецФункции

#КонецОбласти
