
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаДокументовОчереди.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДатаСеанса());
	ВременнойРазброс = Константы.гкс_ВременнойРазбросДляКПП.Получить();
	ТаблицаДокументовОчереди.Параметры.УстановитьЗначениеПараметра("ВременнойРазбросДляКПП", ВременнойРазброс * -1);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокумент(Команда)

	Оповещение = Новый ОписаниеОповещения("СоздатьДокументЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Будет создан документ Въезд на КПП-1. Продолжить?'");
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыиФункции

&НаКлиенте
Процедура ВыполнитьПослеЗаписиДокумента(РезультатЗакрытия, ДопПараметры) Экспорт
	
	Элементы.ТаблицаДокументовОчереди.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументЗавершение(Результат, ДопПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекущиеДанные = Элементы.ТаблицаДокументовОчереди.ТекущиеДанные;
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Контрагент", 					ТекущиеДанные.Контрагент);
		ПараметрыФормы.Вставить("Номенклатура", 				ТекущиеДанные.Номенклатура);
		ПараметрыФормы.Вставить("УсловиеПроезда", 				ТекущиеДанные.УсловиеПроезда);
		ПараметрыФормы.Вставить("НомерТранспортногоСредства", 	ТекущиеДанные.НомерТранспортногоСредства);
		ПараметрыФормы.Вставить("ДокументОснование", 			ТекущиеДанные.Ссылка);
	
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыполнитьПослеЗаписиДокумента", ЭтотОбъект);
		ОткрытьФорму("Документ.гкс_ВъездНаКПП.Форма.ФормаДокумента", 
						ПараметрыФормы, 
						ЭтотОбъект,,,, 
						ОповещениеОЗакрытии);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти	