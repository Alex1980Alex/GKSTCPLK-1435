#Если ТолстыйКлиентОбычноеПриложение ИЛИ Сервер ИЛИ ВнешнееСоединение Тогда
	
#Область ОписаниеПеременных

Перем МенеджерОчереди;
Перем МенеджерОбработки;
Перем ЭтоФайловаяБаза;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ОбработатьОчередь() Экспорт
	
	РезультатОчередь = МенеджерОчереди.ТекущаяОчередь();
	Если РезультатОчередь.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Очередь = РезультатОчередь.Выбрать();	
	Если ЭтоФайловаяБаза Тогда		
		ОбработатьОчередьНепосредственно(Очередь);		
	Иначе		
		ОбработатьОчередьВФоне(Очередь);		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьЭлементОчереди(Очередь) Экспорт
	
	МенеджерОбработки.ОбработатьЭлементОчереди(Очередь.ЭлементОчереди);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаОчередейСообщенийRMQ

Процедура ОбработатьОчередьНепосредственно(Очередь)
		
	Пока Очередь.Следующий() Цикл		
		ОбработатьЭлементОчереди(Очередь);
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОбработатьОчередьВФоне(Очередь) 
		
	ГруппыЭлементовОчередиДляОбработки = МенеджерОбработки.СформироватьГруппыОбработки(Очередь);	
	Для Каждого ГруппаЭлементовОчереди Из ГруппыЭлементовОчередиДляОбработки Цикл		
		ОбработатьГруппуЭлементовОчереди(ГруппаЭлементовОчереди);		
	КонецЦикла;
		
КонецПроцедуры

Процедура ОбработатьГруппуЭлементовОчереди(ГруппаЭлементовОчереди)
	
	ИмяМетода = "гкс_ОчередьСообщенийRMQСлужебный.ОбработкаЭлементаОчередиОтложенногоФормирования";
	МассивЗаданий = Новый Массив;
	
	Для Каждого ЭлементГруппыОчереди Из ГруппаЭлементовОчереди Цикл
		
		ПараметрыЗадания = Новый Массив;
		ПараметрыЗадания.Добавить(ЭлементГруппыОчереди.ЭлементОчереди);
		
		ЗаданиеОбработки = ФоновыеЗадания.Выполнить(ИмяМетода, ПараметрыЗадания); 
		
		МассивЗаданий.Добавить(ЗаданиеОбработки);
		
	КонецЦикла;
	
	Если МассивЗаданий.Количество() = 0 Тогда		
		Возврат;		
	КонецЕсли;
	
	ФоновыеЗадания.ОжидатьЗавершения(МассивЗаданий, МенеджерОбработки.ТаймаутОбработки());
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Инициализация

ЭтоФайловаяБаза = гкс_ОбщегоНазначенияКлиентСервер.ИнформационнаяБазаФайловая(); 
МенеджерОчереди = Справочники.гкс_ОчередьОтложенногоФормированияИсходящихСообщений;
МенеджерОбработки = Обработки.гкс_ОбработчикОчередиОтложенныхСообщений;

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте'");
#КонецЕсли