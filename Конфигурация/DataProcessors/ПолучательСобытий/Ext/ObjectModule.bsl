#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение Тогда

Перем Компонента;
Перем КлючПотребителя;

Перем мИнициализирован;

////////////////////////////////////////////////////////////////////////
//

// Замена конструктора
//
Процедура Инициализация(Знач КлючСоединения, Знач ЧастотаПульса)
	Компонента = ОчередьСообщений.ПолучитьЭкземплярКомпоненты(КлючСоединения, ЧастотаПульса);
	мИнициализирован = Истина;
КонецПроцедуры

// Возвращает клиент очереди сообщений для тонкой настройки канала.
//
// Возвращаемое значение:
//   ОбработкаОбъект.КлиентОчередиСообщений
//
Функция ИспользуемыйКлиент() Экспорт

	Возврат Компонента;

КонецФункции // ИспользуемыйКлиент()

// Создает очередь и привязывает ее к очереди с заданным именем.
//
Процедура ПодписатьсяНаОчередь(Знач ИмяОчереди, Знач КлючСоединения = "", Знач ЧастотаПульса = Неопределено) Экспорт

	Если Не мИнициализирован Тогда 
		Инициализация(КлючСоединения, ЧастотаПульса);
	КонецЕсли;
	
	КлючПотребителя = Компонента.НачатьЧтение(ИмяОчереди,,Ложь);

КонецПроцедуры // ПодписатьсяНаОчередь()

// Получает одно сообщение из очереди
//
Функция ПолучитьСообщение(Данные, Знач Таймаут = -1) Экспорт

	Возврат Компонента.ПолучитьСообщение(КлючПотребителя, Данные, Таймаут);
	
КонецФункции // ПолучитьСообщение()


// Сообщает серверу очередей, что обработка сообщения закончена 
// и его можно удалить из очереди.
//
Процедура СообщениеОбработано() Экспорт

	Компонента.ПодтвердитьСообщение();
	
КонецПроцедуры // СообщениеОбработано()

// Сообщает серверу очередей, что сообщение ошибочно и его надо удалить.
//
Процедура ОтклонитьСообщение(Знач ВернутьВОчередь = Ложь) Экспорт

	Компонента.ОтклонитьСообщение(ВернутьВОчередь);

КонецПроцедуры

// Запросить у компоненты установленные свойства сообщения
//
// Параметры:
//    ТребуемыеСвойства - Структура, Неопределено - Список свойств, разделенный запятыми
//		Если Неопределено, тогда возвращаются все значения свойств и в отдельном свойстве "Заголовки" возвращаются заголовки сообщения
//
Функция ПолучитьСвойстваСообщения(Знач ТребуемыеСвойства = Неопределено) Экспорт

	Возврат Компонента.ПолучитьСвойстваСообщения(ТребуемыеСвойства);

КонецФункции

////////////////////////////////////////////////////////////////////////

мИнициализирован = Ложь;

#КонецЕсли