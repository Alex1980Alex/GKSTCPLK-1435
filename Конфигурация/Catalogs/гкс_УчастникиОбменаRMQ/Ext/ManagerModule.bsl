#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СоздатьПолучитьПриложениеПоПараметрам(Параметры) Экспорт
	
	Приложение = ПолучитьПриложениеПоКоду(Параметры.Код);
	
	Если НЕ ЗначениеЗаполнено(Приложение) Тогда		
		Приложение = СоздатьПриложениеСогласованно(Параметры);			
	КонецЕсли;
	
	Возврат Приложение;
	
КонецФункции

Функция ПолучитьПриложениеПоКоду(КодПриложения) Экспорт
	
	Приложение = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоискПриложенияПоКоду();
	Запрос.УстановитьПараметр("Код", КодПриложения);	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Приложение = Выборка.Приложение;
		
	КонецЕсли;
	
	Возврат Приложение;
		
КонецФункции

Процедура УстановитьБлокировкуПоКоду(Код) Экспорт
	
	гкс_ОбщегоНазначения.УстановитьУпрБлокировкуПоЗначениюИПолю(Код, "Код", "Справочник.гкс_УчастникиОбменаRMQ"); 
	
КонецПроцедуры

Функция ТекущееПриложение(Создавать = Истина) Экспорт
	
	Перем ТекущееПриложение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаТекущееПриложение();	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() И Создавать Тогда		
		ТекущееПриложение = СоздатьТекущееПриложение();			
	Иначе
			
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();		
		ВыборкаДетальныеЗаписи.Следующий();
		
		ТекущееПриложение = ВыборкаДетальныеЗаписи.Ссылка;
								
	КонецЕсли;
	
	Возврат ТекущееПриложение;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоздатьПриложениеСогласованно(Параметры)
	
	НачатьТранзакцию();
	Попытка			
				
		УстановитьБлокировкуПоКоду(Параметры.Код);		
		// При обработке паралллельные фоновые задания могли проскочить
		Приложение = ПолучитьПриложениеПоКоду(Параметры.Код);	
		Если НЕ ЗначениеЗаполнено(Приложение) Тогда			
			Приложение = СоздатьПриложение(Параметры);			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();	
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
	
	Возврат Приложение;
	
КонецФункции

Функция СоздатьПриложение(ПараметрыПриложения)
	
	Попытка
		
		ПриложениеОбъект = Справочники.гкс_УчастникиОбменаRMQ.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(ПриложениеОбъект, ПараметрыПриложения);
		ПриложениеОбъект.Записать();
		
	Исключение	
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
	
	Возврат ПриложениеОбъект.Ссылка;
		
КонецФункции

Функция СоздатьТекущееПриложение()
	
	НачатьТранзакцию();
	Попытка
		
		гкс_ОбщегоНазначения.УстановитьУпрБлокировкуПоЗначениюИПолю(Неопределено, 
		                                                            Неопределено, 
																	"Справочник.гкс_УчастникиОбменаRMQ"); 
																	
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаТекущееПриложение();		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();		
			ВыборкаДетальныеЗаписи.Следующий();
			
			Возврат ВыборкаДетальныеЗаписи.Ссылка;
			
		КонецЕсли;
		
		ПараметрыПриложения = Новый Структура;
		ПараметрыПриложения.Вставить("ЭтаБаза", Истина);
		ПараметрыПриложения.Вставить("Наименование", Метаданные.Синоним);
		ПараметрыПриложения.Вставить("Комментарий", 
		                             Строка(ОчередьСообщенийВызовСервера.ТекущаяУниверсальнаяДатаВМиллисекундахНаСервере()));
		
		Приложение = СоздатьПриложение(ПараметрыПриложения);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();	
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
		
	Возврат Приложение;
	
КонецФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаТекущееПриложение()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	гкс_УчастникиОбменаRMQ.Ссылка КАК Ссылка,
	|	гкс_УчастникиОбменаRMQ.Код КАК Код,
	|	гкс_УчастникиОбменаRMQ.Наименование КАК Наименование
	|ИЗ
	|	Справочник.гкс_УчастникиОбменаRMQ КАК гкс_УчастникиОбменаRMQ
	|ГДЕ
	|	гкс_УчастникиОбменаRMQ.ЭтаБаза";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоискПриложенияПоКоду()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	гкс_УчастникиОбменаRMQ.Ссылка КАК Приложение
	|ИЗ
	|	Справочник.гкс_УчастникиОбменаRMQ КАК гкс_УчастникиОбменаRMQ
	|ГДЕ
	|	гкс_УчастникиОбменаRMQ.Код = &Код";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
	
#КонецЕсли