#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ИмяОбъектаМетаданных, АдресНастроек; 
	
	Параметры.Свойство("ИмяОбъектаМетаданных", ИмяОбъектаМетаданных);
	
	Если НЕ ЗначениеЗаполнено(ИмяОбъектаМетаданных) Тогда
		
		Отказ = Истина;
		ТекстИнформирования = НСтр("ru='Не задано имя объекта для настройки отбора'");
		гкс_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстИнформирования);
		Возврат;
		
	КонецЕсли;
	
	Параметры.Свойство("НастройкиСКД", АдресНастроек); 
	
	ЗаполнитьНастройки(ИмяОбъектаМетаданных, АдресНастроек);	 
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИДВладельца = Новый УникальныйИдентификатор;
	
	//@skip-warning
	Если ТипЗнч(ВладелецФормы) = Тип("УправляемаяФорма") Тогда		
		ИДВладельца = ВладелецФормы["УникальныйИдентификатор"];	
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	АдресНастроек = ПолучитьАдресНастроек();
	
	Результат = Новый Структура;
	
	Если ЗначениеЗаполнено(АдресНастроек) Тогда
		Результат.Вставить("АдресНастроек", АдресНастроек);
	Иначе
		Результат.Вставить("ОчиститьНастройки", Истина);
	КонецЕсли;
	
	ЭтотОбъект.Закрыть(Результат);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьАдресНастроек()
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	Если Настройки.Отбор.Элементы.Количество() = 0 Тогда 
		Возврат Неопределено;		
	КонецЕсли;
	
	ПомещаемоеЗначение = Новый ХранилищеЗначения(Настройки);

	АдресНастроек = ПоместитьВоВременноеХранилище(ПомещаемоеЗначение, ИДВладельца); 
	
	Возврат АдресНастроек;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьНастройки(ИмяОбъектаМетаданных, АдресНастроек)	 
	
	Менеджер = гкс_ОчередьСообщенийRMQСлужебный;
	СхемаКомпоновки = Менеджер.СформироватьСхемуКомпоновкиПоТипуОбъекта(ИмяОбъектаМетаданных);
	
	АдресСхемы = ПоместитьВоВременноеХранилище(СхемаКомпоновки, ЭтотОбъект.УникальныйИдентификатор);	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы);
	
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	
	СохраненныеНастройки = СхемаКомпоновки.НастройкиПоУмолчанию;
	
	Если ЭтоАдресВременногоХранилища(АдресНастроек) Тогда
		
		ДанныеХранилища = ПолучитьИзВременногоХранилища(АдресНастроек);
		
		Если ТипЗнч(ДанныеХранилища) = Тип("ХранилищеЗначения") Тогда
			
			СохраненныеНастройки = ДанныеХранилища.Получить();	
		
		КонецЕсли;
				
	КонецЕсли;
	
	КомпоновщикНастроек.ЗагрузитьНастройки(СохраненныеНастройки);
	
КонецПроцедуры

#КонецОбласти
 
