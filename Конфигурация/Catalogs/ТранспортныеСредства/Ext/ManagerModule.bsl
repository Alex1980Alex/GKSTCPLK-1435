
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция НормироватьНомер(Знач НомерТС) Экспорт
	
	Возврат гкс_СтроковыеФункцииКлиентСервер.НормироватьНомер(НомерТС);
		
КонецФункции

// Формирует строку для поиска транспортного средства по гос.номеру
// без учета региона, разделитель прямой слэш
// Шесть символов гос.номера + / + шесть символов прицепа
// в файле гос номер может быть задан как с регионом, так и без.
// то же самое касается прицепа
// Например С960ТХ 750 / АО2953 39
// Поиск выполняем без учета региона
//
// Параметры:
//  Строка  - Строка - текст строки гос.номера
//
// Возвращаемое значение:
//   Строка   - строка шаблона ХХХХХХ/ХХХХХХ
//
Функция ПолучитьГосНомерДляПоискаПоСтроке(Знач Строка) Экспорт
	
	Если ПустаяСтрока(Строка) Тогда
		Возврат Строка;
	КонецЕсли;
	
	Строка = СтрЗаменить(Строка, " ", "");
	
	Шаблон1 = Лев(Строка, 6);
	Шаблон2 = "";
	
	Строка = СтрЗаменить(Строка, "\", "/");
	
	ПозицияРазделителя = СтрНайти(Строка, "/");
	
	Если ПозицияРазделителя > 0 Тогда
		Шаблон2 = Сред(Строка, ПозицияРазделителя + 1, 6);
	КонецЕсли;
	
	Результат = СокрЛП(Шаблон1) + "/" + СокрЛП(Шаблон2);	
	
	Возврат Результат;
	
КонецФункции

Функция ТранспортноеСредствоПоНаименованию(Наименование) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаТранспортноеСредствоПоНаименованию();	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();                 	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.Ссылка;	
		
КонецФункции 

Функция ТранспортноеСредствоПоНомеру(Номер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаТранспортноеСредствоПоНомеру();	
	Запрос.УстановитьПараметр("Номер", Номер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();                 	
	ВыборкаДетальныеЗаписи.Следующий();
	
	Возврат ВыборкаДетальныеЗаписи.ТранспортноеСредство;	
		
КонецФункции

// Производит поиск транспортного средства по части номера
//
// Параметры:
//  НомерТранспортногоСредства - Строка - номер транспорта
//
//  КоличествоСимволовСравнения - Число - количество символов для поиска
// 
// Возвращаемое значение:
//  СправочникСсылка.ТранспортныеСредства - найденный элемент справочника или ПустаяСсылка
//
Функция ТранспортноеСредствоПоЧастиНомера(НомерТранспортногоСредства, КоличествоСимволовСравнения) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТранспортныеСредства.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
		|ГДЕ
		|	ТранспортныеСредства.НомерТранспортногоСредства ПОДОБНО &ПолеПоискаГосНомер";
	
	Запрос.УстановитьПараметр("ПолеПоискаГосНомер", 
		гкс_ПриемкаТранспорта.ПолеПоискаГосНомер(НомерТранспортногоСредства, КоличествоСимволовСравнения));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	Иначе
		Возврат Справочники.ТранспортныеСредства.ПустаяСсылка();
	КонецЕсли;	
	
КонецФункции


Функция НайтиПоПолюПоискаГосНомер(ГосНомер) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПоискТранспортногоСредстваПоПолюПоискаГосномер();
	
	Запрос.УстановитьПараметр("ПолеПоиска", ГосНомер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ТранспортноеСредство;
	
КонецФункции

#Область ПоискТранспортныхСредств

// Выполняет поиск транспортного средства по гос.номеру
// Источником является файл Word, номера могут быть записаны произвольно
//
// Параметры:
//  ТаблицаНомеров  - ТаблицаЗначений - таблица с гос.номером 
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица с подобранными ТС.
//
Функция ПодобратьТранспортноеСредствоПоГосНомеру(ТаблицаНомеров) Экспорт
	
	// гос номер может быть задан как с регионом, так и без.
	// то же самое касается прицепа
	// Например С960ТХ 750 / АО2953 39
	// Поиск выполняем без учета региона
	
	Если ТаблицаНомеров.Колонки.Найти("ГосНомерШаблон") = Неопределено Тогда
		КС13 = Новый КвалификаторыСтроки(13);
		ТаблицаНомеров.Колонки.Добавить("ГосНомерШаблон", Новый ОписаниеТипов("Строка", , КС13));
		Для Каждого СтрокаТаблицы Из ТаблицаНомеров Цикл
			СтрокаТаблицы.ГосНомерШаблон = ПолучитьГосНомерДляПоискаПоСтроке(СтрокаТаблицы.ГосНомер);
		КонецЦикла;
	КонецЕсли;
	
	// Выполним поиск. 
	// В первую очередь проверим на полное совпадение, там где не найдено будет выполнять поиск по шаблону.
	
	Запрос = Новый Запрос; 		
	Запрос.УстановитьПараметр("ТаблицаНомеров", ТаблицаНомеров);
	
	Запрос.Текст = ТекстЗапросаПоискТранспортныхСредствПоНомерам();
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции 

// Выполняет поиск транспортного средства по гос.номеру
// Если не находит, создает новое
//
// Параметры:
//  Строка  - Строка - текст строки гос.номера
//
// Возвращаемое значение:
//   СправочникСсылка.ТранспортныеСредства   - Транспортное средство
//
Функция НайтиСоздатьТСПоГосНомеруПолеПоиска(ГосНомер, ПараметрыЗаполнения = Неопределено) Экспорт
	
	ТС = Справочники.ТранспортныеСредства.ПустаяСсылка();
	
	Если ПустаяСтрока(ГосНомер) Тогда
		Возврат ТС;
	КонецЕсли;
	
	ГосНомерПолеПоиска = ПолучитьГосНомерДляПоискаПоСтроке(ГосНомер);
		
	ТС = НайтиПоПолюПоискаГосНомер(ГосНомерПолеПоиска);
	Если ЗначениеЗаполнено(ТС) Тогда
		Возврат ТС;
	КонецЕсли;
	
	НачатьТранзакцию();	
	Попытка
		
		УстановитьБлокировкуПоПолюПоискаТС(ГосНомерПолеПоиска);
		ТС = НайтиПоПолюПоискаГосНомер(ГосНомерПолеПоиска);		
		Если НЕ ЗначениеЗаполнено(ТС) Тогда
			ТС = СоздатьПоГосНомеру(ГосНомер, ПараметрыЗаполнения);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ЗаписатьИсключениеВЖурналРегистрации(ПодробноеПредставлениеОшибки);
		
		ВызватьИсключение КраткоеПредставлениеОшибки;
		
	КонецПопытки;

	Возврат ТС;
	
КонецФункции 

Функция НайтиСоздатьТСПоГосНомеру(Знач ГосНомер, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ГосНомер) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ГосНомер = СокрЛП(ГосНомер);
	ГосНомер = СтрЗаменить(ГосНомер, " ", "");
	
	ТС = ТранспортноеСредствоПоНомеру(ГосНомер); 
	Если ЗначениеЗаполнено(ТС) Тогда
		Возврат ТС;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		УстановитьБлокировкуПоНомеруТС(ГосНомер);
		ТС = ТранспортноеСредствоПоНомеру(ГосНомер); 
		Если ЗначениеЗаполнено(ТС) Тогда
			Возврат ТС;
		КонецЕсли;
		
		ТС = СоздатьПоГосНомеру(ГосНомер, ПараметрыЗаполнения);
		
		ЗафиксироватьТранзакцию();
	Исключение
  		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		
		ЗаписатьИсключениеВЖурналРегистрации(ПодробноеПредставлениеОшибки);
		
		ВызватьИсключение КраткоеПредставлениеОшибки;
		
	КонецПопытки;
	
	Возврат ТС;
		
КонецФункции

#КонецОбласти
		
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	МодульЛокализации = гкс_ЛокализацияСервер.МодульЛокализации("ТранспортныеСредства");
	Если МодульЛокализации <> Неопределено Тогда
		МодульЛокализации.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	МодульЛокализации = гкс_ЛокализацияСервер.МодульЛокализации("ТранспортныеСредства");
	Если МодульЛокализации <> Неопределено Тогда
		МодульЛокализации.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьИсключениеВЖурналРегистрации(ТекстОшибки)
	
	ЗаписьЖурналаРегистрации("ТранспортныеСредства.Создание",
	                         УровеньЖурналаРегистрации.Ошибка,
							 Метаданные.Справочники.ТранспортныеСредства,
							 Неопределено,
							 ТекстОшибки);
							 
КонецПроцедуры

#Область СозданиеТранспортныхСредств

Процедура УстановитьБлокировкуПоПолюПоискаТС(ПолеПоиска)
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ТранспортныеСредства");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ПолеПоискаГосНомер", ПолеПоиска);
	
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура УстановитьБлокировкуПоНомеруТС(НомерТС)
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("Справочник.ТранспортныеСредства");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("НомерТранспортногоСредства", НомерТС);
	
	Блокировка.Заблокировать();
	
КонецПроцедуры

// Создает новое транспортное средство
//
// Параметры:
//  ПараметрыСоздания  - Структура - параметры создания
//
// Возвращаемое значение:
//   СправочникСсылка.ТранспортныеСредства   - Транспортное средство
//
Функция СоздатьПоГосНомеру(ГосНомер, ПараметрыЗаполнения = Неопределено)
	
	ТранспортноеСредство = СоздатьЭлемент();
	
	ТранспортноеСредство.Заполнить(ПараметрыЗаполнения);
	
	ТранспортноеСредство.Наименование = ГосНомер;
	ТранспортноеСредство.НомерТранспортногоСредства = ГосНомер;
	ТранспортноеСредство.Записать();
	
	Возврат ТранспортноеСредство.Ссылка;
	
КонецФункции

#КонецОбласти

#Область ТекстыЗапросов

Функция ТекстЗапросаПоискТранспортногоСредстваПоПолюПоискаГосномер()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВсеТранспортныеСредства.Ссылка КАК ТранспортноеСредство
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ВсеТранспортныеСредства
	|ГДЕ
	|	ВсеТранспортныеСредства.ПолеПоискаГосНомер = &ПолеПоиска";
		
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоискТранспортныхСредствПоНомерам()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаНомеров.ГосНомер КАК ГосНомер,
	|	ТаблицаНомеров.ГосНомерШаблон КАК ГосНомерШаблон
	|ПОМЕСТИТЬ ТаблицаНомеровИсходная
	|ИЗ
	|	&ТаблицаНомеров КАК ТаблицаНомеров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаНомеровИсходная.ГосНомер) КАК ГосНомер,
	|	ТаблицаНомеровИсходная.ГосНомерШаблон КАК ГосНомерШаблон
	|ПОМЕСТИТЬ ТаблицаНомеров
	|ИЗ
	|	ТаблицаНомеровИсходная КАК ТаблицаНомеровИсходная
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНомеровИсходная.ГосНомерШаблон
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНомеров.ГосНомер КАК ГосНомер,
	|	МАКСИМУМ(ЕСТЬNULL(ТранспортныеСредства.Ссылка, ЕСТЬNULL(ТранспортныеСредстваПоШаблону.Ссылка, ЗНАЧЕНИЕ(Справочник.ТранспортныеСредства.ПустаяСсылка)))) КАК ТранспортноеСредство
	|ИЗ
	|	ТаблицаНомеров КАК ТаблицаНомеров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|		ПО ТаблицаНомеров.ГосНомер = ТранспортныеСредства.Наименование
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТранспортныеСредства КАК ТранспортныеСредстваПоШаблону
	|		ПО (ТранспортныеСредстваПоШаблону.ПолеПоискаГосНомер = ТаблицаНомеров.ГосНомерШаблон)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНомеров.ГосНомер
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГосНомер";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТранспортноеСредствоПоНаименованию()
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТранспортныеСредства.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ТранспортныеСредства
	|ГДЕ
	|	ТранспортныеСредства.Наименование = &Наименование";
	
	Возврат ТекстЗапроса;	
	
КонецФункции    

Функция ТекстЗапросаТранспортноеСредствоПоНомеру()
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаТС.Ссылка КАК ТранспортноеСредство
	|ИЗ
	|	Справочник.ТранспортныеСредства КАК ТаблицаТС
	|ГДЕ
	|	ТаблицаТС.НомерТранспортногоСредства = &Номер";
	
	Возврат ТекстЗапроса;	
	
КонецФункции  

#КонецОбласти

#КонецОбласти

#КонецЕсли







