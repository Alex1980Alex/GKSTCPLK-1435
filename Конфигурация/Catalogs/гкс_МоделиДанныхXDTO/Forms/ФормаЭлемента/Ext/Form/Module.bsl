
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МодельВСтроке = ТекущийОбъект.Содержимое.Получить();
	Размер = РазмерСтрокой(СтрДлина(МодельВСтроке));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
	ТекстСообщения = НСтр("ru='Настройка модели возможна только в тонком или толстом клиенте'");
	гкс_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);	
	Отказ = Истина;
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Содержимое = Новый ХранилищеЗначения(МодельВСтроке, Новый СжатиеДанных(9));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьФормировательМодели(Команда)
	
	ОбратныйВызов = Новый ОписаниеОповещения("ОткрытьФормировательМоделиЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.гкс_ФормировательМоделиXDTO.Форма.Форма",
	             Неопределено,
	             ЭтотОбъект,
	             ЭтотОбъект.УникальныйИдентификатор,
	             Неопределено,
	             Неопределено,
	             ОбратныйВызов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьМодель(Команда)
	
	Элементы.Модель.Видимость = Истина;
	Элементы.ПоказатьМодель.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьМодель(Команда)
	
	ОбратныйВызов = Новый ОписаниеОповещения("ЗагрузитьМодельЗавершение", ЭтотОбъект);
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите модель'");
	ДиалогОткрытияФайла.Фильтр = "XML Document (*.xml)";
	
	ДиалогОткрытияФайла.Показать(ОбратныйВызов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормировательМоделиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	МодельВСтроке = Результат;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РазмерСтрокой(Размер)
    
    Порог10Килобайт = 10240;
	Порог10Мегабайт = 10485760; // 10*1024*1024
	Порог10Гигабайт = 10737418240; // 10*1024*1024*1024

	ДелительКб = 1024;
	ДелительМб = 1048576; // 1024*1024
	ДелительГб = 1073741824; // 1024*1024*1024 

	ШаблонРазмера = "%1%2";
	ПредставлениеРазмераСтрокой = Строка(Размер);

	Если Размер >= Порог10Килобайт Тогда
		ПредставлениеРазмераСтрокой = гкс_СтроковыеФункцииКлиентСервер
		                                   .ПодставитьПараметрыВСтроку(ШаблонРазмера, Цел(Размер / ДелительКб), " КB");
	ИначеЕсли Размер >= Порог10Мегабайт Тогда
		ПредставлениеРазмераСтрокой = гкс_СтроковыеФункцииКлиентСервер
		                                    .ПодставитьПараметрыВСтроку(ШаблонРазмера, Цел(Размер / ДелительМб), " MB");
	ИначеЕсли Размер >= Порог10Гигабайт Тогда
		ПредставлениеРазмераСтрокой = гкс_СтроковыеФункцииКлиентСервер
		                                    .ПодставитьПараметрыВСтроку(ШаблонРазмера, Цел(Размер / ДелительГб), " GB");
	Иначе
		ПредставлениеРазмераСтрокой = гкс_СтроковыеФункцииКлиентСервер
		                                    .ПодставитьПараметрыВСтроку(ШаблонРазмера, Размер, " B");
	КонецЕсли;

	Возврат ПредставлениеРазмераСтрокой;

КонецФункции

&НаКлиенте
Процедура ЗагрузитьМодельЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	#Если НЕ ВебКлиент Тогда
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Читатор = Новый ЧтениеТекста(ВыбранныеФайлы[0], КодировкаТекста.UTF8);
	МодельВСтроке = Читатор.Прочитать();
	
	Размер = РазмерСтрокой(СтрДлина(МодельВСтроке));
	
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти
