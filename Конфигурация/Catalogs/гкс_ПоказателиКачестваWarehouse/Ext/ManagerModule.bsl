#Если Сервер ИЛИ ТолстыйКлиентОбычноеПриложение ИЛИ ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Создает новый элемент справочника, заполняет по переданному параметру, записывает.
//
// Параметры:
//  ДанныеЗаполнения - см. ИнициализироватьСтруктураНовогоЭлемента()
//
Функция НовыйЭлемент(ДанныеЗаполнения) Экспорт

	ПоказательКачества = СоздатьЭлемент();
	ПоказательКачества.Заполнить(ДанныеЗаполнения);

	Попытка			
		ПоказательКачества.Записать();		
	Исключение
		Комментарий = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации("ИнтеграцияWarehouse.ПолучитьКачественныеПоказатели",
		                         УровеньЖурналаРегистрации.Ошибка, 
		                         Метаданные.Справочники.гкс_ПоказателиКачестваWarehouse, 
		                         Неопределено, 
		                         Комментарий);
								 
		ИсключениеОшибкаСозданияНовогоЭлемента(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат ПоказательКачества.Ссылка;
			
КонецФункции

// Ищет элемент справочника по реквизиту "ИдентификаторWarehouse".
//
// Параметры:
//  Идентификатор - Строка - идентификатор Warehouse
//
Функция ПоказательПоИдентификатору(Идентификатор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторWarehouse", Идентификатор);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПоказателиКачестваWarehouse.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.гкс_ПоказателиКачестваWarehouse КАК ПоказателиКачестваWarehouse
		|ГДЕ
		|	ПоказателиКачестваWarehouse.ИдентификаторWarehouse = &ИдентификаторWarehouse";
	
	РезультатЗапроса 	= Запрос.Выполнить();
	Выборка 			= РезультатЗапроса.Выбрать();
	
	ПоказательСсылка 	= ПустаяСсылка();
	
	Если Выборка.Следующий() Тогда
		ПоказательСсылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат ПоказательСсылка;
			
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру элемента справочника.
//
// Возвращаемое значение:
//  Структура - содержит все реквизиты справочника:
// 		* Наименование - Строка - имя показателя
// 		* НаименованиеКороткое - Строка - имя показателя
// 		* ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения -
// 		* Стандарт - Строка - способ измерения
// 		* Точность - Число - точность измерения
// 		* ЗависимыйПоказатель - СправочникСсылка.гкс_ПоказателиКачестваWarehouse - связанный показатель
// 		* ВерхнийПределЗависимогоПоказателя - Число - пределы
// 		* НижнийПределЗависимогоПоказателя - Число - пределы
// 		* КодWarehouse - Строка - код справочного элемента в веб-сервесе
// 		* ИдентификаторWarehouse - Строка - УИД в веб-сервесе
//
Функция ИнициализироватьСтруктураНовогоЭлемента() Экспорт
	
	ПустаяЕдИзмерения	= Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Наименование", 						"");
	СтруктураЗаполнения.Вставить("НаименованиеКороткое", 				"");
	СтруктураЗаполнения.Вставить("ЕдиницаИзмерения", 					ПустаяЕдИзмерения);
	СтруктураЗаполнения.Вставить("Стандарт", 							"");
	СтруктураЗаполнения.Вставить("Точность", 							0);
	СтруктураЗаполнения.Вставить("ЗависимыйПоказатель", 				ПустаяСсылка());
	СтруктураЗаполнения.Вставить("ВерхнийПределЗависимогоПоказателя", 	0);
	СтруктураЗаполнения.Вставить("НижнийПределЗависимогоПоказателя", 	0);
	СтруктураЗаполнения.Вставить("КодWarehouse", 						"");
	СтруктураЗаполнения.Вставить("ИдентификаторWarehouse", 				"");
	
	Возврат СтруктураЗаполнения;

КонецФункции

Процедура ИсключениеОшибкаСозданияНовогоЭлемента(ПодробноеПредставлениеОшибки)
	
	ТекстОшибки = 
	    СтрШаблон(НСтр("ru = 'Ошибка создания нового элемента справочника ""Показатели качества Warehouse"".%1%2'"), 
	    "|",
	    ПодробноеПредставлениеОшибки);
	
	ВызватьИсключение ТекстОшибки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли