
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбновитьЗаголовокФормы();
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипЕдиницыИзмеренияПриИзменении(Элемент)
	НастроитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	НастроитьФорму();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;	         
	КонецЕсли;
	
	ОбратныйВызов = Новый ОписаниеОповещения("Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение", 
		                                         ЭтотОбъект);
		
	ОткрытьФорму("Справочник.УпаковкиЕдиницыИзмерения.Форма.РазблокированиеРеквизитовЕдиницыИзмерения",
	             Неопределено,
	             ЭтотОбъект,
	             ЭтотОбъект.УникальныйИдентификатор,
	             Неопределено, 
		         Неопределено,
		         ОбратныйВызов, 
		         РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт
       
    Если ТипЗнч(Результат) = Тип("Массив") И Результат.Количество() > 0 Тогда
        ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтотОбъект, Результат);
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	НастроитьФорму();
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	ТипыВеличин = Перечисления.ТипыИзмеряемыхВеличин;
	
	Если Объект.ТипИзмеряемойВеличины = ТипыВеличин.Длина
		Или Объект.ТипИзмеряемойВеличины = ТипыВеличин.Площадь
		Или Объект.ТипИзмеряемойВеличины = ТипыВеличин.Объем
		Или Объект.ТипИзмеряемойВеличины = ТипыВеличин.Вес
		Или Объект.ТипИзмеряемойВеличины = ТипыВеличин.Энергия
		Или Объект.ТипИзмеряемойВеличины = ТипыВеличин.ЭлектрическийЗаряд
		Или Объект.ТипИзмеряемойВеличины = ТипыВеличин.Мощность
		Или Объект.ТипИзмеряемойВеличины = ТипыВеличин.Время Тогда
			
		Элементы.ГруппаКратность.Видимость = Истина;
		ЗаголовокДекорации = НСтр("ru = '%ЕдиницаИзмерения% =';
									|en = '%ЕдиницаИзмерения% ='");
		Элементы.Декорация1.Заголовок = СтрЗаменить(ЗаголовокДекорации, "%ЕдиницаИзмерения%", Объект.Наименование);
	
	Иначе 
		
		Элементы.ГруппаКратность.Видимость = Ложь;
		Объект.Числитель = 0;
		Объект.Знаменатель = 0;		
	
	КонецЕсли;
	
	Если Объект.ТипИзмеряемойВеличины = ТипыВеличин.Длина Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'м';
													|en = 'm'");
	ИначеЕсли Объект.ТипИзмеряемойВеличины = ТипыВеличин.Площадь Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'м2';
													|en = 'm2'");
	ИначеЕсли Объект.ТипИзмеряемойВеличины = ТипыВеличин.Объем Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'м3';
													|en = 'm3'");
	ИначеЕсли Объект.ТипИзмеряемойВеличины = ТипыВеличин.Вес Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'кг';
													|en = 'kg'");
	ИначеЕсли Объект.ТипИзмеряемойВеличины = ТипыВеличин.Энергия Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'ватт-час';
													|en = 'watt-hour'");
	ИначеЕсли Объект.ТипИзмеряемойВеличины = ТипыВеличин.Мощность Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'ватт';
													|en = 'watt '");
	ИначеЕсли Объект.ТипИзмеряемойВеличины = ТипыВеличин.ЭлектрическийЗаряд Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'ампер-час';
													|en = 'ampere hour'");
	ИначеЕсли Объект.ТипИзмеряемойВеличины = ТипыВеличин.Время Тогда
		ПредставлениеБазовойЕдиницыИзмерения = НСтр("ru = 'с';
													|en = 'from'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	ПредставлениеТипа = НСтр("ru = 'Единица измерения';
							|en = 'Unit of measure'");
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (создание)';
				|en = '%1 (creation)'"),
			ПредставлениеТипа);
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2)';
				|en = '%1 (%2)'"),
			Объект.Наименование,
			ПредставлениеТипа);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти




