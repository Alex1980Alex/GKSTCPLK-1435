#Если ТолстыйКлиентОбычноеПриложение ИЛИ Сервер ИЛИ ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ПолучитьСвойство(Знач ИмяСвойства) Экспорт
	
	Перем ЗначениеСвойства;
	
	СодержимоеКакСтруктура = Справочники.гкс_ВходящиеСообщенияRMQ.СодержимоеВСтруктуру(Содержимое);
	СодержимоеКакСтруктура["СвойстваСообщения"].Свойство(ИмяСвойства, ЗначениеСвойства);
	
	Возврат ЗначениеСвойства;
	
КонецФункции

Процедура ЗаполнитьЗагруженныеОбъекты(Знач ЗагруженныеСсылки) Экспорт
	
	ЭтотОбъект.ЗагруженныеОбъекты.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ЗагруженныеСсылки) Тогда		
		Возврат;		
	КонецЕсли;
	
	МассивСсылок = Новый Массив;
	ТипЗначЗагруженныеСсылки = ТипЗнч(ЗагруженныеСсылки);
	
	Если ТипЗначЗагруженныеСсылки = Тип("Массив") Тогда
		
		МассивСсылок = ЗагруженныеСсылки;
		
	ИначеЕсли ТипЗначЗагруженныеСсылки = Тип("ТаблицаЗначений") Тогда
	
		МассивСсылок = ЗагруженныеСсылки.ВыгрузитьКолонку("Объект");
		
	КонецЕсли;
	
	Для Каждого СсылкаНаОбъект Из МассивСсылок Цикл
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект.ЗагруженныеОбъекты.Добавить(),
		Новый Структура("Объект", СсылкаНаОбъект));
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьСвойстваНовогоСообщения() Экспорт
	
	ДополнительныеСвойства.Вставить("Порядок");
	ДополнительныеСвойства.Вставить("ЗафиксироватьВОчереди", Истина);
	ДополнительныеСвойства.Вставить("Состояние");
	ДополнительныеСвойства.Вставить("ДатаИзмененияСостояния");
	ДополнительныеСвойства.Вставить("ОповеститьОбОбработке", Истина);
	ДополнительныеСвойства.Вставить("ПотокСобытий", Справочники.гкс_ПотокиСобытий.ОсновнойПотокПоУмолчанию());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда		
		Возврат;
	КонецЕсли;
	
	гкс_ОчередьСообщенийRMQ.ПередЗаписьюСообщений(ЭтотОбъект, Отказ);

	УниверсальнаяДатаВМС = ОчередьСообщенийВызовСервера.ТекущаяУниверсальнаяДатаВМиллисекундахНаСервере();
	Если НЕ ЗначениеЗаполнено(УниверсальнаяДатаСобытияВМиллисекундах) Тогда		
		УниверсальнаяДатаСобытияВМиллисекундах = УниверсальнаяДатаВМС;		
	КонецЕсли;	
	УниверсальнаяДатаИзмененияСобытияВМиллисекундах = УниверсальнаяДатаВМС;
	
	ЗаполнитьПредставлениеЗагруженныхОбъектов();

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
        
    Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;
	
	Менеджер = Справочники.гкс_ВходящиеСообщенияRMQ;

	ДополнительныеСвойства.Вставить("СсылкаНаСообщение", Ссылка);
	ДополнительныеСвойства.Вставить("КлючОбъекта", КлючОбъекта); 
	
	Менеджер.ЗафиксироватьСообщениеВОчереди(ДополнительныеСвойства);
	Менеджер.ОтметитьОбработкуСообщенияВОчереди(ДополнительныеСвойства);
	Менеджер.УстановитьСостояниеСообщения(ДополнительныеСвойства);
	Менеджер.ЗафиксироватьПроблемуОбработкиОбъекта(ДополнительныеСвойства);
	Менеджер.ОчиститьПроблемыОбъекта(ДополнительныеСвойства);
	Менеджер.СоздатьОповещениеОСтатусеОбработки(ЭтотОбъект);
	  
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда		
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);		
		Если ЭтоНовый() Тогда
			СформироватьСвойстваНовогоСообщения();
			ЗаполнитьЗначенияСвойств(ДополнительныеСвойства, ДанныеЗаполнения);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредставлениеЗагруженныхОбъектов()
	
	МассивПредставление = Новый Массив;
	
	Для Каждого ЗагруженныйОбъект Из ЗагруженныеОбъекты Цикл
		
		Представление = Строка(ЗагруженныйОбъект.Объект);
		
		МассивПредставление.Добавить(Представление);
		
	КонецЦикла;
	
	КраткийСостав = СтрСоединить(МассивПредставление, " ,");
	
КонецПроцедуры

#КонецОбласти
 
#Иначе	
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте'");
#КонецЕсли

