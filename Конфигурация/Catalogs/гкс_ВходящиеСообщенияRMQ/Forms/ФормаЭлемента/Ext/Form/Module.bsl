#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	гкс_ФормыОбъектовИАС.ПриСозданииНаСервереФормыЭлементаСообщения(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	УстановитьВидимостьИДоступностьЭлементов();
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
    
    Перем СвойстваСообщения;
	
	СодержимоеСтруктура = СодержимоеВСтруктуру(ТекущийОбъект.Содержимое);
	СодержимоеСтруктура.Свойство("Текст", ТекстСообщения);
	
	Если СодержимоеСтруктура.Свойство("СвойстваСообщения", СвойстваСообщения) 
		И ЗначениеЗаполнено(СвойстваСообщения) Тогда
		
		ТекстСвойствСообщения = ЗначениеВСтрокуВнутр(СвойстваСообщения);
		ПреобразоватьСвойстваСообщенияВДерево(СвойстваСообщения);
		
	КонецЕсли; 
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Если ручное редактирование разрешено - безусловно перезаписываем содежимое.
	Если ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений Тогда
		
		АктуализироватьСодержимоеСообщения(ТекущийОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийТаблицыФормыЗагруженныеОбъекты

&НаКлиенте
Процедура ЗагруженныеОбъектыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущаяСтрокаТаблицы = Элементы.ЗагруженныеОбъекты.ТекущиеДанные;
	
	Если НЕ ТекущаяСтрокаТаблицы = Неопределено Тогда
		
		ПоказатьЗначение(Неопределено, ТекущаяСтрокаТаблицы.Объект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПреобразоватьСвойстваСообщенияВДерево(СвойстваСообщения)
	
	ДеревоСвойств = Новый ДеревоЗначений;
	ДеревоСвойств.Колонки.Добавить("Ключ");
	ДеревоСвойств.Колонки.Добавить("Значение");
	
	Для Каждого СвойствоСообщения Из СвойстваСообщения Цикл
		
		Ветка = ДеревоСвойств.Строки.Добавить();
		ДобавитьВеткуВДерево(Ветка, СвойствоСообщения);	
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоСвойств, "ДревоСвойствСообщения");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьВеткуВДерево(Ветка, СвойствоСообщения)
	
	Ключ = СвойствоСообщения.Ключ;
	Значение = СвойствоСообщения.Значение;
	
	ТипЗначения = ТипЗнч(Значение);
	
	Ветка.Ключ = Строка(Ключ);
	
	Если ТипЗначения = Тип("Соответствие") ИЛИ ТипЗначения = Тип("Структура") Тогда
		
		Для Каждого КлючИЗначение Из Значение Цикл
			
			ДочерняяВетка = Ветка.Строки.Добавить();
			ДобавитьВеткуВДерево(ДочерняяВетка, КлючИЗначение);
			
		КонецЦикла;
		
	Иначе
		
		Ветка.Значение = Строка(Значение);	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазрешеноРучноеРедактированиеСообщений()
	
	Возврат гкс_ОчередьСообщенийRMQ.РазрешеноРучноеРедактированиеСообщений();
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьИДоступностьЭлементов()
	
	ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений = РазрешеноРучноеРедактированиеСообщений(); 
	ЭтотОбъект.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;

	ЭтотОбъект.Элементы.СтраницаТекстСообщения.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
	ЭтотОбъект.Элементы.СтраницаСвойстваСообщения.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
    ЭтотОбъект.Элементы.КлючОбъекта.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
    ЭтотОбъект.Элементы.КлючИсходящегоСообщения.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
	ЭтотОбъект.Элементы.ТипКонтракта.ТолькоПросмотр = НЕ ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
	ЭтотОбъект.Элементы.ГруппаПредупреждение.Видимость = ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
	ЭтотОбъект.Элементы.Хоп.Видимость = ЭтотОбъект.РазрешеноРучноеРедактированиеСообщений;
	
КонецПроцедуры

&НаСервере
Процедура АктуализироватьСодержимоеСообщения(ТекущийОбъект)
		
	СодержимоеСтруктура = Новый Структура;
	СодержимоеСтруктура.Вставить("Текст", ЭтотОбъект.ТекстСообщения);
	
	СвойстваСообщения = ?(ЗначениеЗаполнено(СокрЛП(ТекстСвойствСообщения)), ЗначениеИзСтрокиВнутр(ТекстСвойствСообщения),
	Новый Соответствие);
	
	СодержимоеСтруктура.Вставить("СвойстваСообщения", СвойстваСообщения);
	
	ТекущийОбъект.Содержимое = Новый ХранилищеЗначения(СодержимоеСтруктура, Новый СжатиеДанных(9));
		
КонецПроцедуры

&НаСервере
Функция СодержимоеВСтруктуру(Содержимое)
	
	Возврат Справочники.гкс_ВходящиеСообщенияRMQ.СодержимоеВСтруктуру(Содержимое);
		
КонецФункции

#КонецОбласти

