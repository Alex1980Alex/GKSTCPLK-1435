#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СформироватьДеревоОперандов(УникальныйИдентификатор) Экспорт	
	ДеревоОперандов = КонструкторФормул.ДеревоПолей();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДеревоОперандов();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОписаниеЗначения = Новый Структура("Представление, Идентификатор, ЭтоБазис");
		ЗаполнитьЗначенияСвойств(ОписаниеЗначения, Выборка);
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("Число"));
		ОписаниеЗначения.Вставить("ТипЗначения", Новый ОписаниеТипов(МассивТипов));
		ДобавитьЗначениеВДерево(ДеревоОперандов, ОписаниеЗначения);
		
		ОписаниеЗначения["ЭтоБазис"] = Истина;		
		ДобавитьЗначениеВДерево(ДеревоОперандов, ОписаниеЗначения);	
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(ДеревоОперандов, УникальныйИдентификатор);
КонецФункции

Функция СформироватьДеревоОператоров(УникальныйИдентификатор) Экспорт	
	ГруппыОператоров = "Разделители, Операторы, ЧисловыеФункции, ЛогическиеОператорыИКонстанты, ПрочиеФункции";
	Возврат ПоместитьВоВременноеХранилище(КонструкторФормул.СписокОператоров(ГруппыОператоров), УникальныйИдентификатор);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаДеревоОперандов()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВсеПоказатели.Ссылка КАК Показатель,
	|	ВсеПоказатели.гкс_ОбозначениеВФормуле КАК Идентификатор,
	|	ВсеПоказатели.Наименование КАК Представление,
	|	Ложь КАК ЭтоБазис
	|ИЗ
	|	Справочник.гкс_ПоказателиАнализовНоменклатуры КАК ВсеПоказатели
	|ГДЕ
	|	ВсеПоказатели.гкс_ОбозначениеВФормуле <> """"
	|	И НЕ ВсеПоказатели.ПометкаУдаления";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Процедура ДобавитьЗначениеВДерево(ДеревоОперандов, ОписаниеЗначения)
	
	НоваяЗапись = ДеревоОперандов.Строки.Добавить();	
	ЗаполнитьЗначенияСвойств(НоваяЗапись, ОписаниеЗначения);
	
	Если ОписаниеЗначения.ЭтоБазис Тогда		
		НоваяЗапись.Идентификатор = НоваяЗапись.Идентификатор + "_Базис";
		НоваяЗапись.Представление = НоваяЗапись.Представление + " (Базис)";	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли