
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	 ЗаполнитьВспомогательныеДанныеДляКонструктораФормул();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСкидки

&НаКлиенте
Процедура СкидкиФормулаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ИмяТаблицы", "Скидки");
  	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииРедактированияФормулы", ЭтотОбъект, ПараметрыОповещения);
  	
  	КонструкторФормулКлиент.НачатьРедактированиеФормулы(ПараметрыФормулы(Элемент.ТекстРедактирования), ОписаниеОповещения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиФормулаОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ИмяТаблицы", "Услуги");
  	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииРедактированияФормулы", ЭтотОбъект, ПараметрыОповещения);
  	
  	КонструкторФормулКлиент.НачатьРедактированиеФормулы(ПараметрыФормулы(Элемент.ТекстРедактирования), ОписаниеОповещения);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьВспомогательныеДанныеДляКонструктораФормул()
	
	АдресХранилищаДереваОперандов = СформироватьДеревоОперандов();
	АдресХранилищаДереваОператоров = СформироватьДеревоОператоров(); 
	
КонецПроцедуры
  
&НаСервере
Функция СформироватьДеревоОперандов()
	
	МенеджерОбъекта = Справочники.гкс_АлгоритмыРасчетаПоказателейКачестваНоменклатуры; 
	Возврат МенеджерОбъекта.СформироватьДеревоОперандов(УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция СформироватьДеревоОператоров()
	
	МенеджерОбъекта = Справочники.гкс_АлгоритмыРасчетаПоказателейКачестваНоменклатуры; 
	Возврат МенеджерОбъекта.СформироватьДеревоОператоров(УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Функция ПараметрыФормулы(ФормулаСтрока)
	СтруктураПараметров = КонструкторФормулКлиент.ПараметрыРедактированияФормулы();
	СтруктураПараметров.Формула		= ФормулаСтрока;
	СтруктураПараметров.Операнды	= АдресХранилищаДереваОперандов;
	СтруктураПараметров.Операторы	= АдресХранилищаДереваОператоров;
	
	Возврат СтруктураПараметров;
КонецФункции

#Область ОбратныеВызовы

&НаКлиенте
Процедура ПриЗавершенииРедактированияФормулы(ОписаниеФормулы, ДопПараметры) Экспорт
	Если ОписаниеФормулы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИмяТаблицыФормы = ДопПараметры["ИмяТаблицы"];
	Элементы[ИмяТаблицыФормы].ТекущиеДанные.Формула = ОписаниеФормулы.Формула;
	Элементы[ИмяТаблицыФормы].ТекущиеДанные.ФормулаПредставление = ОписаниеФормулы.ПредставлениеФормулы;
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#КонецОбласти
