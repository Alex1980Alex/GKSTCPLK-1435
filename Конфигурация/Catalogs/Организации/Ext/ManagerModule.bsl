#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
//
// Возвращаемое значение:
//	Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	РеквизитыОрганизации = Метаданные.Справочники.Организации.Реквизиты;
	
	Для Каждого РеквизитСправочника Из РеквизитыОрганизации Цикл
		Если СтрНайти(РеквизитСправочника.Имя, "ЗонаПогрузки") = 0 Тогда
			Результат.Добавить(РеквизитСправочника.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает организацию по контрагенту
//
// Параметры:
//  КонтрагентОрганизации  - СправочникСсылка.Контрагенты - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   СправочникСсылка.Организации   - ссылка на организацию
//
Функция ОрганизацияКонтрагента(КонтрагентОрганизации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Организации.Ссылка КАК ОрганизацияСсылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.КонтрагентОрганизации = &КонтрагентОрганизации";
	
	Запрос.УстановитьПараметр("КонтрагентОрганизации", КонтрагентОрганизации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Результат = ВыборкаДетальныеЗаписи.ОрганизацияСсылка;
	Иначе
		Результат = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли